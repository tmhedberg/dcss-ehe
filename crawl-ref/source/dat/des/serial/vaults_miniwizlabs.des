#
#  vaults_miniwizlabs.des
#
#  A Vaults serial vault of rooms with wizards.  The style is
#    from the Halls of the Hellbinder wizlab.
#
#  The following rooms are chosen in layout_vaults.des:
#    -> 1-3 '4's
#    -> 1   '7's
#

NAME:     vaults_room_4_spin_miniwizlab_octagon
TAGS:     vaults_room_4_spin_miniwizlab allow_dup luniq
TAGS:     no_rotate no_hmirror no_vmirror
NSUBST:   1 = 1:1 / *:.
KFEAT:    nopqrstu = floor
COLOUR:   nopqrstu = yellow
COLOUR:   12. = red
FTILE:    n = sigil_straight_e_sw
FTILE:    o = sigil_straight_w_se
FTILE:    p = sigil_straight_s_nw
FTILE:    q = sigil_straight_n_sw
FTILE:    r = sigil_straight_w_ne
FTILE:    s = sigil_straight_e_nw
FTILE:    t = sigil_straight_n_se
FTILE:    u = sigil_straight_s_ne
FTILE:    12. = floor_pebble_lightred
KMONS:    1 = wizard
MAP
.no.
u11p
t11q
.sr.
ENDMAP

NAME:     vaults_room_4_spin_miniwizlab_statues
TAGS:     vaults_room_4_spin_miniwizlab allow_dup luniq
TAGS:     no_rotate no_hmirror no_vmirror
KFEAT:    pqrs = floor
COLOUR:   pqrs = yellow
COLOUR:   . = red
FTILE:    p = sigil_curve_s_e
FTILE:    q = sigil_curve_s_w
FTILE:    r = sigil_curve_n_w
FTILE:    s = sigil_curve_n_e
FTILE:    G. = floor_pebble_lightred
KMONS:    p = wizard
MAP
G..G
.pq.
.sr.
G..G
ENDMAP

NAME:     vaults_room_4_spin_miniwizlab_Ys
TAGS:     vaults_room_4_spin_miniwizlab allow_dup luniq
TAGS:     no_rotate no_hmirror no_vmirror
NSUBST:   1 = 1:1 / *:.
KFEAT:    v^<> = floor
COLOUR:   v^<> = yellow
COLOUR:   12. = red
FTILE:    v = sigil_y
FTILE:    ^ = sigil_y_inverted
FTILE:    > = sigil_y_left
FTILE:    < = sigil_y_right
FTILE:    12. = floor_pebble_lightred
KMONS:    1 = wizard
MAP
.^^.
<11>
<11>
.vv.
ENDMAP

NAME:     vaults_room_4_spin_miniwizlab_round_rectangle
TAGS:     vaults_room_4_spin_miniwizlab allow_dup luniq
TAGS:     no_rotate no_hmirror no_vmirror
NSUBST:   1 = 1:1 / *:.
KFEAT:    |-pqrs = floor
COLOUR:   |-pqrs = yellow
COLOUR:   12. = red
FTILE:    | = sigil_straight_n_s
FTILE:    - = sigil_straight_e_w
FTILE:    p = sigil_curve_s_e
FTILE:    q = sigil_curve_s_w
FTILE:    r = sigil_curve_n_w
FTILE:    s = sigil_curve_n_e
FTILE:    12. = floor_pebble_lightred
KMONS:    1 = wizard
MAP
p--q
|11|
|11|
s--r
ENDMAP

NAME:     vaults_room_4_spin_miniwizlab_rhombus
TAGS:     vaults_room_4_spin_miniwizlab allow_dup luniq
TAGS:     no_rotate no_hmirror no_vmirror
NSUBST:   1 = 1:1 / *:.
KFEAT:    /& = floor
COLOUR:   /& = yellow
COLOUR:   12. = red
FTILE:    / = sigil_straight_ne_sw
FTILE:    & = sigil_straight_nw_se
FTILE:    12. = floor_pebble_lightred
KMONS:    1 = wizard
MAP
./&.
/11&
&11/
.&/.
ENDMAP

NAME:     vaults_room_7_spin_miniwizlab_Ys
TAGS:     vaults_room_7_spin_miniwizlab allow_dup luniq
TAGS:     no_rotate no_hmirror no_vmirror
SHUFFLE:  23
SUBST:    34 = .
SUBST:    2 : 234.
KFEAT:    /&v^<>o = floor
COLOUR:   /&v^<>o = yellow
COLOUR:   1234. = red
FTILE:    / = sigil_straight_ne_sw
FTILE:    & = sigil_straight_nw_se
FTILE:    v = sigil_y
FTILE:    ^ = sigil_y_inverted
FTILE:    > = sigil_y_left
FTILE:    < = sigil_y_right
FTILE:    1234. = floor_pebble_lightred
KMONS:    1 = wizard
KMONS:    2 : chaos spawn / ice devil / neqoxec /\
            smoke demon / soul eater / sun demon / ynoxinul
KMONS:    3 = small abomination / large abomination w:5 / nothing w:20
KMONS:    4 : fire bat
MAP
..&./..
.3.v.3.
&.2^2./
.><1><.
/.2v2.&
.3.^.3.
../.&..
ENDMAP

NAME:     vaults_room_7_spin_miniwizlab_big_octagon
TAGS:     vaults_room_7_spin_miniwizlab allow_dup luniq
TAGS:     no_rotate no_hmirror no_vmirror
SHUFFLE:  23
SUBST:    3 = .
SUBST:    2 : 234.
KFEAT:    nopqrstu|-" = floor
COLOUR:   nopqrstu|-" = yellow
COLOUR:   1234. = red
FTILE:    n = sigil_straight_e_sw
FTILE:    o = sigil_straight_w_se
FTILE:    p = sigil_straight_s_nw
FTILE:    q = sigil_straight_n_sw
FTILE:    r = sigil_straight_w_ne
FTILE:    s = sigil_straight_e_nw
FTILE:    t = sigil_straight_n_se
FTILE:    u = sigil_straight_s_ne
FTILE:    | = sigil_straight_n_s
FTILE:    - = sigil_straight_e_w
FTILE:    " = sigil_rhombus
FTILE:    1234. = floor_pebble_lightred
KMONS:    " = wizard
KMONS:    2 : chaos spawn / ice devil / neqoxec /\
            smoke demon / soul eater / sun demon / ynoxinul
KMONS:    3 = small abomination / large abomination w:5 / nothing w:20
KMONS:    4 : fire bat
MAP
.......
..n-o..
.u232p.
.|3"3|.
.t232q.
..s-r..
.......
ENDMAP

NAME:     vaults_room_7_spin_miniwizlab_diagonals
TAGS:     vaults_room_7_spin_miniwizlab allow_dup luniq
TAGS:     no_rotate no_hmirror no_vmirror
SHUFFLE:  234
SUBST:    34 = .
SUBST:    2 : 234.
KFEAT:    /&v^<> = floor
COLOUR:   /&v^<>1 = yellow
COLOUR:   234. = red
FTILE:    / = sigil_straight_ne_sw
FTILE:    & = sigil_straight_nw_se
FTILE:    v = sigil_y
FTILE:    ^ = sigil_y_inverted
FTILE:    > = sigil_y_left
FTILE:    < = sigil_y_right
FTILE:    1 = sigil_cross
FTILE:    234. = floor_pebble_lightred
KMONS:    1 = wizard
KMONS:    2 : chaos spawn / ice devil / neqoxec /\
            smoke demon / soul eater / sun demon / ynoxinul
KMONS:    3 = small abomination / large abomination w:5 / nothing w:20
KMONS:    4 : fire bat
MAP
.&.../.
&4&2/4/
.&3v3/.
.2>1<2.
./3^3&.
/4/2&4&
./...&.
ENDMAP

NAME:     vaults_room_7_spin_miniwizlab_4_circles
TAGS:     vaults_room_7_spin_miniwizlab allow_dup luniq
TAGS:     no_rotate no_hmirror no_vmirror
SHUFFLE:  234
SUBST:    34 = .
SUBST:    2 : 234.
KFEAT:    pPqQrRsS = floor
COLOUR:   pPqQrRsS = yellow
COLOUR:   1. = red
FTILE:    pP = sigil_curve_s_e
FTILE:    qQ = sigil_curve_s_w
FTILE:    rR = sigil_curve_n_w
FTILE:    sS = sigil_curve_n_e
FTILE:    1234. = floor_pebble_lightred
KMONS:    1 = wizard
KMONS:    2 : chaos spawn / ice devil / neqoxec /\
            smoke demon / soul eater / sun demon / ynoxinul
KMONS:    3 = small abomination / large abomination w:5 / nothing w:20
KMONS:    4 : fire bat
MAP
2.....2
.pq3pq.
.sR4Sr.
.34143.
.pQ4Pq.
.sr3sr.
2.....2
ENDMAP
