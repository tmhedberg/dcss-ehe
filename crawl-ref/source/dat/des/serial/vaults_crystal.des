#
#  vaults_crystal.des
#
#  A Vaults serial vault with crystal walls and crystal golems.
#
#  The following rooms are chosen in layouts_vaults.des:
#    -> 3 '10's
#

NAME:     vaults_room_10_spin_crystal_cross
TAGS:     vaults_room_10_spin_crystal allow_dup
MAP
bbb....bbb
bbb....bbb
bbb....bbb
..........
..........
..........
..........
bbb....bbb
bbb....bbb
bbb....bbb
ENDMAP

NAME:     vaults_room_10_spin_crystal_spiral
TAGS:     vaults_room_10_spin_crystal allow_dup no_monster_gen
TAGS:     no_monster_gen no_item_gen
SUBST:    1 = 1*
MONS:     crystal golem
MAP
..........
.b........
.b.bbbbbb.
.b.b....b.
.b.b.bb.b.
.b.b.1b.b.
.b.bbbb.b.
.b......b.
.bbbbbbbb.
..........
ENDMAP

NAME:     vaults_room_10_spin_crystal_corners
TAGS:     vaults_room_10_spin_crystal allow_dup
MONS:     statue tile:mons_statue_mage name:spearthrower n_adj; \
          spells:lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear
MAP
..........
.bb....bb.
.b1....Gb.
..........
..........
..........
..........
.bG....Gb.
.bb....bb.
..........
ENDMAP

NAME:     vaults_room_10_spin_crystal_double_spiral
TAGS:     vaults_room_10_spin_crystal allow_dup no_monster_gen
SHUFFLE:  1234
SUBST:    234 = .
SUBST:    1 : 12.
SUBST:    2 = 2.
MONS:     crystal golem
MONS:     statue tile:mons_statue_mage name:spearthrower n_adj; \
          spells:lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear
MAP
.........4
.bbbbbbbbb
.b2.......
.b.3bbbbb.
.b.....1b.
.b1.....b.
.bbbbb3.b.
.......2b.
bbbbbbbbb.
4.........
ENDMAP

NAME:     vaults_room_10_spin_crystal_center_box
TAGS:     vaults_room_10_spin_crystal allow_dup
MAP
..........
..........
..bbbbbb..
..bbbbbb..
..bbbbbb..
..bbbbbb..
..bbbbbb..
..bbbbbb..
..........
..........
ENDMAP

NAME:     vaults_room_10_spin_crystal_scattered
TAGS:     vaults_room_10_spin_crystal allow_dup no_monster_gen
SUBST:    b = bb.
NSUBST:   . = 1:1 / 4=1... / *:.
MONS:     crystal golem
MAP
.b.....b.b
b.b.b.b.b.
.b.b.b.b.b
..b.b.b.b.
.b.b.b.b..
..b.b.b.b.
.b.b.b.b..
b.b.b.b.b.
.b.b.b.b.b
b.b.....b.
ENDMAP

NAME:     vaults_room_10_spin_crystal_rhombus
TAGS:     vaults_room_10_spin_crystal allow_dup
MAP
bbb....bbb
bb..bb..bb
b..bbbb..b
..bbbbbb..
.bbbbbbbb.
.bbbbbbbb.
..bbbbbb..
b..bbbb..b
bb..bb..bb
bbb....bbb
ENDMAP

NAME:     vaults_room_10_spin_crystal_spear_corner
TAGS:     vaults_room_10_spin_crystal allow_dup
MONS:     statue tile:mons_statue_mage name:spearthrower n_adj; \
          spells:lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear
MAP
bbb.......
bbbbbbbbb.
bbbbbbbbb.
.bb1......
.bb.......
.bb.......
.bb.......
.bb......b
.bb.....bb
.......bbb
ENDMAP

NAME:     vaults_room_10_spin_crystal_big_corners
TAGS:     vaults_room_10_spin_crystal allow_dup
SHUFFLE:  12
SUBST:    2 : b.
MONS:     crystal golem
MAP
bbb....bbb
bbbb..bbbb
bbbb..bbbb
.bb1..1bb.
....22....
....22....
.bb1..1bb.
bbbb..bbbb
bbbb..bbbb
bbb....bb.
ENDMAP

NAME:     vaults_room_10_spin_crystal_pinwheel
TAGS:     vaults_room_10_spin_crystal allow_dup
SUBST:    1 : 12
NSUBST:   2 = 1:2 / *:bbG
MONS:     crystal golem
MONS:     statue tile:mons_statue_mage name:spearthrower n_adj; \
          spells:lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear
MAP
bbb....bbb
bbbb...bbb
bbbbbb.bbb
.....1.bb.
..b1...b..
..b...1b..
.bb.1.....
bbb.bbbbbb
bbb...bbbb
bbb....bbb
ENDMAP


NAME:     vaults_room_10_facing_crystal_wide_hallway
TAGS:     vaults_room_10_facing_crystal allow_dup
SHUFFLE:  yY / zZ
SUBST:    y = b
SUBST:    Y = u
SUBST:    zZ = .
SUBST:    u : b.
SUBVAULT: A : vaults_door_10
MAP
zzz.......
zz........
z.ybbbbbbb
..........A
...YuZ.uu.A
...YuZ.uu.A
..........A
z.ybbbbbbb
zz........
zzz.......
ENDMAP

NAME:     vaults_room_10_facing_crystal_parallel_walls
TAGS:     vaults_room_10_facing_crystal allow_dup
SHUFFLE:  yY / zZ
SUBST:    y = b
SUBST:    Y = u
SUBST:    zZ = .
SUBST:    u : b.
SUBVAULT: A : vaults_door_10
MAP
..........
.yY.yY.yY.
.bu.bu.bu.
.bu.bu.bu.A
.bu.bu.bu.A
.bu.bu.bu.A
.bu.bu.bu.A
.bu.bu.bu.
.yY.yY.yY.
..........
ENDMAP

NAME:     vaults_room_10_facing_crystal_golem_guard
TAGS:     vaults_room_10_facing_crystal allow_dup no_monster_gen
SHUFFLE:  12
SUBST:    2 : 1...
MONS:     crystal golem
SUBVAULT: A : vaults_door_10
MAP
.......bbb
.......bbb
..bb...bbb
..bb...2..A
.......1..A
.......1..A
..bb...2..A
..bb...bbb
.......bbb
.......bbb
ENDMAP

NAME:     vaults_room_10_facing_crystal_parallel_hallways
TAGS:     vaults_room_10_facing_crystal allow_dup
MAP
.......xxx
.bb.bbbxxx
.bb.bbbbbb
.bb.......+
.bbbbbbbbbx
.bbbbbbbbbx
.bb.......+
xxb.bbbbbb
xxb.bbbxxx
xxb....xxx
ENDMAP

NAME:     vaults_room_10_facing_crystal_statue_hallway
TAGS:     vaults_room_10_facing_crystal allow_dup no_monster_gen
NSUBST:   1 = 1:1 / *:GGGG1
SHUFFLE:  +-
SUBST:    - = x+++
MONS:     statue tile:mons_statue_mage name:spearthrower n_adj; \
          spells:lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear
MAP
xxx....bbb
xx....bbbb
x....bbbbb
....bbbbbbx
.G1.......+
.G1.......-
....bbbbbbx
x....bbbbb
xx....bbbb
xxx....bbb
ENDMAP


NAME:     vaults_room_10_dead_end_crystal_boxed
TAGS:     vaults_room_10_dead_end_crystal allow_dup
TAGS:     no_monster_gen no_item_gen
SHUFFLE:  +-
SUBST:    - = x++
SHUFFLE:  cC
SHUFFLE:  dD
SHUFFLE:  eE
SUBST:    cde = b
SUBST:    CDE = B
NSUBST:   B = 1:. / 2=bbb. / *:b
SUBST:    1 = *:2 %:3 .:5 1:2 0:2 9:1
MONS:     crystal golem
MAP
bbbbbbbbbb
b.......bb
b.bCcCb.bb
b.D...b...-
b.d.1.b...+
b.D...b...-
b.bEeEb.bbx
b.......bb
bbbbbbbbbb
bbbbbbbbbb
ENDMAP

NAME:     vaults_room_10_dead_end_crystal_funnel
TAGS:     vaults_room_10_dead_end_crystal allow_dup no_item_gen
NSUBST:   1 = 1:1 / *:G.
MONS:     statue tile:mons_statue_mage name:spearthrower n_adj; \
          spells:lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear
SUBVAULT: A : vaults_door_10
MAP
xxxxxxxbb1
xxxxbb....
xxxbb1....
bb........A
b1........A
|.........A
xbb1......A
xxbb......
xxxxxbb1..
xxxxxxbb..
ENDMAP

NAME:     vaults_room_10_dead_end_crystal_fire_crabs
TAGS:     vaults_room_10_dead_end_crystal allow_dup
TAGS:     no_monster_gen no_item_gen
NSUBST:   1 = 1:1 / 4=1.. / 1:d / 4=%. / *:.
MONS:     fire crab
ITEM:     wand of flame / wand of fire / wand of fireball
SUBVAULT: A : vaults_door_10
MAP
bbbbb1bbbb
b1b.....1b
b...1b..bb
b.1b.bb1..A
bb.b......A
bb.b......A
b.1b.bb1..A
b...1b..bb
b1b.....1b
bbbbb1bbbb
ENDMAP

NAME:     vaults_room_10_dead_end_crystal_simulacrums
TAGS:     vaults_room_10_dead_end_crystal allow_dup
TAGS:     no_monster_gen no_item_gen
NSUBST:   d = 1:d / 1:e / 4=e_ / *:_
NSUBST:   _ = 1:1 / 1:2 / 3=2.. / *:.
NSUBST:   + = 1:+ / *:x
MONS:     dragon simulacrum / hydra simulacrum
MONS:     wyvern simulacrum w:20 / lindwurm simulacrum w:20 /\
          swamp drake simulacrum / fire drake simulacrum /\
          death drake simulacrum /\
          steam dragon simulacrum w:5 / mottled dragon simulacrum w:5
ITEM:     any potion good_item
ITEM:     any potion / any potion q:2 w:2
MAP
dbd_dbd__d
_b_b_b_bbb
d__bd_d__.
bb_b_bb_b.+
d_d_d___b.+
b_bbb_b_b.+
b_d__db__.x
d_b_b_b_bb
b_b_b__d__
bd_dbdbbbd
ENDMAP

NAME:     vaults_room_10_dead_end_crystal_guarded_box
TAGS:     vaults_room_10_dead_end_crystal allow_dup
TAGS:     no_monster_gen no_item_gen
: if crawl.coinflip() then
:   subst("2 = .")
: else
:   subst("1 = .")
:   shuffle("23")
: end
SHUFFLE:  pqr
NSUBST:   p = 1:+ / *:x+
SUBST:    qr = x
NSUBST:   % = 2:* / 10:% / *:.
MONS:     crystal golem
MONS:     statue tile:mons_statue_mage name:spearthrower n_adj; \
          spells:lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear
MONS:     orange crystal statue
SUBVAULT: A : vaults_door_10
MAP
x....2....
x......bb.
xppxx..bb1
%%%%x..1..A
%%%%q.bb..A
%%%%q.bb..A
%%%%x..1..A
xrrxx..bb1
x......bb.
x....2....
ENDMAP

NAME:     vaults_room_10_city_crystal_solid
TAGS:     vaults_room_10_city_crystal allow_dup
SUBST:    z : b.
SUBST:    y : bb.
MAP
zzzzzzzzzzzz
zyyyyyyyyyyz
zybbbbbbbbyz
zybbbbbbbbyz
zybbbbbbbbyz
zybbbbbbbbyz
zybbbbbbbbyz
zybbbbbbbbyz
zybbbbbbbbyz
zybbbbbbbbyz
zyyyyyyyyyyz
zzzzzzzzzzzz
ENDMAP

NAME:     vaults_room_10_city_crystal_boxes
TAGS:     vaults_room_10_city_crystal allow_dup no_monster_gen
NSUBST:   1 = 1:1 / 2=1.. / *:.
MONS:     crystal golem
MAP
bbb..bb..bbb
bbb..bb..bbb
bbb..11..bbb
...111111...
...1bbbb1...
bb11bbbb11bb
bb11bbbb11bb
...1bbbb1...
...111111...
bbb..11..bbb
bbb..bb..bbb
bbb..bb..bbb
ENDMAP

NAME:     vaults_room_10_city_crystal_octathrope
TAGS:     vaults_room_10_city_crystal allow_dup
SUBST:    u : bb.
SUBST:    y : b.
SUBST:    z : bb.
SUBST:    s : r.
SUBST:    r : qq.
SUBST:    q : ppp.
SUBST:    p : bbbb.
MAP
ssss.zz.ssss
sqqr.bb.rqqs
sqpr.bb.rpqs
srrr.yy.rrrs
............
zbby.uu.ybbz
zbby.uu.ybbz
............
srrr.yy.rrrs
sqpr.bb.rpqs
sqqr.bb.rqqs
ssss.zz.ssss
ENDMAP

NAME:     vaults_room_10_city_crystal_maze
TAGS:     vaults_room_10_city_crystal allow_dup
TAGS:     no_monster_gen no_item_gen
SUBST:    1 = 11%.
MAP
bbbbb.bbbbb.
b..1b.b.....
b.bbb.b.bbb.
b.....b.b1b.
bbbbbbb.b.b.
b.....b...b.
b.bbb.bbbbb.
b.b..1b1..b.
b.b.bbbbb.b.
b.b.......b.
b.bbbbbbbbb.
............
ENDMAP

NAME:     vaults_room_10_city_crystal_rhombus
TAGS:     vaults_room_10_city_crystal allow_dup
SHUFFLE:  12
SUBST:    2 = .
SUBST:    1 = 1G
MONS:     statue tile:mons_statue_mage name:spearthrower n_adj; \
          spells:lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear;\
                 lehudib's_crystal_spear;lehudib's_crystal_spear
MAP
.....bb.....
.2..bbbb..2.
..1bbbbbb1..
..bbbbbbbb..
.bbbbbbbbbb.
bbbbbbbbbbbb
bbbbbbbbbbbb
.bbbbbbbbbb.
..bbbbbbbb..
..1bbbbbb1..
.2..bbbb..2.
.....bb.....
ENDMAP
