# The Rogue's Gallery, by Grunt.
#
# This collection of vaults showcase monsters reflective of popular choices
# for player combos while still being (relatively) interesting monsters
# in their own right.
#
# These are the top 30 winning combos in 0.10 and 0.11-a online as of
# 9 August 2012:
#
# 138x MiFi, 79x MiBe, 68x DEFE, 64x SpEn, 48x DDEE,
#  47x MiGl, 39x MuSu, 32x MiMo, 27x OgBe, 26x HOPr,
#  25x DEWz, 23x MfGl, 23x HOFi, 22x CeHu, 22x KoBe,
#  22x HOHe, 21x DDBe, 19x MfIE, 18x VpEn, 17x SEEE,
#  17x DDNe, 17x DsWz, 17x MiDK, 17x DECj, 17x HOBe,
#  17x DrTm, 17x TeAE, 17x DsBe, 16x DsFE, 16x MuWz
#
# All of these except HOHe (a He is difficult to add as a foe at time of
# writing) and DrTm (no monster transumtations)  are present; instead the
# next two places (HEWz and MfBe) are used.
#
# DDBe, DDNe, and HOPr are currently disabled as they silently redefine
# the existing monsters with those names.
#
# Currently, you are guaranteed exactly nine foes, with no two races or roles
# being the same.

default-depth: D:20-26, Elf, Crypt, !Crypt:$

NAME: serial_grunt_rogues_gallery
TAGS: extra luniq_serial
ORIENT: float
CHANCE: 1%
{{
  hook("post_place", function()
    dgn.place_maps{tag="grunt_rogue_3", count=1}
    dgn.place_maps{tag="grunt_rogue_2", count=2}
    dgn.place_maps{tag="grunt_rogue_1", count=2}
  end)
}}

NAME: grunt_rogue_3_row
TAGS: grunt_rogue_3 unrand transparent
SUBVAULT: A : grunt_rogue_subvault
SUBVAULT: B : grunt_rogue_subvault
SUBVAULT: C : grunt_rogue_subvault
SHUFFLE: de, fg, hi
SUBST: dfh = +
SUBST: egi = c
MAP
...............
.ccdcccfccchcc.
.cAAAcBBBcCCCc.
.cAAAcBBBcCCCc.
.cAAAcBBBcCCCc.
.ccecccgcccicc.
.......@.......
ENDMAP

NAME: grunt_rogue_3_hall
TAGS: grunt_rogue_3 unrand transparent
SUBVAULT: A : grunt_rogue_subvault
SUBVAULT: B : grunt_rogue_subvault
SUBVAULT: C : grunt_rogue_subvault
MAP
...............
.ccccccccccccc.
.cAAAcBBBcCCCc.
.cAAAcBBBcCCCc.
.cAAAcBBBcCCCc.
.cc+ccc+ccc+cc.
.c...........c.
.c.....T.....c.
.c...........c.
.cccccc+cccccc.
.......@.......
ENDMAP

NAME: grunt_rogue_3_triangleish
TAGS: grunt_rogue_3 unrand transparent
SUBVAULT: A : grunt_rogue_subvault
SUBVAULT: B : grunt_rogue_subvault
SUBVAULT: C : grunt_rogue_subvault
MAP
.............
....ccccc....
....cAAAc....
..G.cAAAc.G..
....cAAAc....
....cc+cc....
...cc...cc...
..c+..T..+c..
.cc.......cc.
.ccccc.ccccc.
.cBBBc.cCCCc.
.cBBB+.+CCCc.
.cBBBc.cCCCc.
.ccccc+ccccc.
.............
......G......
......@......
ENDMAP

NAME: grunt_rogue_2_row
TAGS: grunt_rogue_2 unrand transparent
SUBVAULT: A : grunt_rogue_subvault
SUBVAULT: B : grunt_rogue_subvault
SHUFFLE: de, fg
SUBST: df = +
SUBST: eg = c
MAP
...........
.ccdcccfcc.
.cAAAcBBBc.
.cAAAcBBBc.
.cAAAcBBBc.
.ccecccgcc.
.....@.....
ENDMAP

NAME: grunt_rogue_2_hall
TAGS: grunt_rogue_2 unrand transparent
SUBVAULT: A : grunt_rogue_subvault
SUBVAULT: B : grunt_rogue_subvault
SUBVAULT: C : grunt_rogue_subvault
MAP
...............
.....cc+cc.....
.....c...c.....
...G.c...c.G...
.....c...c.....
.ccccc...ccccc.
.cAAAc...cCCCc.
.cAAA+.T.+CCCc.
.cAAAc...cCCCc.
.ccccc...ccccc.
.....c...c.....
...G.c...c.G...
.....c...c.....
.....cc+cc.....
.......@.......
ENDMAP

NAME: grunt_rogue_2_box
TAGS: grunt_rogue_2 unrand transparent
SUBVAULT: A : grunt_rogue_subvault
SUBVAULT: B : grunt_rogue_subvault
SHUFFLE: de, fg
SUBST: df = +
SUBST: eg = c
MAP
...........
.ccdcccccc.
.cAAAccccc.
.eAAAccccc.
.cAAAccccc.
.ccccccccc.
.cccccBBBc.
.cccccBBBf.
.cccccBBBc.
.ccccccgcc.
.....@.....
ENDMAP

NAME: grunt_rogue_1_simple
TAGS: grunt_rogue_1 unrand transparent
SUBVAULT: A : grunt_rogue_subvault
MAP
...@...
.cc+cc.
.cAAAc.
.cAAAc.
.cAAAc.
.ccccc.
.......
ENDMAP

NAME: grunt_rogue_1_enclosed
TAGS: grunt_rogue_1 unrand transparent
SUBVAULT: A : grunt_rogue_subvault
NSUBST: d = 1:+ / *:c
NSUBST: e = 1:+ / *:c
MAP
.....@.....
.ccccdcccc.
.c.......c.
.c.ccecc.c.
.c.cAAAc.c.
.d.eAAAe.d.
.c.cAAAc.c.
.c.ccecc.c.
.c.......c.
.ccccdcccc.
...........
ENDMAP

NAME: grunt_rogue_1_cross
TAGS: grunt_rogue_1 unrand transparent
SUBVAULT: A : grunt_rogue_subvault
MAP
.........
....c....
...ccc...
..cc.cc..
.ccAAAcc.
cc.AAA.cc
.ccAAAcc.
..cc.cc..
...c+c...
....+....
....@....
ENDMAP

NAME: grunt_rogue_mifi
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_mi uniq_grunt_rogue_fi
KMONS: O = minotaur name:fighter n_suf hd:15 god:okawaru ; \
           great sword good_item | battleaxe good_item | trident good_item . \
           chain mail w:10 good_item | splint mail w:5 good_item | \
           plate armour good_item w:1
SHUFFLE: dh, ei, fj, gk
KITEM: df = any armour good_item
KITEM: eg = any weapon good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_mibe
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_mi uniq_grunt_rogue_be
KMONS: O = minotaur name:berserker n_suf hd:15 god:trog \
           spells:berserker_rage;trog's_hand;.;.;.;brothers_in_arms \
           priest_spells ; \
           great sword ego:anti-magic good_item | \
           battleaxe ego:anti-magic good_item | \
           trident ego:anti-magic good_item . \
           chain mail w:10 good_item | splint mail w:5 good_item | \
           plate armour good_item w:1
SHUFFLE: dh, ei, fj, gk
KITEM: d = any armour good_item
KITEM: e = any weapon ego:anti-magic good_item
KITEM: f = amulet of rage
KITEM: g = potion of berserk rage q:5
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_spen
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_sp uniq_grunt_rogue_en
KMONS: O = spriggan defender name:spriggan_enchanter n_rpl n_des name_nocorpse \
           spells:confuse;ensorcelled_hibernation;invisibility;haste \
           actual_spells \
           god:nemelex_xobeh ; \
           quick blade good_item . leather armour good_item
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of maledictions
KITEM: e = book of enchantments
KITEM: f = legendary deck
KITEM: g = potion of invisibility q:5
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_defe
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_de uniq_grunt_rogue_fe
KMONS: O = deep elf annihilator name:deep_elf_scorcher n_rpl n_des \
           name_nocorpse \
           spells:bolt_of_fire;fireball;blink;fire_storm;bolt_of_magma;blink \
           god:vehumet ; robe good_item . \
           quarterstaff ego:flaming good_item | \
           lajatang ego:flaming good_item w:5 | \
           staff of fire w:5
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of flames
KITEM: e = book of fire
KITEM: f = robe ego:fire_resistance good_item
KITEM: g = any weapon ego:flaming good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_ddee
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_dd uniq_grunt_rogue_ee
KMONS: O = deep dwarf col:white hd:15 name:deep_dwarf_earth_mage n_rpl n_des \
           name_nocorpse \
           spells:iron_shot;petrify;stoneskin;\
           lee's_rapid_deconstruction;shatter;blink \
           actual_spells \
           god:makhleb ; robe good_item . \
           quarterstaff good_item | \
           lajatang good_item w:5 | \
           staff of earth w:5
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of geomancy
KITEM: e = book of the earth
KITEM: f = robe good_item
KITEM: g = any weapon good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_migl
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_mi uniq_grunt_rogue_gl
KMONS: O = minotaur name:gladiator n_suf hd:15 god:okawaru ; \
           great sword good_item | battleaxe good_item | trident good_item . \
           javelin good_item q:20 . \
           throwing net q:3 . \
           leather armour w:10 good_item | ring mail w:5 good_item | \
           chain mail good_item w:1
SHUFFLE: dh, ei, fj, gk
KITEM: df = any armour good_item
KITEM: eg = any weapon good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_mimo
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_mi uniq_grunt_rogue_mo
KMONS: O = minotaur name:monk n_suf hd:15 god:okawaru ; \
           chain mail w:10 good_item | splint mail w:5 good_item | \
           plate armour good_item w:1
SHUFFLE: dh, ei, fj, gk
KITEM: defg = any armour good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_musu
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_mu uniq_grunt_rogue_su
KMONS: O = mummy priest name:mummy_summoner hd:15 col:white n_rpl n_des \
           god:sif_muna actual_spells \
           spells:summon_demon;summon_ugly_thing;summon_hydra;\
                  shadow_creatures;summon_elemental;summon_greater_demon ; \
           robe good_item . \
           quarterstaff good_item | \
           lajatang good_item w:5 | \
           staff of summoning w:5
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of callings
KITEM: e = book of summonings
KITEM: f = staff of channeling
KITEM: g = rod of demonology
MAP
d.e
.O.
f.g
ENDMAP

# Disabled, as it's silently redefining a monster.
NAME: grunt_rogue_ddbe
WEIGHT: 0
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_dd uniq_grunt_rogue_be
KMONS: O = deep dwarf berserker hd:15 god:trog ; \
           battleaxe good_item | executioner's axe good_item w:5 . \
           chain mail good_item w:10 | \
           splint mail good_item w:5 | \
           plate armour good_item w:1
SHUFFLE: dh, ei, fj, gk
KITEM: d = any armour good_item
KITEM: e = any weapon ego:anti-magic good_item
KITEM: f = amulet of rage
KITEM: g = potion of berserk rage q:5
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_ogbe
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_og uniq_grunt_rogue_be
KMONS: O = ogre col:yellow name:berserker n_suf hd:15 god:trog \
           spells:berserker_rage;trog's_hand;.;.;.;brothers_in_arms \
           priest_spells ; \
           giant club ego:anti-magic good_item | \
           giant spiked club ego:anti-magic good_item w:5 . \
           animal skin good_item
SHUFFLE: dh, ei, fj, gk
KITEM: d = any armour good_item
KITEM: e = any weapon ego:anti-magic good_item
KITEM: f = amulet of rage
KITEM: g = potion of berserk rage q:5
MAP
d.e
.O.
f.g
ENDMAP

# Disabled; it silently redefines a monster.
NAME: grunt_rogue_hopr
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_ho uniq_grunt_rogue_pr
WEIGHT: 0
KMONS: O = orc high priest hd:15 god:beogh ; \
           battleaxe race:orcish good_item | \
           executioner's axe race:orcish good_item w:5 | \
           dire flail race:orcish good_item | \
           great mace race:orcish good_item w:5 . \
           chain mail race:orcish w:10 good_item | \
           splint mail race:orcish w:5 good_item | \
           plate armour race:orcish good_item w:1
KMONS: H = orc knight
KMONS: I = orc sorcerer
SHUFFLE: dh, ei, fj, gk
KITEM: df = any armour race:orcish good_item
KITEM: eg = any weapon race:orcish good_item
MAP
dHe
HOI
fHg
ENDMAP

NAME: grunt_rogue_kobe
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_ko uniq_grunt_rogue_be
KMONS: O = kobold col:lightred name:berserker n_suf hd:15 god:trog \
           spells:berserker_rage;trog's_hand;.;.;.;brothers_in_arms \
           priest_spells ; \
           sabre ego:anti-magic good_item | \
           quick blade ego:anti-magic good_item w:5 . \
           leather armour good_item . \
           buckler good_item
SHUFFLE: dh, ei, fj, gk
KITEM: d = any armour good_item
KITEM: e = any weapon ego:anti-magic good_item
KITEM: f = amulet of rage
KITEM: g = potion of berserk rage q:5
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_hofi
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_ho uniq_grunt_rogue_fi
KMONS: O = orc warlord god:okawaru ; \
           battleaxe race:orcish good_item | \
           executioner's axe race:orcish good_item w:5 | \
           dire flail race:orcish good_item | \
           great mace race:orcish good_item w:5 . \
           chain mail race:orcish w:10 good_item | \
           splint mail race:orcish w:5 good_item | \
           plate armour race:orcish good_item w:1
SHUFFLE: dh, ei, fj, gk
KITEM: df = any armour race:orcish good_item
KITEM: eg = any weapon race:orcish good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_mfgl
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_mf uniq_grunt_rogue_gl
KMONS: O = merfolk javelineer name:merfolk_gladiator n_rpl n_des hd:15 \
           name_nocorpse god:okawaru ; \
           trident good_item | halberd good_item | bardiche good_item . \
           javelin good_item q:20 . \
           throwing net q:3 . \
           leather armour w:10 good_item | ring mail w:5 good_item | \
           chain mail good_item w:1
SHUFFLE: dh, ei, fj, gk
KITEM: df = any armour good_item
KITEM: eg = any weapon good_item
MAP
d.e
.O.
f.g
ENDMAP

# Disabled, as it's silently redefining a monster.
NAME: grunt_rogue_ddne
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_dd uniq_grunt_rogue_ne
WEIGHT: 0
KMONS: O = deep dwarf necromancer hd:15 \
           spells:agony;haunt;bolt_of_draining;dispel_undead;animate_dead;\
           death's_door \
           god:makhleb ; robe good_item . \
           quarterstaff ego:draining good_item | \
           lajatang ego:draining good_item w:5 | \
           staff of death w:5
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of necromancy
KITEM: e = book of death
KITEM: f = robe ego:positive_energy good_item
KITEM: g = any weapon ego:draining good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_mfie
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_mf uniq_grunt_rogue_ie
KMONS: O = merfolk aquamancer name:merfolk_gelid n_rpl n_des hd:15 \
           name_nocorpse spells:bolt_of_cold;ozocubu's_refrigeration;blink;\
           ice_storm;throw_icicle;blink \
           god:vehumet ; \
           trident good_item | halberd good_item | bardiche good_item . \
           robe ego:cold_resistance good_item
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of frost
KITEM: e = book of ice
KITEM: f = robe ego:cold_resistance good_item
KITEM: g = any weapon ego:freezing good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_dswz
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_ds uniq_grunt_rogue_wz
KMONS: O = demonspawn name:sorcerer n_suf n_noc col:lightmagenta \
           hd:15 god:vehumet actual_spells \
           spells:fireball;summon_elemental;haste;lightning_bolt;\
                  agony;invisibility
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of minor magic
KITEM: e = book of wizardry
KITEM: f = robe good_item
KITEM: g = any weapon good_item
MAP
d.e
.O.
f.g
ENDMAP

# This is only a one HD increment, so...
NAME: grunt_rogue_dewz
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_de uniq_grunt_rogue_wz
KMONS: O = deep elf sorcerer hd:15 god:vehumet
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of minor magic
KITEM: e = book of wizardry
KITEM: f = robe good_item
KITEM: g = any weapon good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_cehu
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_ce uniq_grunt_rogue_hu
KMONS: O = centaur warrior name:centaur_hunter n_rpl n_des n_noc \
           hd:15 god:okawaru ; \
           longbow good_item . \
           arrow ego:poisoned q:20 | arrow ego:flame q:20 | \
           arrow ego:ice q:20
SHUFFLE: dh, ei, fj, gk
KITEM: d = longbow good_item
KITEM: efg = arrow good_item q:20
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_vpen
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_vp uniq_grunt_rogue_en
KMONS: O = vampire name:enchanter n_suf hd:15 col:lightred \
           spells:confuse;ensorcelled_hibernation;invisibility;haste \
           actual_spells \
           god:kikubaaqudgha ; \
           quick blade good_item . leather armour good_item
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of maledictions
KITEM: e = book of enchantments
KITEM: f = ring of life protection
KITEM: g = scroll of torment
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_seee
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_se uniq_grunt_rogue_ee
KMONS: O = elf col:brown hd:15 name:sludge_elf_earth_mage n_rpl n_des \
           name_nocorpse \
           spells:iron_shot;petrify;stoneskin;\
           lee's_rapid_deconstruction;shatter;blink \
           actual_spells \
           god:sif_muna ; robe good_item . \
           quarterstaff good_item | \
           lajatang good_item w:5 | \
           staff of earth w:5
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of geomancy
KITEM: e = book of the earth
KITEM: f = robe good_item
KITEM: g = any weapon good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_midk
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_mi uniq_grunt_rogue_dk
KMONS: O = minotaur name:death_knight n_suf hd:15 god:yredelemnul col:magenta \
           spells:.;animate_dead;mirror_damage;.;drain_life;drain_life \
           priest_spells ; \
           great sword ego:draining good_item | \
           battleaxe ego:draining good_item | \
           trident ego:draining good_item . \
           chain mail w:10 good_item | splint mail w:5 good_item | \
           plate armour good_item w:1
KMONS: Z = zombie
SHUFFLE: dh, ei, fj, gk
KITEM: d = any armour good_item
KITEM: e = any weapon ego:draining good_item
KITEM: f = scroll of torment
KITEM: g = ring of life protection
MAP
dZe
ZOZ
fZg
ENDMAP

# The HD doesn't do anything; it's there in case someone redefines
# deep elf annihilators.
NAME: grunt_rogue_decj
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_de uniq_grunt_rogue_cj
KMONS: O = deep elf annihilator hd:15 god:vehumet
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of conjurations
KITEM: e = book of power
KITEM: f = robe good_item
KITEM: g = any weapon good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_hobe
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_ho uniq_grunt_rogue_be
KMONS: O = orc warlord name:orc_berserker n_rpl n_des n_noc hd:15 god:trog \
           spells:berserker_rage;trog's_hand;.;.;.;brothers_in_arms \
           priest_spells ; \
           great sword ego:anti-magic good_item | \
           battleaxe ego:anti-magic good_item | \
           trident ego:anti-magic good_item . \
           chain mail w:10 good_item | splint mail w:5 good_item | \
           plate armour good_item w:1
SHUFFLE: dh, ei, fj, gk
KITEM: d = any armour good_item
KITEM: e = any weapon ego:anti-magic good_item
KITEM: f = amulet of rage
KITEM: g = potion of berserk rage q:5
MAP
d.e
.O.
f.g
ENDMAP

# Disabled until monster transmutations make their appearance again...
#NAME: grunt_rogue_drtm
#TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_dr uniq_grunt_rogue_tm
#KMONS: O = base draconian name:transmuter n_suf hd:15 god:sif_muna \
#           dbname:draconian_transmuter \
#           spells:blade_hands;statue_form;dragon_form;stoneskin;\
#                  condensation_shield \
#           actual_spells ; robe good_item
#SHUFFLE: dh, ei, fj, gk
#KITEM: d = book of changes
#KITEM: e = book of transfigurations
#KITEM: f = potion of cure mutation
#KITEM: g = amulet of resist mutation
#MAP
#d.e
#.O.
#f.g
#ENDMAP

NAME: grunt_rogue_habe
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_ha uniq_grunt_rogue_be
KMONS: O = halfling col:lightred name:berserker n_suf hd:15 god:trog \
           spells:berserker_rage;trog's_hand;.;.;.;brothers_in_arms \
           priest_spells ; \
           sabre ego:anti-magic good_item | \
           quick blade ego:anti-magic good_item w:5 . \
           leather armour good_item . \
           buckler good_item
SHUFFLE: dh, ei, fj, gk
KITEM: d = any armour good_item
KITEM: e = any weapon ego:anti-magic good_item
KITEM: f = amulet of rage
KITEM: g = potion of berserk rage q:5
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_teae
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_te uniq_grunt_rogue_ae
KMONS: O = tengu col:lightcyan name:aerator n_suf hd:15 god:vehumet \
           spells:lightning_bolt;swiftness;chain_lightning;\
           airstrike;.;blink \
           actual_spells ; \
           robe good_item . \
           quarterstaff ego:electrocution good_item | \
           lajatang ego:electrocution good_item w:5 | \
           staff of air w:5
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of air
KITEM: e = book of the sky
KITEM: f = robe good_item
KITEM: g = any weapon ego:electrocution good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_dsbe
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_ds uniq_grunt_rogue_be
KMONS: O = demonspawn name:berserker n_suf hd:15 god:trog col:lightred \
           spells:berserker_rage;trog's_hand;.;.;.;brothers_in_arms \
           priest_spells ; \
           great sword ego:anti-magic good_item | \
           battleaxe ego:anti-magic good_item | \
           trident ego:anti-magic good_item . \
           chain mail w:10 good_item | splint mail w:5 good_item | \
           plate armour good_item w:1
SHUFFLE: dh, ei, fj, gk
KITEM: d = any armour good_item
KITEM: e = any weapon ego:anti-magic good_item
KITEM: f = amulet of rage
KITEM: g = potion of berserk rage q:5
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_dsfe
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_ds uniq_grunt_rogue_fe
KMONS: O = demonspawn name:scorcher n_suf n_noc col:lightblue hd:15 \
           spells:bolt_of_fire;fireball;blink;fire_storm;bolt_of_magma;blink \
           god:vehumet actual_spells ; robe good_item . \
           quarterstaff ego:flaming good_item | \
           lajatang ego:flaming good_item w:5 | \
           staff of fire w:f
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of flames
KITEM: e = book of fire
KITEM: f = robe ego:fire_resistance good_item
KITEM: g = any weapon ego:flaming good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_muwz
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_mu uniq_grunt_rogue_wz
KMONS: O = mummy priest name:mummy_sorcerer hd:15 col:lightmagenta n_rpl n_des \
           god:sif_muna actual_spells \
           spells:fireball;summon_elemental;haste;lightning_bolt;\
                  agony;invisibility ; \
           robe good_item . \
           quarterstaff good_item | \
           lajatang good_item w:5 | \
           staff of wizardry w:5
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of minor magic
KITEM: e = book of wizardry
KITEM: f = robe good_item
KITEM: g = any weapon good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_hewz
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_he uniq_grunt_rogue_wz
KMONS: O = elf name:high_elf_sorcerer hd:15 col:lightmagenta n_rpl n_des n_noc \
           god:vehumet actual_spells \
           spells:fireball;summon_elemental;haste;lightning_bolt;\
                  agony;invisibility ; \
           robe good_item . \
           quarterstaff good_item | \
           lajatang good_item w:5 | \
           staff of wizardry w:5
SHUFFLE: dh, ei, fj, gk
KITEM: d = book of minor magic
KITEM: e = book of wizardry
KITEM: f = robe good_item
KITEM: g = any weapon good_item
MAP
d.e
.O.
f.g
ENDMAP

NAME: grunt_rogue_mfbe
TAGS: grunt_rogue_subvault unrand patrolling uniq_grunt_rogue_mf uniq_grunt_rogue_be
KMONS: O = merfolk name:berserker n_suf hd:15 god:trog col:white \
           spells:berserker_rage;trog's_hand;.;.;.;brothers_in_arms \
           priest_spells ; \
           halberd ego:anti-magic good_item | \
           glaive ego:anti-magic good_item | \
           bardiche ego:anti-magic good_item . \
           leather armour w:10 good_item | ring mail w:5 good_item | \
           chain mail good_item w:1
SHUFFLE: dh, ei, fj, gk
KITEM: d = any armour good_item
KITEM: e = any weapon ego:anti-magic good_item
KITEM: f = amulet of rage
KITEM: g = potion of berserk rage q:5
MAP
d.e
.O.
f.g
ENDMAP
