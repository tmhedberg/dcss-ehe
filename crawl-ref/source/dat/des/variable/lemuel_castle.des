###############################################################################
# Lemuel's Castle - Sub-vault version
#
# Originally a castle by Lemuel in large.des. Now randomised by carwin using
# subvaults, and using its own des file due to size.
#
# A castle with a randomized interior. Contains depth-appropriate inhabitants
# and loot. Based on Lemuel's Castle vault. Interior is built as a subvault
# with the tag "lemuel_castle_interior." Feel free to make more!
# To make sure the subvault orients correctly with the bridge, the two
# characters on the bottom left and bottom right of each subvault should be
# left empty (spaces).

{{
-- configuration for Lemuel Castle vault and its subvaults
function castle_lemuel_subvault_setup(e)
         e.tags("no_monster_gen");
         e.tags("no_rotate");
         e.colour("? = brown")
         e.ftile("? = floor_orc")
         e.kfeat("? = floor")
         e.subst("X = c +:1")
         e.subst("~ = ~:2 .")
         e.subst("8 = 89")
         e.tile("c = wall_stone_brick")
         e.ftile(".~*X|+$ = floor_cobble_blood")
         e.ftile("12348 = floor_cobble_blood")
         e.ftile("= = floor_cobble_blood")
         e.kfeat(", = floor")
         e.kfeat("m = iron_grate")
         if you.absdepth() < 7 then
            e.subst("*=*.")
            e.subst("*=$")
            e.subst("|=*")
            e.subst("*=*.")
            e.mons("gnoll")
            e.mons("gnoll / gnoll shaman w:5")
            e.mons("ogre / gnoll sergeant")
            e.mons("jackal")
            e.mons("gnoll")
            e.mons("jackal")
         elseif you.absdepth() < 16 then
            e.subst("*:*$")
            e.subst("|:*$")
            e.mons("orc");
            e.mons("w:5 orc knight / orc warrior")
            e.mons("orc sorcerer / orc warlord / orc knight")
            e.mons("warg")
            e.mons("orc")
            e.mons("warg")
         elseif you.absdepth() < 25 then
            e.subst("*:*$")
            e.subst("|=|.")
            e.mons("deep elf soldier / deep elf fighter")
            e.mons("deep elf knight / deep elf mage / deep elf summoner / deep elf priest")
            e.mons("deep elf annihilator / deep elf sorcerer / deep elf demonologist /\
                    deep elf blademaster / deep elf master archer")
            e.mons("nothing")
            e.mons("deep elf soldier")
            e.mons("nothing")
         else
            e.subst("*:*$")
            e.subst("w:wl")
            e.mons("red draconian / green draconian / yellow draconian / pale draconian /\
                    black draconian / purple draconian")
            e.mons("pale draconian knight / green draconian annihilator /\
                    purple draconian knight")
            e.mons("red draconian scorcher / white draconian zealot /\
                    yellow draconian caller / black draconian shifter")
            e.mons("lindwurm / death drake / swamp drake")
            e.mons("red draconian / green draconian / yellow draconian / pale draconian /\
                    black draconian / purple draconian")
            e.mons("lindwurm / death drake / swamp drake")
            e.mons("plant / w:1 oklob plant")
        end
        e.mons("plant")
end
}}


###############################################################################
# Lemuel Castle Interior Subvaults
###############################################################################

############################################################
# Lemuel Castle Interior original
NAME:   lemuel_castle_interior_original
TAGS:   lemuel_castle_interior
: castle_lemuel_subvault_setup(_G)
MAP
+c....+~~~.~~~+..+~~.~~~~+.***c+
.c||..ccccccccc++ccccccccc.***c.
1c||2.c1...1..1321..1...1c.***c1
.cccccc..1...211112...1..cccccc.
.+....+....1...22...1....+....+.
.+....+..1...1....1...1..+....+.
.cccccccccccccc++cccccccccccXcc.
1c1.1..ccccccc....ccccccc..m..c1
.c.2...+.....+....+.....+..m.8c.
+c1.1..ccccccc....ccccccc..m..c+
  cccccccccccc++++cccccccccccc
ENDMAP

############################################################
# Lemuel Castle Interior : Carwin Interior #1
NAME:   lemuel_castle_interior_carwin_1
TAGS:   lemuel_castle_interior
: castle_lemuel_subvault_setup(_G)
MAP
+c....+........cc........+...*c+
.c.11.ccc+cccccccccccc+ccc..2.c.
.c|.2.X*m.1..........1.m|c....c.
.ccm+mc|m.....1111.....m|cm+mcc.
....~.c|m..1...22......m*c.~1...
......c.+....1132.2....+.X..1...
.cccccccccccccccccccccccccccccc.
.c........1..m....m..1........c.
.+.12........+....+..........2+.
+c........1..m....m..1........c+
  cccccccccccc++++cccccccccccc
ENDMAP

############################################################
# Lemuel Castle Interior : Carwin Interior #2
NAME:   lemuel_castle_interior_carwin_2
TAGS:   lemuel_castle_interior
: castle_lemuel_subvault_setup(_G)
SUBST:  $=$:5 .
ITEM:   w:5 meat ration / sausage / beef jerky / potion of water
MAP
+c....+~~.....1............~$*c+
...1..cccccccccccccccccccccccXc.
cc....c......cG..Gc.1.......c|c1
*cc++cc..cG..+....+...12....m|c.
|....~12.cG..+....+...23....+~c.
$....~.2.....c....c...12....m*c.
*cc++ccccccccc....ccccccc++ccXc+
cc.~~.c.m444dc....c2..$c.~~.....
...12.c.cm4mmc....c$cc.c12......
+c....+...2..c....c...........c+
  cccccccccccc++++cccccccccccc
ENDMAP

############################################################
# Lemuel Castle Interior : Carwin Interior #3
NAME:   lemuel_castle_interior_carwin_3
TAGS:   lemuel_castle_interior
: castle_lemuel_subvault_setup(_G)
SUBST:  $=$:5 .
MAP
+c....+~~.*c$*|cc|*$cccccc$...c+
.c.1..ccccccmc1321cmcccccc....c.
.c..2........c.12.c...........c.
.cc++c.........ww.....2..1c++cc.
.....c.2.......ww.........c.....
.2...c...1...c..$.c...1...c..1..
.ccccc+cccccccccXcccccccc+ccccc.
.c.1.c.~..1..+....+2.....~c2..c.
.~.c$c.~2....c....c.....~.c$c1~.
+c..2c.~..1..c....c..1..~.c...c+
  cccccccccccc++++cccccccccccc
ENDMAP

############################################################################
NAME:     lemuel_castle_with_subvaults
DEPTH:    D:3-27
ORIENT:   northwest
: castle_lemuel_subvault_setup(_G)
SUBVAULT: A : lemuel_castle_interior
SUBST:    A = .
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x,,,,,,7,,,,,,,,,,,,,,,,,6,6,,,,,,7,,,,,,,,,,,,
x,,,,,,,,,,,,,,7,,,,,,,,6,6,6,,,7,,,,,,,,,,,,,7
x,,,,wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
x,,,,wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww
x,,,,wwccnccwwwwwwwwwwwwwwwwwwwwwwwwwwwwccnccww
x,,,,wwc1.1c4....4.......4......4....4..c1.1cww
x,,,,wwc.1.cXccccccccccccccccccccccccccXc.1.cww
x,,,,wwcccAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAcccww
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www
x,7,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www
x,,,,wwcccAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAcccww
x,,,7wwc.1.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.1.cww
x,,,,wwc1.1c............................c1.1cww
x,,,,wwccnccwwwwwwwwwwww????wwwwwwwwwwwwccnccww
x,,,,wwwwwwwwwwwwwwwwwww????wwwwwwwwwwwwwwwwwww
x7,,,wwwwwwwwwwwwwwwwwww????wwwwwwwwwwwwwwwwwww
x,,,,,,,,,,,,,,,,,,656,,,@,,,,656,,,,,,,,,,,,7,
ENDMAP
