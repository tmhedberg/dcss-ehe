###############################################################################
# float.des: This is the place for floating regular vaults (ones defined with
#            ORIENT: float). Vaults with other ORIENT arguments  (encompass,
#            north, northwest etc.) are split into large_abstract.des and
#            large_themed.des. Minivaults (those without ORIENT lines)
#            are split into mini_monsters.des and mini_features.des.
#            Sufficiently complicated-enough vaults
#            using global lua functions go into individual .des files.
#
# <<1>>      Decorative
# <<2>>      Themed threat
# <<3>>      Abstract / generic threat
#
# The dividing line between generic and themed is potentially vague;
# if specified monsters are used lightly and they're very common amd in-place
# or very generic monsters, opt for <3>, otherwise use <2>.
###############################################################################

# Functionality for some of HangedMan's decor vaults.
{{
function init_hm_decor_walldepth(e, variables)
local a = you.absdepth() * 5
  if a - 75 > 0 then
    e.subst(variables .. " : x:50 c:" .. a .. " b:" .. a - 75)
  else
    e.subst(variables .. " : x:50 c:" .. a)
  end
end

function init_protected_by_tso(e)
  e.tags('uniq_protected_by_tso')
  e.colour('- : yellow')
  e.lua_marker('!', message_at_spot("You almost think you hear a voice: \"Seek not undue power, mortal.\"", "warning"))
  e.subst('- : +')
end

function grunt_megastairs_setup(e)
  e.orient("float")
  e.tags("extra luniq_grunt_megastairs")
  e.depth("Depths, Crypt, Elf, Snake, Zot, Dis, Geh, Coc, Tar")
  e.nsubst("0 = 2:0 / 2:0. / *:.")
  e.subst("@ = @+")
  if you.depth() == 1 then
    e.shuffle("{[(")
    e.subst("{ = }")
    e.subst("[ = ]")
    e.subst("( = )G")
  elseif you.depth() == dgn.br_depth() then
    e.shuffle("{[(")
    e.subst("( = (G")
  else
    e.shuffle("{[(}])")
    e.subst("( = (<")
    e.subst(") = )>")
  end
  if you.branch() == "Dis" then
    e.subst("x = v")
  elseif you.branch() == "Depths" then
    e.subst("x : cccccvvvb")
  else
    e.subst("x = c")
  end
  e.subst("G : GGGGTTVt")
  e.kmons("0 = 0 band")
end
}}

# Set the default depth for vaults.  Be sure to adapt or specify
# the DEPTH of particularly easy, tough, or thematic vaults.
default-depth: D:12-, Depths, !Depths:$

###############################################################################
#
# <<1>> Decorative vaults.
#       No threats.
#
###############################################################################

##############################################################################
# Elevator
NAME:    lemuel_elevator
TAGS:    allow_dup extra luniq_staircase
DEPTH:   D:2-, !D:$, Elf:2, Crypt:2-, !Crypt:$, Zot:2-, !Zot:$, \
         Tar:2-, !Tar:$, Dis:2-, !Dis:$, Geh:2-, !Geh:$, Coc:2-, !Coc:$
ORIENT:  float
SUBST:   m:mn
SHUFFLE: ]}), [{(
MAP
mmmm
m{}m
mmmm
ENDMAP

NAME:   firewood_loft
TAGS:   extra
DEPTH:  D, !D:$, Depths, !Depths:$
ORIENT: float
: if you.absdepth() > 8 then
KITEM:  " = staff, quarterstaff, quarterstaff good_item
: else
KITEM:  " = staff, quarterstaff, quarterstaff
: end
KITEM:  ' = staff, any magical staff / any rod w:1
MAP
xxxxx
x"']x
xxxxx
ENDMAP

################################################################################
# Staircase flavour vaults.
#
NAME:    minmay_staircase_windowed_small
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
ORIENT:  float
WEIGHT:  5
SHUFFLE: )]}([{
SUBST:   ? = cn
SUBST:   c : c x:2 v:1 b:1
SUBST:   n : n:18 G:1 .:1
SUBST:   G : ...G
MAP
ccccccccc
cG.GcG.Gc
c...+.).c
cG..c...c
cc+cc?n?c
cG..c...
c...+...
cG.Gc...
ccccc
ENDMAP

NAME:    minmay_staircase_windowed_large
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
ORIENT:  float
WEIGHT:  5
SHUFFLE: )]}([{
SUBST:   c : c x:2 v:1 b:1
SUBST:   n : n:18 G:1 .:1
SUBST:   G : ...G
MAP
ccccccccccccc
cG...GcG...Gc
c.....c.....c
c.....+..)..c
c.....c.....c
cG...GcG...Gc
ccc+ccccnnncc
cG...Gc.....
c.....c.....
c.....+.....
c.....c.....
cG...Gc.....
ccccccc
ENDMAP

NAME:    minmay_staircase_statues
TAGS:    transparent extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
ORIENT:  float
WEIGHT:  5
SHUFFLE: )]}([{
MAP
G.G
.).
G.G
ENDMAP

NAME:    minmay_staircase_statues_hall_small
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
ORIENT:  float
WEIGHT:  3
SHUFFLE: )]}([{
SUBST:   c : c x:2 v:1 b:1
MAP
   ccccc
   cG.Gc
   c...c
cccc...cccc
cG.......Gc
c.).......+
cG.......Gc
cccc...cccc
   c...c
   cG.Gc
   ccccc
ENDMAP

NAME:    minmay_staircase_statues_hall_large
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
ORIENT:  float
WEIGHT:  2
SHUFFLE: )]}([{, AB
SUBST:   A = .
SUBST:   B = G
SUBST:   C = G.
SUBST:   c : c x:2 v:1 b:1
MAP
     ccccccc
     cB.A.Bc
     c.....c
     c.....c
     c.....c
cccccc.....cccccc
cB.............Bc
c................
cA.)....C.......@
c................
cB.............Bc
cccccc.....cccccc
     c.....c
     c.....c
     c.....c
     cB.A.Bc
     ccccccc
ENDMAP

NAME:    minmay_staircase_bubble
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
ORIENT:  float
SUBST:   m = mmmm.
MAP
 .......
..bb.bb..
.bb...bb.
.b.....b.
.m..)..m.
.b.....b.
.bb...bb.
..bbmbb..
 .......
ENDMAP

NAME:    infiniplex_staircase_squiggle
ORIENT:  float
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SHUFFLE: }]){[(
MAP
    xxxxx
xxxxx...x
x}..x.x.xx
xxx.x.x..@
  x...xxxx
  xxxxx
ENDMAP

NAME:    infiniplex_staircase_zigzag_1
ORIENT:  float
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
NSUBST:  @ 1:@ / *:x
SHUFFLE: })]{([
MAP
xxxxxxxxx
x}..x...x
xxx.x.x.x
x...x.x.x
x.xxx.x.x
x.....x.x
xxxxxxx.x
@.......x
x@xxxxxxx
ENDMAP

NAME:    infiniplex_staircase_zigzag_2
ORIENT:  float
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SHUFFLE: })]{([
MAP
xxxxxxxxxxxxx
x}..x...x...x
xxx.x.x.x.x.x
xxx...x.x.x.x
xxxxxxx.x.x.x
xxxxxxx...x.x
xxxxxxxxxxx@x
ENDMAP

NAME:    infiniplex_staircase_zigzag_3
ORIENT:  float
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SHUFFLE: })]{([
MAP
xxxxxxxxx@x
xxxxx...x.x
xxxxx.x.x.x
x}..x.x.x.x
xxx...x.x.x
xxxxxxx.x.x
xxxxxxx...x
xxxxxxxxxxx
ENDMAP

NAME:    infiniplex_staircase_zigzag_4
ORIENT:  float
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
NSUBST:  @ 1:@ / *:x
SHUFFLE: })]{([
MAP
xxxxxxxxx
x.......x
x.xxxxx.x
x.....x.x
xxxxx.x.x
x}..x.x.x
xxx.x.x.x
x...x.x.x
x.xxx.x.x
x.....x.@
xxxxxxx@x
ENDMAP

NAME:    infiniplex_staircase_zigzag_5
ORIENT:  float
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
NSUBST:  @ 1:@ / *:x
SHUFFLE: })]{([
MAP
xxxxxxx@x
x.....x.@
x.xxx.x.x
x...x.x.x
xxx.x.x.x
x}x.x.x.x
x.x.x.x.x
x...x.x.x
xxxxx.x.x
x.....x.x
x.xxxxx.x
x.......x
xxxxxxxxx
ENDMAP

NAME:    infiniplex_staircase_spiral
ORIENT:  float
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SHUFFLE: })]{([
MAP
xxxxxxxxx
@.......x
xxxxxxx.x
x.....x.x
x.xxx.x.x
x.x}..x.x
x.xxxxx.x
x.......x
xxxxxxxxx
ENDMAP

NAME:    infiniplex_staircase_spiral_double
ORIENT:  float
TAGS:    transparent extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SHUFFLE: })]{([
SUBST:   m : mxxxx
MAP
xxxxxxxxxxx
@.........x
xmxxxxxmx.x
x.......x.x
x.xxxxx.x.x
x.m..}..m.x
x.x.xxxxx.x
x.x.......x
x.xmxxxxxmx
x.........@
xxxxxxxxxxx
ENDMAP

NAME:    infiniplex_staircase_x
ORIENT:  float
TAGS:    transparent extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SUBST:   D : .x
SHUFFLE: }]){[(
MAP
@xx   xx@
x.xx xx.x
xx.xxx.xx
 xx.D.xx
  xD}Dx
 xx.D.xx
xx.xxx.xx
x.xx xx.x
@xx   xx@
ENDMAP

NAME:    infiniplex_staircase_split_hallway
ORIENT:  float
TAGS:    transparent extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SHUFFLE: })]{([
MAP
  x@x
 xx.xx
xxx.xxx
xx...xx
x.x+x.x
x.x.x.x
x.x}x.x
x.x.x.x
x.x+x.x
xx...xx
xxx.xxx
 xx.xx
  x@x
ENDMAP

NAME:    infiniplex_staircase_trident
ORIENT:  float
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
NSUBST:  } = 1:} / *:.
NSUBST:  ~ = 1:~ / *:.
SHUFFLE: })]{([
MAP
xxxxxxx
x}x}x}x
x.x.x.x
x.x.x.x
x.x.x.x
x.x.x.x
x+x+x+x
x~~~~~x
x.....x
x.....x
xx...xx
 xx.xx
  x@x
ENDMAP

NAME:    infiniplex_staircase_hallway_doors
ORIENT:  float
TAGS:    transparent extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SHUFFLE: DEHJK
SUBST:   D : +
SUBST:   E : +xx
SUBST:   H : +xx
SUBST:   J : +xx
SUBST:   K : +xx
SHUFFLE: py/qz
SUBST:   p = .
SUBST:   q = x
SUBST:   y = @
SUBST:   z = x
SHUFFLE: })]{([
MAP
    xxxxxxxxxxxxxxxxxzxyx
    @.................ppx
xxxxxHxDxExDxKxDxExDxHxxx
xxx}....................J
xxxxxHxDxExDxKxDxExDxHxxx
    @.................ppx
    xxxxxxxxxxxxxxxxxzxyx
ENDMAP

NAME:    infiniplex_staircase_diamond
ORIENT:  float
TAGS:    extra luniq_staircase allow_dup decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SUBST:   c : x:45 .:15 c:10 m:5
NSUBST:  ! = 1:+ / *:!
SUBST:   ! = +x
SHUFFLE: }]){[(
MAP
   x!x
  xx.xx
 xx...xx
xx..c..xx
x}.ccc..!
xx..c..xx
 xx...xx
  xx.xx
   x!x
ENDMAP

NAME:    infiniplex_staircase_star
ORIENT:  float
TAGS:    transparent extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SUBST:   D : .x
SUBST:   x : x:40 c:5 m:4 b:1
NSUBST:  ! = 1:@ / *:!
SUBST:   ! = @...
SHUFFLE: }]){[(
MAP
...!...!...
....DxD....
....xxx....
!...DxD...!
.DxD...DxD.
.xxx.}.xxx.
.DxD...DxD.
!...DxD...!
....xxx....
....DxD....
...!...!...
ENDMAP

NAME:    infiniplex_staircase_door_maze
ORIENT:  float
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
NSUBST:  D = 1:+ / *:x
NSUBST:  E = 1:+ / *:x
NSUBST:  H = 1:+ / *:x
SHUFFLE: }]){[(
MAP
xExExExEx
E.D.D.H.E
x+x+x+x+x
E.D.D.x.E
x+x+x+x+x
E.D}D.x.E
x+xxx+x+x
E.+.+.H.E
xExExExEx
ENDMAP

NAME:    infiniplex_staircase_9_rooms
ORIENT:  float
TAGS:    extra luniq_staircase no_monster_gen decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  2
# Room contents
NSUBST:  } = 1:} / *:0
SHUFFLE: }]){[(
NSUBST:  0 = 1:% / 2:0 / *:.
# Doors
NSUBST:  @ = 1:+ / *:Z
NSUBST:  D = 1:+ / *:Z
NSUBST:  E = 1:+ / *:Z
NSUBST:  H = 1:+ / *:Z
NSUBST:  J = 1:Z / *:+
SUBST:   Z = +xxxxxxxxx
MAP
xxExxxZxxx+xx
x...x...x...x
E.0.E.}.J.0.+
x...x...x...x
xxExxxJxxxJxx
x...x...x...x
+.}.+.}.J.}.Z
x...x...x...x
xxDxxxDxxxHxx
x...x...x...x
D.0.+.}.H.0.H
x...x...x...x
xxDxxxDxxxHxx
ENDMAP

NAME:    infiniplex_staircase_pillars_1
ORIENT:  float
TAGS:    extra luniq_staircase uniq_infiniplex_staircase_pillars decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SUBST:   c : x:45 .:15 c:10 m:5
SHUFFLE: }p/)q/]r
SUBST:   ) = x
SUBST:   ] = x
SUBST:   q = x
SUBST:   r = x
SHUFFLE: })]{([
MAP
   xxx
   x)x
  xxqxx
xxx...xx
x}p.c..@
xxx...xx
  xxrxx
   x]x
   xxx
ENDMAP

NAME:    infiniplex_staircase_pillars_2
ORIENT:  float
TAGS:    extra luniq_staircase uniq_infiniplex_staircase_pillars decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SUBST:   c : x:45 .:15 c:10 m:5
SHUFFLE: }p/)q/]r
SUBST:   ) = x
SUBST:   ] = x
SUBST:   q = x
SUBST:   r = x
SHUFFLE: })]{([
# entry
SHUFFLE: D@/E'/H"
SUBST:   E = x
SUBST:   H = x
SUBST:   ' = x
SUBST:   " = x
MAP
   xxx
   x)x   x@x
  xxqxx xxDxx
xxx...xxx...xx
x}p.c.....c.E'
xxx...xxx...xx
  xxrxx xxHxx
   x]x   x"x
   xxx
ENDMAP

NAME:    infiniplex_staircase_pillars_3a
ORIENT:  float
TAGS:    extra luniq_staircase uniq_infiniplex_staircase_pillars decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
SUBST:   c : x:45 .:15 c:10 m:5
SHUFFLE: }p/)q/]r
SUBST:   ) = x
SUBST:   ] = x
SUBST:   q = x
SUBST:   r = x
SHUFFLE: })]{([
# entry
SHUFFLE: D@/E'/H"
SUBST:   E = x
SUBST:   H = x
SUBST:   ' = x
SUBST:   " = x
MAP
   xxx
   x)x
  xxqxx xxxxx
xxx...xxx...x
x}p.c.....c.x
xxx...xxx...x
  xxrxx xx.xx
   x]x   x.x
   xxx  xx.xx
       xx...xx
       "H.c.D@
       xx...xx
        xxExx
         x'x
ENDMAP

NAME:    infiniplex_staircase_pillars_3b
ORIENT:  float
TAGS:    extra luniq_staircase uniq_infiniplex_staircase_pillars decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  2
SUBST:   c : x:45 .:15 c:10 m:5
SHUFFLE: }p/)q/]r
SUBST:   ) = x
SUBST:   ] = x
SUBST:   q = x
SUBST:   r = x
SHUFFLE: })]{([
# entry
SHUFFLE: D@/E'/H"
SUBST:   E = x
SUBST:   H = x
SUBST:   ' = x
SUBST:   " = x
MAP
   xxx
   x)x         x@x
  xxqxx xxxxx xxDxx
xxx...xxx...xxx...xx
x}p.c.....c.....c.E'
xxx...xxx...xxx...xx
  xxrxx xxxxx xxHxx
   x]x         x"x
   xxx
ENDMAP

NAME:    infiniplex_staircase_river
ORIENT:  float
TAGS:    extra luniq_staircase decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
NSUBST:  D = 1:W / *:w
NSUBST:  E = 1:W / *:w
NSUBST:  H = 1:W / *:w
NSUBST:  } = 1:} / *:.
SHUFFLE: }]){[(
MAP
xxxxxx
x....x
x.}}.x
x.}}.x
x....x
xww..x
xwDDwx
xwEEwx
xwHHwx
x..wwx
x....x
x@..@x
ENDMAP

NAME:    infiniplex_staircase_pool_1
ORIENT:  float
TAGS:    transparent extra luniq_staircase no_pool_fixup no_monster_gen decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$
WEIGHT:  3
# Water
SUBST:   D = WW.
SUBST:   E = W..
# Place @(s)
NSUBST:  ! = 1:@ / *:!
SUBST:   ! = @...
SHUFFLE: }]){[(
MAP
     .!.
   ..EDE..
  !EDWWWDE!
 .EwwwWwwwE.
 .DwcwWwcwD.
.EWwwwWwwwWE.
!DWWWW}WWWWD!
.EWwwwWwwwWE.
 .DwcwWwcwD.
 .EwwwWwwwE.
  !EDWWWDE!
   ..EDE..
     .!.
ENDMAP

NAME:    infiniplex_staircase_pool_2
ORIENT:  float
TAGS:    transparent extra luniq_staircase no_monster_gen decor
DEPTH:   D:2-, !D:$, Depths:2-, !Depths:$, Lair, Swamp
WEIGHT:  3 (D), 10 (Swamp)
# Water
SUBST:   D = WW.
SUBST:   E = W..
SUBST:   Z = wwW
# Guarantee a path
NSUBST:  S = 1:W / *:w
# Place @(s)
NSUBST:  ! = 1:@ / *:!
SUBST:   ! = @...
SHUFFLE: }]){[(
MAP
  !EEDEE!
 EDWZZZWDE
!DZwwwwwZD!
EWwwwwwwwWE
EZwwDEDwwZE
DZwwE}EwwZD
EZwwDEDwwZE
EWwSWwwwwWE
!DZSSSwwZD!
 EDWZZZWDE
  !EEDEE!
ENDMAP

# This is replaced by infiniplex_staircase_pool_lava for low levels
NAME:    infiniplex_staircase_pool
ORIENT:  float
TAGS:    extra luniq_staircase decor
DEPTH:   D, !D:$, Lair, Swamp
WEIGHT:  2 (D), 20 (Swamp)
SUBST:   D = ww.
SUBST:   E = w..
NSUBST:  ! = 1:@ / *:!
SUBST:   ! = @...
SHUFFLE: }]){[(
MAP
   ...
  !....E!
 .D.}.DwDE
 EwDEDwwwD.
Ewwwwwwwww!
Dwwwwwwwww.
Ewwwwwwww.
!wwwwwwwE.
 .DwwwDE!
  ..!...
ENDMAP

#  This is about the same as infiniplex_staircase_pool but with lava.
#    There are a few small changes to accout for lava being impassible.
NAME:    infiniplex_staircase_pool_lava
ORIENT:  float
TAGS:    extra luniq_staircase decor
DEPTH:   Depths
WEIGHT:  2
SUBST:   D = ll.
SUBST:   E = l..
NSUBST:  ! = 1:@ / *:!
SUBST:   ! = @...
SHUFFLE: }]){[(
MAP
   ...
  !....E!.
 .D.}.DlDE.
.ElDEDlllD.
Elllllllll!
Dlllllllll.
Ellllllll.
!lllllllE.
 .DlllDE!
  ..!...
ENDMAP

##############################################################################
# Celtic spiral tile pattern (rotated 45 degrees)
# http://en.wikipedia.org/wiki/Celtic_maze
NAME:   mumra_celtic_spiral_tile
ORIENT: float
TAGS:   transparent allow_dup decor
DEPTH:  D:1-10
WEIGHT: 2
MAP
            xxxxxxxxxxxxx
            x...........x
            x.xxxxx.xxx.x
            x.x...x.x.x.x
            x.xxx.x.x.x.x
            x.....x.x...x
      xxxxxxxxxxxxx.xxxxxx@xxxx
      x...........x...........x
      x.xxxxx.xxx.x.xxx.xxxxx.x
      x.x...x.x.x.x.x.x.x...x.x
      x.xxx.x.x.x.x.x.x.x.xxx.x
      x.....x.x...x...x.x.....x
xxxxxxxxxxx.x.xxxxxxxxx.xxxxxxx
x...........x...........x
x.xxxxx.xxx.x.xxx.xxxxx.x
x.x...x.x.x.x.x.x.x...x.x
x.xxx.x.x.x.x.x.x.x.xxx.x
x.....x.x...x...x.x.....x
xxxx@xx.xxxxxxxxx.xxxxxxx
      x...........x
      x.xxxxxxxxx.x
      x.x...x...x.x
      x.xxx.x.xxx.x
      x.....x.....x
      xxxxxxxxxxxxx
ENDMAP

##############################################################################

NAME:   wad_pebble_path
DEPTH:  D:2-, !D:$, Depths:2-, !Depths:$
ORIENT: float
WEIGHT: 4
FTILE: ' = floor_pebble
MONS:   plant
MAP
c..............@..............c
.x.............'.............x.
x.x........I..'''..I........x.x
cx.x.........'''''.........x.xc
ccx.x.....I.'''''''.I.....x.xcc
cccx.x.......'''''.......x.xccc
ccccx.x....I..'''..I....x.xcccc
cccccx.x...x..c'c..x...x.xccccc
......x.xcxxxc.'.cxxxcx.x......
cccccx.x...x..c'c..x...x.xccccc
ccccx.x.......'''.......x.xcccc
cccx.x........'''........x.xccc
ccx.x.........'''.........x.xcc
cx.x..........'''..........x.xc
x.x.......c.cc'''cc.c.......x.x
.x.......c.c.cc'cc.c.c.......x.
c........cccccc+cccccc........c
ENDMAP

##############################################################################

NAME:   wad_lake_hut
DEPTH:  D:2-, !D:$, Depths:2-, !Depths:$
ORIENT: float
WEIGHT: 4
FTILE: ' = floor_pebble
FTILE: "1 = floor_dirt
COLOUR: "1 = brown
MONS:   plant
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxx1"1"WxxxxxxxxxxWWWwwwWWWWxx
xxx""""WWWWxccccxWWWWWWwwwWWWWx
xx"1""WWWWx.cccc.xWWWWWWwwWWWxx
x11""Wxxxxcc.''.ccxxxxWWwwWxxxx
x""""WWwwxcc''''ccxxWWWwwwWWWWx
x""""Wwwwxcc''''ccxWWWwwwWWWWWx
x"11Wwxxxxcc.''.ccxWwwwwxxxxxxx
x1"WWwwwWWxcc++ccxWwwwWW"11""1x
x"""WWwwwWWxc..cxWWWwwwWW""xx+x
xx"1"WWwwxxxxx+xxxxWWwwwWW"x''x
xx""1WWwxxWW.....xxxWWwwWWWx''x
x1"""Wxxx"W..''..WWxxWwwWWWx''x
x""Wxxx""WW.''''..WWxxWwwW"x''x
xWxxx""1WWx''''''x.W"xxww"1xxxx
xxx""""WWxxx.''.xxxWx"xxwww""1x
x""xx1WWwwx..''..xwwW""xxwww""x
x""1"WWWww..''''..ww"xx"xxww1"x
x1"""1WWwwx''''''xwwW"xx"xxww1x
x"xx""WW.xxx.''.xxx.WW1"""xxwwx
xx""""1Wwwx..''..xwwWW"""xxxxxx
xxx"1""Www..''''..wwWW""""1"xxx
xx"xx"WWwwx''''''xwwWW"x""xx"xx
xx""1"WW.xxx.''.xxx.WW""x"1"1xx
x"xx""WWwwx..''..xwwWW"""""1xxx
xx"1""1Www..''''..wwWW"""xx11xx
xxx"xx"1wwx''''''xwwWW"xx.11xxx
xxxx"1""Wxxx.''.xxx.WW".xx.xxxx
xxxxxx""""x..''..x..WWxx1xxxxxx
xxxxxxxxxxx.''@'.xxxxxxxxxxxxxx
ENDMAP

NAME:  minmay_weird_star
TAGS:  no_pool_fixup no_monster_gen
DEPTH:  D:8-, !D:$
ORIENT: float
MONS:  plant
SUBST: x : x:9 c:1
MAP
xxxxxxxxxxx@..@xxxxxxxxxxx
x|11xxxxxx......xxxxxx11|x
x11llxxxxx..ww..xxxxxll11x
x1llllxxxx.wwww.xxxxllll1x
xxlllllxxx.wbbw.xxxlllllxx
xxxlllllx..wbbw..xlllllxxx
xxxxllll...wbbw...llllxxxx
xxxxxll...wwbbww...llxxxxx
xxxxxx...wwbbbbww...xxxxxx
xxxxx...wwbbbbbbww...xxxxx
x......wwbbbbbbbbww......x
@..wwwwwbbbbbbbbbbwwwww..@
..wwbbbbbbbbbbbbbbbbbbww..
..wwbbbbbbbbbbbbbbbbbbww..
@..wwwwwbbbbbbbbbbwwwww..@
x......wwbbbbbbbbww......x
xxxxx...wwbbbbbbww...xxxxx
xxxxxx...wwbbbbww...xxxxxx
xxxxxll...wwbbww...llxxxxx
xxxxllll...wbbw...llllxxxx
xxxlllllx..wbbw..xlllllxxx
xxlllllxxx.wbbw.xxxlllllxx
x1llllxxxx.wwww.xxxxllll1x
x11llxxxxx..ww..xxxxxll11x
x|11xxxxxx......xxxxxx11|x
xxxxxxxxxxx@..@xxxxxxxxxxx
ENDMAP
####

NAME:    hangedman_decor_cross_pass
TAGS:    transparent extra decor no_pool_fixup
ORIENT:  float
DEPTH:   D:8-, Depths, Elf
SHUFFLE: _- / _- / -_, '" / '" / "', !? / !? / !?
SHUFFLE: HIJK / HIJK / IHKJ, LMNO / LMNO / MLON, PQRS / PQRS / QPSR
SUBST:   HJLNPR = ., I : z:6 .:1, K : z., M : z:6 .:1, O : z., Q : z:6 .:1, S : z.
SUBST:   A : z., B : z., C : z., ~"! = x, - : ++., ' : ++. ? : +++., = : xx+.
SUBST:   y : z.., Y = TVCtwz .:30
: init_hm_decor_walldepth(_G, "z")
MAP
           x...x
           x...x
       .xxxx...xxxx
       .!...HIJ...xxxxx
       .?...KAK...'...x
       .!...JIH...x...x
       .xxxx...x""x...xxxx
  .........x...x...LMN...x
  xx+xx....x...'...OBO...x
  x...x....xx=xx...NML...x
  x...x........xxxx...xx'xx
xxx...xxxx........x..."...x
...PQR...x..y.....x..."...x
...SCS...=........xx'xx...xxx
...RQP...x....Y....x...PQR...
xxx...xx-xx........=...SCS...
  x...~...x.....y..x...RQP...
  x...~...x........xxxx...xxx
  xx-xx...xxxx........x...x
   x...LMN...xx=xx....x...x
   x...OBO...-...x....xx+xx
   x...NML...x...x.........
   xxxx...x~~x...xxxx.
      x...x...HIJ...!.
      x...-...KAK...?.
      xxxxx...JIH...!.
          xxxx...xxxx.
             x...x
             x...x
ENDMAP

###############################################################################
#
# <<2>> Themed threat vaults.
#       Explicitly tries to evoke some idea,
#       using uncommon or out-of-place monsters for the depth.
#
###############################################################################

##############################################################################
# Stone golems
#
NAME:   minmay_stone_box
DEPTH:  D:10-
ORIENT: float
KMONS:  " = stone golem
KITEM:  " = any scroll / any potion
MAP
cccccccccc
c{......>c
c........c
c........c
c...""...c
c...""...c
c........c
c........c
c<......}c
cccccccccc
ENDMAP

##############################################################################
# Skeleton sneak attack
#
NAME:   minmay_skeletal_surprise
TAGS:   no_monster_gen no_item_gen
DEPTH:  Depths, !Depths:$
ORIENT: float
MONS:   skeletal warrior
KMONS:  $ = skeletal warrior / skeleton / bone dragon w:1 / nothing w:40
KITEM:  $ = $
MAP
ccccccccccccc
c$$$ccccc$$$c
c$$$ccccc$$$c
c$$$ccccc$$$c
cccc1ccc1cccc
ccccc...ccccc
ccccc.}.ccccc
ccccc...ccccc
cccc1ccc1cccc
c$$$ccccc$$$c
c$$$ccccc$$$c
c$$$ccccc$$$c
ccccccccccccc
ENDMAP

##############################################################################
# Cavepeople
#
NAME:   erik_1
ORIENT: float
WEIGHT: 5
TAGS:   no_monster_gen
DEPTH:  D:7-11, Lair
#
SHUFFLE: rR/sS/uU/vV/wW/yY/zZ, uU/nN
SUBST: r=n, R=N, s=n, S=N
SUBST: n=., N=@
SUBST: u=x, v=x, w=x, y=x, z=x
SUBST: U=x, V=x, W=x, Y=x, Z=x
#
SUBST: 1 = 1:20 ., 2=2., 3=23, 4=24
MONS:  jackal/hound, human, Edmund/Jessica/Sigmund, Ijyb/Psyche
MONS:  sheep/nothing
MAP
         xSxxxxx    xxxxxxxxxxUx
         xxssxxx    xx1xxxxxxuxx
xxxxxxxxxxxxxssx    x111xxxxxuxx
xxxzzzxxxxxxxxxsxxxxxx1xxxxxxuxx
Zzzxzxzxx555xxsxxxxxxx1xxxxxuxxx
xxxxxxxzxx5xxx.xxx..x1xxxxxuxxxx
   xxxxzxx5xxxx......xxxxxuuxxxx
   xxxzxxxx5xx...2...cccxxxux
   xxxzxxxx....2224..+$cxxuxx
   xxxxz.......2ll22.cccxxxux
   xxxxxx......3222.xxxxxxuxxxxxxxxx
   xxxxxxyxxxx2.......xxuuxxvvvvxxxx
   xxxxxyxxxx.......xx..vxvvxxxxvxxx
xxxxxxyyxxxxxrxxxxx.xxwxxvxxxxxxxvvx
xxxyxyxxxxxxrxxrxxxx.xxwxxxxxxxxxvxx
xyyyyxxxxxxxxrrxrrxxxxxxwxxxxxxxvxxx
xyxxxx     xxxxrxrrxxxxxxwwxxxxxxvvxxxxx
Yxxxxx     xxxxxxxrxxxxxxxxwxxxxxxxvvxvV
xxxxxx   xxxxxxxrrx       xxwxxxxxxxxvxx
         xxxrrrrxxx       xxwxxxxxxxxxxx
         xxRxxxxxxx       xxWxx
ENDMAP

##############################################################################
# Old Forge (1KB)
#
NAME:    old_forge
DEPTH:   D:4-12
ORIENT:  float
KITEM:   ? = long sword unrand:singing_sword, damaged short sword, \
             damaged long sword, damaged hand axe
KITEM:   ! = damaged executioner's axe, damaged short sword, \
             damaged long sword, damaged morningstar
KITEM:   + = damaged eveningstar, damaged short sword, \
             damaged long sword, damaged war axe
KITEM:   & = damaged hammer / nothing
MONS:    fungus, plant
MONS:    giant mite, scorpion
MONS:    rat / porcupine, green rat / orange rat
MONS:    bat
KFEAT:   w = web
SHUFFLE: ?!+
SUBST:   c:cccvvb , b=cc.
SUBST:   1 : 1112
SUBST:   1=1. , 2=2.... , 3=3. , 4=4. , 5=5. , 6=6.
SUBST:   ':.x''' , '=.x , .=......ww17
MAP
   xxxxxxxxxxxxxxxx
  xxx11'cccccccxxxx
 xxx'...c..?v3cx1xx
xx111.3.c&'.v3c14xx
x111'.......&&c4xxx
xxx3.11..1xxccc.44x
  xxx.1.1113x..'xxx
   xxx'&..11.'xxx
    x1xxx'..xxx
    xxx xx'.'xx
         xxx44xxxx
           xx'.44xx
            xxxx.'x
               xx@x
ENDMAP

##############################################################################
# Starter slime pit
#
# The slime creature or amoeba may seem too tough for the level where this
# appears, but since they are normal speed with no range attacks, they can
# be escaped.
#
NAME:   lemuel_slime
DEPTH:  D:6-, Lair
ORIENT: float
MONS:   w:20 ooze / jelly / fungus
MONS:   slime creature / giant amoeba / oklob plant
MAP
xxxxxxxxxx
xxxx111xxx
xx1111111x
@+1112111x
xx1111111x
xxxx111xxx
xxxxxxxxxx
ENDMAP

##############################################################################
# Beware the mummy's curse (Lemuel)
#
NAME:    lemuel_MiniTomb
ORIENT:  float
DEPTH:   Depths, !Depths:$, Crypt
MONS:    zombie / nothing
MONS:    mummy, patrolling guardian mummy, patrolling greater mummy
MONS:    zombie
MONS:    mummy / nothing
SHUFFLE: 2F, cbv
SUBST:   F = G:99 F:1
KMONS:   F = orange crystal statue / silver statue / ice statue
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx3$$xxxxxxxxxxxxx
ccxxxxxxxxccc+cccccccccxxxx
cccxxxxxxxc.....c...1$cxxxc
@Fcx6$.xxxc..1..c...25ccccc
cccccc+cccc.62..c...15c$$$c
@+111.....+.63..+..F11+4||c
cccccc+cccc.62..c...15c$$$c
@Fcx6$.xxxc..1..c...25ccccc
cccxxxxxxxc.....c...1$cxxxc
ccxxxxxxxxccc+cccccccccxxxx
xxxxxxxxxxx3$$xxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

#############################################################################
# Insect pit (Lemuel)
#
NAME:   lemuel_insect_pit
TAGS:   no_rotate
ORIENT: float
DEPTH:  D:5-12
SUBST:  1 = 1:70 2:60 3:25
MONS:   goliath beetle w:20 / giant cockroach w:30 / butterfly w:20
MONS:   bumblebee / giant centipede / giant mite w:30
MONS:   killer bee / scorpion / soldier ant w:5
MAP
cccccccc+cccccccc
c...............c
c...............c
c....vvvvvvv....c
c....v11111v....c
c....=11*11=....c
c....v11111v....c
c....vvvvvvv....c
c...............c
c...............c
cccccccc+cccccccc
ENDMAP


#############################################################################
# Troll bridge (Lemuel)
#
NAME:   lemuel_troll_bridge
DEPTH:  D:9-, Depths
ORIENT: float
TAGS:   no_pool_fixup patrolling
KPROP:  1 = no_rtele_into
SUBST:  1 = 1:50 2 3:5
MONS:   troll
MONS:   rock troll
MONS:   iron troll
MAP
xxxxx...........................xxxxxx
xwwwx...........................xxxxxx
xwwwx...........................xxxxxx
xwwwx...........................xxxxxx
xwwwwwwwwwwwwwwwww.wwwwwwwwwww..xxxxxx
xxwwwwwwwwwwwxxxxw.wwwwwwwwwwwwwwwwxxx
xxwwwwwwwwwwwx11xw.wxxxxwwwwwwwwwwwwxx
xxwwwwwwwwwwwx11=...=11xwwwwwwwwwwwwwx
xxxwwwwwwwwwwxxxxw.wx11xwwwwwwwwwwwwwx
xxxxxwwwwwwwwwwwww.wxxxxwwwwwwwwwwwwwx
xxxxx...wwwwwwwwww.wwwwwwwwwwwwwwwwwwx
xxxxx...........................xwwwwx
xxxxx...........................xxwwwx
xxxxx...........................xxwwwx
xxxxx...........................xxxxxx
ENDMAP

##############################################################################
# Anthole (zaba)
# Intentionally used ORIENT: float in order to enforce rooms+corridors.
#
NAME:   zaba_anthole
TAGS:   extra
ORIENT: float
DEPTH:  D:5-9, Lair
MONS:   worker ant / nothing w:5
MAP
   @
 xx+xx
xx...xx
x.111.x
x.111.x
xx...xx
 xx+xx
   @
ENDMAP

##############################################################################
# Lions and Tigers and Bears, oh my! (or at least bears)
#
NAME:   lemuel_bear_cage
ORIENT: float
DEPTH:  D:7-
MONS:   black bear / grizzly bear / polar bear
MONS:   hippogriff / griffon
MONS:   yak / sheep / weight:3 death yak
MONS:   hound / wolf w:5 / hog
SUBST:  1 = 1:30 2 3 4 .
MAP
cccccccccc
c.1.1.1.1c
c1.1.1.1.c
c.1.1.1.1c
c1.1.1.1.c
c.1.1.1.1c
cnn====nnc
..........
@........@
..........
xxxxxxxxxx
ENDMAP

##############################################################################
# Vampire Tower by Lemuel Pitkin
#
NAME:    lemuel_vampire_tower
ORIENT:  float
DEPTH:   D:12-, Depths, !Depths:$, Crypt
MONS:    rat / porcupine / w:6 green rat / w:3 orange rat / w:20 bat
MONS:    flying skull, human zombie
MONS:    vampire, vampire mage, vampire knight
KITEM:   > = any good_item
KFEAT:   > = >
KFEAT:   ; = altar_yredelemnul/altar_kikubaaqudgha
SHUFFLE: defg
SUBST:   d = +, e = x, f = x, g = x
SUBST:   1 = 1 .:20
SUBST:   1 : 1:30 2
SUBST:   w:w.
MAP
F..wwwwwwwwwwwwwwwww..F
..wwwwwwwwwwwwwwwwwww..
.www111xxxx+xxxx111www.
www111xxx22322xxx111www
ww111xx.+23432+.xx111ww
ww11dx..ccccccc..xe11ww
ww1xx..ccc|>|ccc..xx1ww
wwxx..ccccc6ccccc..xxww
wwx..ccc33+4+33ccc..xww
wwx.ccccccc+ccccccc.xww
wwx.c***x.....ccccc4xww
wwx.c***+..5..ccccc.xww
wwx4cc**x4.;.4ccccc.xww
wwx..ccxx.....cccc..xww
wwxx..ccx..4..ccc..xxww
ww1xx..cc.....cc..xx1ww
ww11fx..ccc.ccc..xg11ww
ww111xx..cc+cc..xx111ww
www111xx..333..xx111www
.www111xxxxxxxxx111www.
..wwwwwwwwwwwwwwwwwww..
F..wwwwwwwwwwwwwwwww..F
ENDMAP

##############################################################################
# Swampy vault
#
NAME:    onia_ninara_swampy_vault
TAGS:    no_pool_fixup no_monster_gen
DEPTH:   Depths, !Depths:$, Lair:6-, Snake:1-4
ORIENT:  float
SHUFFLE: AB@
SUBST:   x = .:30 W x:4
SUBST:   A : x, B : x
SUBST:   D = .:30 W
KFEAT:   E = w:20 . / x / w:50 W / w:30 w / ~
KFEAT:   F = w:150 w / w:70 W / ~ / . / x
KFEAT:   G = x / . / w / W
KFEAT:   H = . / W
KMONS:   D = Plant / w:200 nothing
KMONS:   E = w:2 slime creature / swamp worm /\
             w:2 vampire mosquito / w:1 crocodile / w:1000 nothing
KMONS:   F = w:2 slime creature / swamp worm /\
             w:2 vampire mosquito / w:2 swamp drake / w:1000 nothing
KMONS:   H = swamp drake / swamp dragon / hydra
KITEM:   H = any good_item / any potion / gold
: if you.branch() ~= "Snake" and you.absdepth() < 18 then
NSUBST:  H = 3:H / 2 = HH.W / *: .W
: end
MAP
xxxxxxxxxxxxxxxxx@xxxxxxxxxxxxxxx
xxxxxxGGGxxxxxxGEDEExxxxGGxxxxxxx
xxxxGHHFFGGGGFFFEEDEEGGFFFxxGGxxx
xxFFFHHFFFFFGFFFFEEDEEFFFFFFFFGxx
xGGFFFFDDEEFFFFFEEDEEFFFFFFFFFFGx
xxGFFFFEEDDEEFFEEDEEFFFEEEFFFFGxx
xGFFFFFFFEEDEEEDDEEEEEEEDDEEEFFGx
xGGFFFFFFFEEDEDEEEEEEEDDEEDDDEEDB
xxGFFFFFFFFEEDEDDEDDDDEEEEEEEDDxx
xxxGFFFFFFEEDEEEEDEEEEEFFFFFFFFxx
xxxxFFFFFEEDEEFFFFDDFFFFFFFFFGGGx
xxxGFFFFEEDEEFFFFFFEDEFFFFFFFxxxx
xGGFFFFFFDEEFFFFFFFFEDFFFFFFGGxxx
xGFFFFFFEEDEEFFFFFFFFEDEFFFGxxxxx
xxGxxxGFEEDEEFFFFGGGFFHHHGxxxxxxx
xxxxxxxGFEEDEEGGxxxxxGFHGxxxxxxxx
xxxxxxxxGFDEGGxxxxxxxxxGxxxxxxxxx
xxxxxxxxxxAxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Another low-level vault - LP
#
NAME:    lemuel_reptile_caves
DEPTH:   D:10-13, Lair:1-5
ORIENT:  float
MONS:    giant gecko / giant newt, crocodile, iguana
MONS:    fire drake / swamp drake
MONS:    adder, water moccasin
SUBST:   4 = 34
SUBST:   3 = 3:20 2
SUBST:   2 = 2:20 1
SUBST:   1 = 1.
SHUFFLE: ab/cd
SUBST:   a = w, b = x, c = x, d = .
SHUFFLE: 123/566
SUBST:   X=x.
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxXW**wXxxxxxxxxxxxxxxX**XXx
xXwWwwwXxxxxxxX33xxxxxX423Xx
xXWww232XxxxxX121xxxxX.13Xxx
xXXWww2XxxxxxX.1xxxxxw222Xxx
xxXXWwwwaaxxxxXWxxaaaw1121Xx
xxxXWww1XaaaaaWwaaaxXWw211Xx
xxxxXW23xxxxxaWwwxxxXww11XXx
xxxxXX1XxxxxxxxWwwxxxxX.Xxxx
xxxxxxX.XxxxXwWwwwxxx...Xxxx
xxxxxxx..Xxxx2xWwxxX..XXxxxx
xxxxxxxX.12111xw3X211bxxxxxx
xxxxxxxxxxXXXxxW2xxxxbbxxxxx
xxxxxxxxxxxxxxxw1Xxxxxbbxxxx
xxxxxxxxxxxxxXww1Xxxxxxbxxxx
xxxxxxxxxxxXwwww111@...Xxxxx
ENDMAP

##############################################################################
# A guard of gargoyles
#
NAME:    gargoyle_guard
ORIENT:  float
TAGS:    patrolling no_monster_gen
DEPTH:   D:10-, !D:$
# you should be able to arrive both via stairs and via corridor
SUBST:   { = {([)]}
MONS:    gargoyle, metal gargoyle / gargoyle
MAP
xxxxxxxxxxxxxxxxxxx
xxxxxxxxx2xxxxxxxxx
xxxxx1x1x.x1x1xxxxx
xxxxx.........xxxxx
xxxx...........xxxx
xxxx...........xxxx
xxx...G..{..G...xxx
xxxx...........xxxx
xxxx...........xxxx
xxxxx.........xxxxx
xxxxx1x1x.x1x1xxxxx
xxxxxxxxx.xxxxxxxxx
xxxxxxxxx.xxxxxxxxx
xxxxxxxxx.xxxxxxxxx
xxxxxxxxx@xxxxxxxxx
ENDMAP

##############################################################################
# Ogre Cave, by Lemuel
#
NAME:    lemuel_ogre_cave
ORIENT:  float
DEPTH:   D:9-, !D:$, Orc
TAGS:    no_pool_fixup
MONS:    ogre, two-headed ogre, ogre mage / Erolcha, plant / fungus
MONS:    ogre zombie, two-headed ogre zombie
SHUFFLE: WX/ZY , 12/12/67
SUBST:   W:ww. , X:. , Z:x , Y:+.
SHUFFLE: *5/WW
SUBST:   *=. , W=wW , !:$.
SUBST:   5 = 5 6 + ^:5 I:5 F:5
KFEAT:   A = >
KITEM:   A = $
MAP
              xxxxx
          xxxxxx12xx
         xx*..x.122xx
 xxxxxxxxx5xxx...11xx
xxWxxxxxxx*xxxxx>...xx
xxWWxxxxxx.xxxxxx**xxxx
xWWWxxxxxx.xxxxxxx**xxxxx
WWWW**xxxxx.xxxxxxx5xxxxxxxx
xWWWW..xxxxx.xxxxxx.xccccccx
xxWW....1xxx.xxxxx.xxc$$$Acx
@..X...111..xxxxxx.xxc$$$$cx
xxWWW...11xxxxxxx.xxxc!!!!cx
xWWW....*xxxxxxx*..xxc+ccccx
xxWWW..**xxxxxx*....x..xxxxx
 xxWWWW*xxxxxxx*.11.Y.xx
  xxWWWWWxxxxxxx1221xxx
  xxxxWWWWxxxxxxx23xx
  xxxxxWWxxxxxxxxx|xx
  xxWWWWxxxxxxxxxxxx
  WWWWxxx
ENDMAP

##############################################################################
# Slaugherhouse (Zaba)
#
# Butchers have either knives (daggers) or hand axes. They have some chance
# of opening the doors so animals can go for a walk, that's intended.
#
NAME:   slaughterhouse
DEPTH:  Lair:7-, D:11-
TAGS:   no_monster_gen
ORIENT: float
WEIGHT: 5
MARKER: F = lua:fog_machine { \
                pow_max = 10, delay_min = 10, delay_max = 40, \
                size = 4, size_buildup_amnt = 4, \
                size_buildup_time = 25, cloud_type = "freezing vapour" \
            }
MONS:   death yak w:5 / sheep w:3 / yak w:7
MONS:   generate_awake human ; dagger | hand axe
SUBST:  F=d, 2=2.
ITEM:   meat ration w:5 / sausage / nothing
MAP
 cccccc       ccccccc
 cddddcxxxxxxxxcddddc
 cdFdd+..+..+..+ddFdc
 cddddcxxx..xxxcddddc
 cccccc..x..x..cccccc
.........x2.x.........
.nnnnnn..x..x..nnnnnn.
.n1111nxxx..xxxn1111n.
.n1111+..+..+..+1111n.
.n1111nxxx..xxxn1111n.
.nnnnnn..x..x..nnnnnn.
.........x.2x.........
 xxxxxx..x..x..xxxxxx
 x...2xxxx..xxxx2...x
 x..2.+..+..+..+.2..x
 x...2xxxx..xxxx2...x
 xxxxxx  x..x  xxxxxx
         x++x
          @.
ENDMAP

##############################################################################
# The Shining One Protects a Powerful Item, vault series by abrahamwl
#
# The basic theme of these vaults is that the Shining One has placed a
# powerful item under the guard of an angel or daeva.

NAME:    abrahamwl_protected_by_tso_1
TAGS:    patrolling no_monster_gen no_item_gen no_pool_fixup
ORIENT:  float
WEIGHT:  4
DEPTH:   Depths
MONS:    generate_awake angel / generate_awake daeva / w:1 angel / w:1 daeva
: init_protected_by_tso(_G)
MAP
         xxxxx
   xxxxxxxcccxxxxxxx
   xccccccc1cccccccx
   xc|...-...-...|cx
   xcccccc...ccccccx
   xxxxxcc...ccxxxxx
     xxxcG...Gcxxx
    xxxxcc...ccxxxx
    xccccc...cccccx
    xcxxxx---xxxxcx
    xc...........cx
    xc...........cx
    xc..G.....G..cx
    xc...........cx
    xc.....1.....cx
    xc...........cx
    xc..G.....G..cx
    xc...........cx
    xc...........cx
    xcccccc-ccccccx
    xxxxxxx!xxxxxxx
       xxxx+xxxx
           @
ENDMAP

NAME:    abrahamwl_protected_by_tso_2
ORIENT:  float
TAGS:    patrolling no_monster_gen no_item_gen no_pool_fixup
WEIGHT:  4
DEPTH:   Depths
MONS:    generate_awake angel / generate_awake daeva / w:1 angel / w:1 daeva
KFEAT:   A = altar_the_shining_one
KITEM:   A = w:1 acquire:the_shining_one any / nothing
: if crawl.one_chance_in(3) then
SUBST:   | = .
SUBST:   % = |
SUBST:   ? = +
: else
SUBST:   % = c
SUBST:   ? = c
: end
COLOUR:  _ : yellow
FTILE:   _A : floor_limestone
SUBST:   T = T G:1
: init_protected_by_tso(_G)
MAP
    xxxxxxxxxxxxxxx
  xxxcccccccccccccxxx
  xccc_____A_____cccx
  xc%?Wwwww_wwwwW+|cx
  xcccxWwww_wwwWxcccx
  xxxcxxWWw_wWWxxcxxx
   xccxxxx._.xxxxccx
   xxcxxxx---xxxxcxx
    xc...........cx
    xc...........cx
    xc..G.....G..cx
    xc...........cx
    xc.....1.....cx
    xc...........cx
    xc..G.....G..cx
    xc...........cx
    xc...........cx
    xcccccc-ccccccx
    xxxxxxx!xxxxxxx
       xxxx+xxxx
           @
ENDMAP

NAME:    abrahamwl_protected_by_tso_3
ORIENT:  float
TAGS:    patrolling no_monster_gen no_item_gen no_pool_fixup
TAGS:    uniq_protected_by_tso
WEIGHT:  4
DEPTH:   Depths
MONS:    generate_awake angel / generate_awake daeva / w:1 angel / w:1 daeva
KFEAT:   A = altar_the_shining_one
KITEM:   A = w:1 acquire:the_shining_one any / nothing
: if crawl.one_chance_in(3) then
SUBST:   | = .
SUBST:   % = |
SUBST:   ? = +
: else
SUBST:   % = c
SUBST:   ? = c
: end
COLOUR:  _ : yellow
FTILE:   _A : floor_limestone
SUBST:   T = T F:1
MAP
      xxxxxxxxxxx
     xxxxxcccxxxxx
    xxxxxxc|cxxxxxx
    xcccccc+ccccccx
   xxcWWWWTATWWWWcx
  xxxcW.._____..Wcx
  xcccWt..___..tWcx
  xc%?W...___...Wcx
  xcccWt..___..tWcx
  xxxcxxxx---xxxxcx
    xc...........cx
    xc...........cx
    xc..G.....G..cx
    xc...........cx
    xc.....1.....cx
    xc...........cx
    xc..G.....G..cx
    xc...........cx
    xc...........cx
    xcccccc-ccccccx
    xxxxxxx!xxxxxxx
       xxxx+xxxx
           @
ENDMAP

##############################################################################
# Eyes! by Minmay
#
NAME:   minmay_eyes
DEPTH:  Depths
ORIENT: float
KFEAT:  m = iron_grate
MONS:   eye of devastation / great orb of eyes w:2
MONS:   golden eye / eye of draining w:2
MONS:   giant orange brain
MAP
...................................
.cccccccccccc.........cccccccccccc.
.c.............................m2c.
.c.1.............1.............mmc.
.c...............................c.
.c...cccccccccnnnnnnnccccccccc...c.
.c...c.......................c...c.
.c...c.1.........1.........1.c...c.
.c...c.......................c...c.
.c...c...ccccccnnnnncccccc...c...c.
.c...c...c...............c...c...c.
.c...c...c.1.....1.....1.c...c...c.
.c...c...c...............c...c...c.
.n...c...c...cccmmmccc...c...c...n.
.n...n...c...c3.....3c...c.......n.
.n...n...n...c..%.%..c...n.......n.
.n...n...n.....%|*|%.m...n.......n.
.n.1.n.1.n.1....*|*..m.1.n.1...1.n.
.n...n...n.....%|*|%.m...n.......n.
.n...n...n...c..%.%..c...n.......n.
.n...n...c...c3.....3c...c.......n.
.n...c...c...cccmmmccc...c...c...n.
.c...c...c...............c...c...c.
.c...c...cmm.....1.....1.c...c...c.
.c...c...c2m.............c...c...c.
.c...c...cccccc.....cccccc...c...c.
.c...c.......................c...c.
.c...c.1.........1.........mmc...c.
.c...c.....................m2c...c.
.c...cccccccccnnnnnnnccccccccc...c.
.c...............................c.
.c.1.............1.............1.c.
.c...............................c.
.ccccccccccccnnnnnnnnncccccccccccc.
...................................
ENDMAP

##############################################################################
# Elemental Laboratory (by Mu.)
#
NAME:   mu_elemental_laboratory
DEPTH:  Depths, Elf, !Elf:$
ORIENT: float
TAGS:   no_monster_gen no_item_gen no_pool_fixup
MARKER: ! = lua:fog_machine { \
                pow_max = 10, delay_min = 10, delay_max = 40, \
                size = 2, size_buildup_amnt = 5, \
                size_buildup_time = 25, cloud_type = "flame" \
            }
MARKER: ? = lua:fog_machine { \
                pow_max = 10, delay_min = 10, delay_max = 40, \
                size = 2, size_buildup_amnt = 5, \
                size_buildup_time = 25, cloud_type = "thin mist" \
            }
MARKER: * = lua:fog_machine { \
                pow_max = 10, delay_min = 10, delay_max = 40, \
                size = 2, size_buildup_amnt = 5, \
                size_buildup_time = 25, cloud_type = "black smoke" \
            }
KMONS:  1 = fire elemental
NSUBST: ; = 3:2 / *=.ll
KMONS:  2 = molten gargoyle
KMONS:  e = earth elemental
SUBST:  E = exxx....
NSUBST: ' = 3:3 / 3:W / *:.
KMONS:  3 = clay golem
NSUBST: w = 3:4 / *:w
KMONS:  4 = water elemental
KFEAT:  4 = deep_water
NSUBST: " = 3:5 / *:.
KMONS:  5 = vapour
KMONS:  6 = air elemental
KMONS:  7 = fire vortex
#
: if you.in_branch("elf") then
KMONS:  Z = col:flash deep elf sorcerer \
        name:deep_elf_elementalist n_rpl n_des n_noc \
        tile:mons_deep_elf_elementalist \
        spells:iron_shot;summon_air_elementals;sticky_flame_range;\
        summon_water_elementals;haste;blink actual_spells \
        ; robe ego:fire_resistance race:elven | \
        robe ego:cold_resistance race:elven | \
        robe ego:resistance race:elven . dagger ego:freezing race:elven | \
        dagger ego:flaming race:elven | dagger ego:electrocution race:elven
: else
KMONS:  Z = col:flash wizard hd:15 tile:mons_master_elementalist \
        name:master_elementalist n_rpl n_des n_noc \
        spells:iron_shot;summon_air_elementals;sticky_flame_range;\
        summon_water_elementals;haste;blink actual_spells \
        ; robe ego:fire_resistance | robe ego:cold_resistance | \
        robe ego:resistance \
        . dagger ego:freezing | dagger ego:flaming | \
        dagger ego:electrocution
: end

KITEM:  B = randbook disc:air / randbook disc:fire / randbook disc:ice / \
            randbook disc:earth
KITEM:  | = staff of fire / staff of cold / staff of earth / staff of air / \
            staff unrand:elemental_staff
MAP
       ccccccccc
      ccE+'''cwcc
     ccEEc'''cwwcc
    ccEEEc'''cwwwcc
   ccEEEEc'''cwwwwcc
  ccEEEEEc'''cwwwwwcc
 ccEEEEEEc''Wcwwwwwwcc
ccEEEEEEEc'WW+wwwwwwwcc
cEEEEEEEEcccccwwwwwwwwc
ccccccc+ccB|Bcccccccc+c
c;;;;;;;c$...$c"""""""c
c;;;;;;;c$.Z.$c""?"?""c
c;;;;;;;c$...$c"""""""c
c+cccccccc...cc+ccccccc
c........c+++c........c
cc..!1.!1c...c.6..6..cc
 cc......c7.7c......cc
  cc.....c...c.....cc
   cc.1!.c.*.c.6..cc
    cc...c...c...cc
     cc..c.7.c..cc
      cc.c...+.cc
       c+ccccccc
        @
ENDMAP

##############################################################################
# Guardian serpents guard a snake-like passage. (nrook)
#
NAME:    nrook_guardian_serpent_snake_passage
ORIENT:  float
DEPTH:   D:9-, Depths, Snake
TAGS:    no_monster_gen no_rotate
MONS:    patrolling guardian serpent
SHUFFLE: yY/zZ
SUBST:   y=., Y=+, Z=x, z=b
COLOUR:  *. = lightgreen
SUBST:   ' = .
MAP
            xxxxxxxxxxxxxxx
            xxxxbbbbbbbbxxx'
            xxbbb..*...bbbx'
            xbb...bbbb...yY1
            xb..bbbbbbbbbbx@
            xb.bbxxxxxxxxxx'
            xb..bbbbxx
            xbb.*..bbx
            xxbbbb..bx
 xxxxxxxxxxxxxxxxbb.bx
'xbbbbxxxxxxxxxxxbb.bx
1Zz..bbbxxxxxxxbbb..bx
@xbb...bbbbbbbbb.*.bbx
'xxbbb...*.......bbbxx
 xxxxbbbbbbbbbbbbbxxxx
ENDMAP

##############################################################################
# Ice Spiral vaults by Lemuel
# There is a path to the center that the freezing clouds never appear on.
# It's pretty obvious. -LP
NAME:    lemuel_ice_spiral_1
WEIGHT:  1
ORIENT:  float
TAGS:    no_item_gen no_monster_gen
MONS:    ice beast / nothing
ITEM:    nothing, nothing
SHUFFLE: 1ef
SUBST:   * : *$
SUBST:   * = ***.|
MARKER:  1 = lua:fog_machine { cloud_type = "freezing vapour", \
             pow_min = 2, pow_max = 4, delay = 17, start_clouds = 1, \
             size = 2, walk_dist = 0, spread_rate= 0 }
MARKER:  e = lua:fog_machine { cloud_type = "freezing vapour", \
             pow_min = 2, pow_max = 4, delay = 19, start_clouds = 1, \
             size = 2, walk_dist = 0, spread_rate= 0 }
MARKER:  f = lua:fog_machine { cloud_type = "freezing vapour", \
             pow_min = 2, pow_max = 4, delay = 23, start_clouds = 1, \
             size = 2, walk_dist = 0, spread_rate= 0 }
MAP
mG++Gmmmmmmmmmmmmmmmmmm
m......f..1..1...e...fm
m....1...f..e..f...1..m
m......e..............m
m....f...............em
m......1..............m
m....1.....e.1.f......m
m...........f.e.1....1m
m......e..............m
m....e......***.......m
m......1....***.f....fm
m.....................m
m....f1..e1.f..1e.....m
m....................em
m.....................m
m.....................m
m....................1m
mf..1..e..f..e..f..e..m
mmmmmmmmmmmmmmmmmmmmmmm
ENDMAP

# Wait for the freezing cloud to disappear, then run to the next gap,
# then wait. It creates an agreeable sense of tension. -LP
#
NAME:    lemuel_ice_spiral_2
ORIENT:  float
DEPTH:   D:5-, Depths
TAGS:    no_item_gen uniq_spiral layout_rooms layout_city
MONS:    nothing
ITEM:    nothing, nothing
SHUFFLE: Bb/Mm/Mm/Mm
COLOUR:  B = blue
SUBST:   M=. , m=n, B=. , b=c
SUBST:   + : +=
SUBST:   * : *$
MARKER:  1 = lua:fog_machine { cloud_type = "freezing vapour", \
              pow_min = 5, pow_max = 5, delay = 120, start_clouds = 1, \
              size = 2, walk_dist = 0, spread_rate= 0 }
MARKER:  e = lua:fog_machine { cloud_type = "freezing vapour", \
              pow_min = 9, pow_max = 9, delay = 200, start_clouds = 1, \
              size = 2, walk_dist = 0, spread_rate= 0 }
MAP
mG+Gmmmmmmmmmmmmmmmmm
mBBB111B111BeeeeeeeBm
mmmmmmmmmmmmmmmmmmmBm
m*****eeeeeee111B111m
mmmmmmmmmmmmmmmmmmmmm
ENDMAP

##############################################################################
# Flame loot (Lemuel)
#
NAME:   lemuel_Flame_loot_2
DEPTH:  D:9-, Depths
ORIENT: float
ITEM:   nothing
SUBST:  * = |**...
SUBST:  * : *$
SUBST:  L = ll.
MONS:   molten gargoyle / fire drake / lindwurm / nothing w:20
MARKER: d = lua:fog_machine { cloud_type = "flame", \
             pow_min = 10, pow_max = 10, delay = 10, \
             size = 2, walk_dist = 0, spread_rate= 0 }
MAP
ccccc...ccccc
ccccc...ccccc
ccccc...ccccc
ccccc.d.ccccc
ccccc...ccccc
ccccc...ccccc
ccccc.d.ccccc
ccccc...ccccc
ccccc...ccccc
ccccc...ccccc
c...LLlLL...c
c..LLlllLL..c
c.LLll*llLL.c
cLLll***llLLc
cLll**1**llLc
cLlll***lllLc
cLLlll*lllLLc
c.LLlllllLL.c
c..LLlllLL..c
ccccccccccccc
ENDMAP

##############################################################################
# Firewalk (Lemuel)
#
NAME:    lemuel_firewalk_3
DEPTH:   Depths, Abyss
TAGS:    uniq_firewalk ruin_abyss
WEIGHT:  2
ORIENT:  float
ITEM:    nothing, nothing, nothing
SHUFFLE: def
MARKER:  d = lua:fog_machine { cloud_type = "flame", \
             pow_min = 2, pow_max = 4, delay = 39, start_clouds = 1, \
             size = 2, walk_dist = 2, spread_rate= 0 }
MARKER:  e = lua:fog_machine { cloud_type = "flame", \
             pow_min = 2, pow_max = 6, delay = 59, start_clouds = 1, \
             size = 2, walk_dist = 2, spread_rate= 0 }
MARKER:  f = lua:fog_machine { cloud_type = "flame", \
             pow_min = 2, pow_max = 8, delay = 79, start_clouds = 1, \
             size = 2, walk_dist = 2, spread_rate= 0 }
MAP
xxxxxxxG@Gxxxxxxx
x...............x
x...............x
x.d..e.....f..d.x
x...............x
x...............x
x...............x
x.e..f.....d..e.x
x...............x
x...............x
x...............x
x.f..d.....e..f.x
x...............x
x...............x
x...............x
x.d..e.....f..d.x
x...............x
x...............x
x......$$$......x
x.e.f..$$$..d.e.x
x......$$$......x
xxxxxxxxxxxxxxxxx
ENDMAP

#############################################################################
# The Elephants' Graveyard (by Mu.)
#
NAME:   mu_elephants_graveyard
ORIENT: float
TAGS:   no_monster_gen no_rotate no_vmirror
DEPTH:  Depths, Lair:1-7
  # next: we generate more dire elephants at greater depths
  # we also turn the shallow water into deep water at the lowest depths
: if you.branch() == "Lair" then
MONS:   elephant, fungus, plant
: else
MONS:   w:1 elephant / dire elephant, fungus, plant
SUBST: W : w
:end
: dgn.delayed_decay(_G, '%', 'w:1 elephant corpse / w:5 elephant skeleton / nothing')
SUBST:  . = .:69 1:1
SUBST:  P = 3
SUBST:  a = .2
SUBST:  b = 23
SUBST:  c = 3tt
MAP
............................
............................
............................
............................
........tttttttttttt........
......tttccccccccccttt......
.....ttcccbbbbbbbbccctt.....
....ttccbbbaaaaaabbbcctt....
....tccbbaaa...1aaabbcct....
...ttcbbaa...%%...aabbctt...
..ttccbaa..%%%%%%..aabcctt..
..tccbba..%%%%%%%%..abbcct..
..tcbbaa.%%.PPP%%%%.aabbct..
..tcbaa..%%.PWPP%%%..aabct..
..tcba1.%%%11WPP%%%%..abct..
..tcba.%%%%11WWP%%%%%.abct..
..tcba.%%%%.WWWP%%%%%.abct..
..tcba..%%%.WWWP%%%%1.abct..
..tcbaa.1%%..WWP.%%..aabct..
..tcbbaa.%%%..WP.%%.aabbct..
..tccbba..%%%.WW....abbcct..
..ttccbaa......WW..aabcctt..
...ttcbbaa......W.aabbctt...
....tccbbaaaa..aaaabbcct....
....ttccbbbba..abbbbcctt....
.....ttccccca..accccctt.....
......tttttta..atttttt......
............................
............1..1............
............................
.............@@.............
ENDMAP

##############################################################################
# Boring beetle workplace (evilmike)
#
NAME:   evilmike_boring_beetle
DEPTH:  Depths
ORIENT: float
NSUBST: m = 4:| / 4:* / 9:0 / 4:9 / 2:8 / 10:. / 150:x / *:m
SUBST:  n = m
MONS:   generate_awake boring beetle
MAP
vvvvvvvvvvvvvvvvvvv
vmmmmmmmmmmmmmmmmmv
vmmmmmmmmmmmmmmmmmv
vmmmmmmmmmmmmmmmmmv
vmmmmmmmmmmmmmmmmmv
vmmmmmmmmmmmmmmmmmv
vmmmmmmmmmmmmmmmmmv
vnmmmmmmmmmmmmmmmmv
vnnmmmmmmmmmmmmmmmv
@nnnmmmmmmmmmmmmm1v
vnnmmmmmmmmmmmmmmmv
vnmmmmmmmmmmmmmmmmv
vmmmmmmmmmmmmmmmmmv
vmmmmmmmmmmmmmmmmmv
vmmmmmmmmmmmmmmmmmv
vmmmmmmmmmmmmmmmmmv
vmmmmmmmmmmmmmmmmmv
vmmmmmmmmmmmmmmmmmv
vvvvvvvvvvvvvvvvvvv
ENDMAP

##############################################################################

NAME:    hangedman_screamer
ORIENT:  float
TAGS:    no_monster_gen
DEPTH:   D:9-, Depths, Crypt
MONS:    patrolling flying skull
MONS:    zombie / skeleton
: if you.absdepth() > 19 then
MONS:    vampire knight / ghoul / large abomination / phantasmal warrior
KMONS:   4 = Boris / lich / unborn
KMONS:   5 = lich / unborn
: elseif you.absdepth() > 13 then
MONS:    skeletal warrior / plague shambler / eidolon
KMONS:   4 = Frances / vampire mage / deep dwarf necromancer
KMONS:   5 = vampire mage / deep dwarf necromancer
: else
MONS:    wight / necrophage / shadow w:5
KMONS:   4 = Josephine / skeletal warrior / necromancer
KMONS:   5 = skeletal warrior / necromancer
: end
SHUFFLE: Aa / Bb / Cc / Dd / Ee
SUBST:   4 = 4 3:2, 2 = 2 3:2, ABCD = ., abcd = @, Ee = x
: if you.branch() == "Crypt" then
SUBST:   x = c
: end
MAP
    xxxxxxxxx
    x2233322x
xaaxx22x5x22xxccx
xAAx.x2x+x2x.xCCx
xAAxx.xx1xx.xxCCx
xAAxxx.x*x.xxxCCx
x...............x
x...............x
xBBxxx.x*x.xxxDDx
xBBxx.xx1xx.xxDDx
xBBx.x2x+x2x.xDDx
xbbxx22x4x22xxddx
    x2233322x
    xxxxxxxxx
ENDMAP

##############################################################################

NAME:   nicolae_whirlpool
TAGS:   patrolling no_pool_fixup no_rotate no_monster_gen
ORIENT: float
MONS:   centaur, centaur warrior
DEPTH:  D:3-, Depths
: if you.branch() == "D" then
SUBST:  1 = ..1
SUBST:  2 = 1
: end
KMONS:  ' = plant / nothing w:100
SUBST:  ' = W
KFEAT:  @ = W
MAP
@ww@wwwwwwww@
ww'ww''''''ww
w'ww'wwwwww'w
w'w'ww''''ww@
w'w'w'wwww'ww
w'w'w1.1'ww'w
w'w'w.2.w'w'w
w'ww'1.1w'w'w
ww'wwww'w'w'w
@ww''''ww'w'w
w'wwwwww'ww'w
ww''''''ww'ww
@wwwwwwww@ww@
ENDMAP

##############################################################################

NAME:   wad_bat_island
DEPTH:  D:8-, !D:$
ORIENT: float
WEIGHT: 4
FTILE: >.12t = floor_grass
COLOUR: .12 = green
FTILE: ; = floor_grass_dirt_mix
COLOUR: ; = brown
MONS:   plant, bat, shark
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxx>xxxxxwwwwwwxxxwxwxxxxxxxwxxwxxxwwwxxxxxx
xxxx1;WWxwwwwwwwwwwwwwxxxxxwwwwwwwwwwwwxxxxx
xxxxx;1Wxxxxwwwwwwwwwwwwwwxxwwwwxwwwwwwwwwxx
xxx21;WWwwxxxwwwwwwwwwwwwwwwwwwxxxwwwwwwwwwx
xxxt;WWwwwwwwwwwwWWWWWwww3wwwwwwxwwwwwwwwwwx
xx1;WWwwwww3xwwwWW;;;WWwwwwWWwwwwwwwwwwwwwxx
xx;WWwwwwxwwwwwWW;2.t;WWwwWwwWwwwwwWWWWwwxxx
xxWWwwwwxxwwwwWW;.12.2;WWWwwwwWWWwWxxxxWwwxx
xxxwWWWxxxwwwwWW;t..1t;WWwwwwwwwwWxxxwwwWwwx
xxxwwwwWWxwwwwWW;2tt2.;WWwwww3wwwwwwwwwwWwwx
xxwwwwwwwWxWWWwWW;.1t;WWwwwwwwwwwwwwwwwxWwwx
xwwww3wwwwWwwwwwWW;;;WWwwwwwwxxwwwwwwwxWwxwx
xxwwwwwwwwwwxwwwwWWWWWwwwwwxxxxwwwwwwwWwwwxx
xxwwwwxxwwwxxxwwwwwwwwwwwwwwwwwwwwwwwwWwwwwx
xxwwwxxxwwwwxwwwwwwwwWWWWwwwwwwwwwWWwWWwwwxx
xxwwxxxwwwwwwxxwwwwWWxxxxWWWwwwWWWwwWxwwwxxx
xxxxxxwwwwwxxxwxwwWxxxwxwwwwWWWwxxxxxwwxxxxx
xxxxxxxwxxxxxwxxx@xxxxxwxxxxxxwxwxwxxxxwwxxx
ENDMAP

###############################################################################

NAME:    hangedman_surveillance_ring
TAGS:    no_monster_gen no_item_gen transparent patrolling
ORIENT:  float
DEPTH:   Depths, !Depths:$
WEIGHT:  3
MONS:    generate_awake giant eyeball, eye of devastation, giant orange brain
MONS:    generate_awake golden eye, slime creature, brown ooze
KFEAT:   > = known shaft trap
KMASK:   ' = !no_monster_gen
KMASK:   ' = !no_item_gen
SHUFFLE: {[(
SUBST:   ( = (:70 <, [ = [:50 <
MAP
  '''''''''''''''''
  ''''''cnnnc''''''
''''ccccc.}.ccccc''''
'''cc%.c..4..c.%cc'''
''cc%..+.cnc.+..%cc''
''c%.3%cnc6cnc%3.%c''
''c..%cc.....cc%..c''
''cc+cc.......cc+cc''
'cc..n.........n..cc'
'n..cc....<....cc..n'
'n.1n2...[{(...2n1.n'
'n..cc....>....cc..n'
'cc..n.........n..cc'
''cc+cc.......cc+cc''
''c..*cc.....cc*..c''
''c*55*ccc.ccc*55*c''
''cc*54n.+++.n45*cc''
'''cc*.n..2..n.*cc'''
''''ccccc...ccccc''''
  ''''..c+++c..''''
  ''''.........''''
ENDMAP

###############################################################################

NAME:    hangedman_opposites_gauntlet
TAGS:    no_monster_gen patrolling
ORIENT:  float
DEPTH:   Depths, !Depths:$
MONS:    spriggan rider ; wand of frost . crossbow ego:flame . bolt q:25 / \
         spriggan rider ; wand of flame . crossbow ego:frost . bolt q:25
MONS:    two-headed ogre ; giant club ego:holy_wrath . \
         great mace ego:draining . wand of confusion | wand of slowing
MONS:    rakshasa ; scythe ego:anti-magic | trident ego:anti-magic . \
         dart ego:exploding q:25 / mermaid ; chain mail . javelin ego:poisoned q:6
MONS:    fire giant ; battleaxe ego:freezing
MONS:    frost giant ; great sword ego:flaming
MONS:    ettin ; giant club ego:holy_wrath . great mace ego:draining . \
         wand of confusion | wand of slowing / golden dragon
MONS:    vampire knight ; wand of lightning | wand of draining / \
         greater naga ; chain mail . halberd ego:anti-magic | glaive ego:anti-magic
KMONS:   A = fire drake / flaming corpse / fire elemental
KMONS:   B = lindwurm / mottled dragon / efreet w:5
KMONS:   C = fire crab / hell knight
KMONS:   D = simulacrum w:5 / polar bear
KMONS:   E = harpy simulacrum w:5 / snapping turtle simulacrum w:5 / ice dragon
KMONS:   F = hydra simulacrum w:5 / golden dragon simulacrum w:5 / azure jelly
KMONS:   G = insubstantial wisp / air elemental w:5
KMONS:   H = air elemental / sixfirhy / vapour w:5
KMONS:   I = spriggan air mage / storm dragon
KMONS:   J = stone golem w:5 / earth elemental w:5 / basilisk
KMONS:   K = catoblepas / iron golem w:5 / boulder beetle
KMONS:   L = iron dragon / crystal golem
KMONS:   M = orc warrior ; long sword ego:holy_wrath / holy swine / \
             merfolk ; trident ego:holy_wrath
KMONS:   N = paladin / apis / phoenix w:5
KMONS:   O = angel / cherub
KMONS:   P = hellwing / red devil / hell hound
KMONS:   Q = hell beast / smoke demon / ynoxinul
KMONS:   R = shadow demon / reaper / cacodemon w:5
KMONS:   S = eidolon / skeletal warrior / zombie w:5
KMONS:   T = flayed ghost / guardian mummy / plague shambler
KMONS:   U = death ooze / bone dragon / profane servitor w:5
KMONS:   V = elephant / very ugly thing / wolf spider
KMONS:   W = dire elephant / alligator / emperor scorpion
KMONS:   Y = anaconda / alligator snapping turtle
KMONS:   X = rock troll / orc knight / hill giant
KMONS:   Z = iron troll / naga warrior / minotaur
KMONS:   a = spriggan defender w:5 / merfolk impaler w:5 / orc warlord
KMONS:   d = deep elf mage / wizard / boggart
KMONS:   e = deep elf conjurer / ogre mage / great orb of eyes
KMONS:   f = deep elf annihilator / sphinx w:5 / lich w:5
KMONS:   g = centaur / yaktaur / deep dwarf artificer w:5
KMONS:   h = centaur warrior / yaktaur captain / cyclops
KMONS:   i = stone giant / merfolk javelineer w:5
KFEAT:   ! = granite_statue
KFEAT:   u = fountain_sparkling
KMASK:   @' = !no_monster_gen
KITEM:   | = superb_item, star_item / any w:20
: if you.absdepth() < 20 then
SHUFFLE: ABDE / GHJK / MNPQ / VWde, PQ / ST, VW / XZ, de / gh
: else
SUBST: B = C, A = B, E = F, D = E, H = I, G = H, K = L, J = K
SUBST: N = O, M = N, Q = R, P = Q, 2 = 6, 3 = 7
SHUFFLE: BCEF / HIKL / NOQR / WYef, QR / TU, WY / Za, ef / hi
NSUBST:  1 = 1:4 / 1:5
: end
MAP
      vvvcbbb
    vvv..2..bbb
   vv1...u...1bb
  vv...b...v...bb
 vv...bb+c+vv...bb
vv...bb.....vv...bb
v|3bbb.......vvv3|b
v|bbM.........Pvv|b
bbbNMvvv...bbbPQvvv
vvvvvvvv+c+bbbbbbbb
bbbHGvvv...bbbJKvvv
b*bbG.........Jvv*v
b*Abbb.......vvvD*v
bBA..bb.....vv..DEv
vvv...bb+c+vv...bbb
b$vv...b...v...bb$v
b$$vv.........bb$$v
bb$.vvv.....bbb.$vv
 bb..!v++c++b!..vv
  bb...........vv
   bbb.u.}.u.vvv
     bnbbnvvnv
     '@'''''@'
ENDMAP

###############################################################################
# Nasty temple of Zin. Note that the boss is locked in a room until
# the player opens it... and the player can see it ahead of time.

NAME:   evilmike_silver_temple
TAGS:   no_monster_gen
DEPTH:  Depths, !Depths:$
ORIENT: float
MONS:   daeva god:Zin, angel god:Zin dbname:zin_angel, cherub god:Zin
: if you.depth() > 3 then
MONS:   silver star
: else
KMONS:  4 = Mennas, angel god:Zin dbname:zin_angel
: end
NSUBST: A = 3 / '
KFEAT:  _ = altar_zin
FTILE:  '+_!$%*|1234D = floor_vault
TILE:   v = dngn_metal_wall_white
TILE:   c = dngn_transparent_wall_white
COLOUR: v = white
KFEAT:  D = runed_door
SUBST:  !' = .
: set_feature_name("metal_wall", "silver wall")
MAP
............@@@@@@@............
....vvvvvvv...T.T...vvvvvvv....
...v.v%%%vv.........v%%%%v.v...
..v.vv'A'vvv..T.T..vv%vv%vv.v..
.v.v.v'''vvv.......vv3''2v.v.v.
.vvvvv'''vvvv.T.T.vvvv''vvvvvv.
.v'2'+'''vvvv.....vvv''''v'''v.
.v'''vvvvvvvvv...vvvv'vv'''v'v.
.v'''v'''vvvvv...vvvv'vv'''v'v.
.v'''v'''vv3'vv.vv'3v''''v'''v.
.v'''+'''vv''vvDvv''vvvvvvvv+v.
.v+vvv'A'vv+vvv'vvv+vvvvvvvv1v.
.v1'vv%%%v'''''''''''vvvvvv''v.
.vv''vvvvv'v'v'''v'v'vvvvv''vv.
..vv'''''+'''''''''''+'''''vv..
.v.vv''vvv'v'v'''v'v'vvv''vv.v.
.vv.vv''vv'''''''''''vv''vv.vv.
.....vv''vvvv'''''vvvv''vv.....
...v.vvv''vvv'2'1'vvv''vvv.v...
.v...vvvv''vvv'''vvv''vvvv...v.
...v.vvvvv'3vvvnvvv3'vvvvv.v...
.v...vvvvvvDv'''''Dvvvvvvv...v.
...v.v$$$$v''''4''''v****v.v...
.v...v$$$$D'''v_v'''D*||*v...v.
...v.v$$$$vvvvvvvvvvv****v.v...
.....vvvvvvvvvvvvvvvvvvvvv.....
...............................
ENDMAP

###############################################################################

NAME:    hangedman_six_sided_sadism
TAGS:    patrolling no_monster_gen no_item_gen
DEPTH:   Depths
ORIENT:  float
MONS:    centaur warrior / yaktaur w:5 / orc knight w:15 / \
         deep elf conjurer / deep troll / boggart w:5 / efreet w:5 / \
         rakshasa w:5 / wizard / necromancer / spriggan rider w:5 / \
         vampire / octopode w:5 ; trident good_item
MONS:    orc high priest / orc sorcerer / naga warrior / naga mage / \
         skeletal warrior / yaktaur captain w:5 / ogre mage / \
         deep elf knight / phantasmal warrior / guardian mummy w:5 / \
         guardian serpent w:5 / harpy w:5
MONS:    fire giant / frost giant / stone giant w:5 / ettin / \
         hell knight w:5 / orc warlord / \
         minotaur w:5 ; any armour good_item . any weapon good_item / \
         vampire knight / vampire mage / ghoul w:5 / base draconian / \
         spriggan defender w:3 / spriggan air mage / siren w:2 / \
         deep elf death mage
MONS:    titan / lich / deep elf annihilator / deep elf sorcerer / \
         sphinx w:5 / greater naga w:5 / mummy priest w:1 / \
         nonbase draconian w:1
SHUFFLE: AB, CD, EF, GH, IJ, KL / MN, OP / QR, ST, ST / UV
SUBST:   ACEG : x, BDFH : ., c : cccvb, x : x:70 cb
SUBST:   IKO = 1, LPS = 2, T = 3, JMNQRUV = %
: if you.absdepth() > 20 then
SUBST:   3 = 334, 2 = 223, 1 = 112
: else
SUBST:   4 = 3
: end
MAP
             @.ccccccc.@
            ..cc.....cc..
           ..cc.V.S.U.cc..
          ..ccT.xxxxx.Tcc..
        ...cc..xxxxxxx..cc...
    .....ccc.Uxxxxxxxxx..ccc.....
   ..ccc+ccc..xxxxxxxxxV.ccccccc..
  ..cc.....cc..xxxxxxx..cc..3..cc..
 ..cc...I...ccS.xxxxx.Sc+.......cc..
@.cc.........cc.V.T.U.+c2..HGH..3cc.@
.cc..I..J..I..cc.....cc...Gx.xG...cc.
cc....JxxxJ....cc+++cc...GH%%%HG...cc
cc....JxxxJ....cc+++cc...HG%%%GH...cc
.cc..I..J..I..cc.*|*.cc...Hx.xH...cc.
@.cc.........cc*.1.2.*cc2..GHG..1cc.@
 ..cc...I...cc|.c...c.|cc.......cc..
  ..cc.....cc*.c.ccc.c.*cc..1..cc..
   ..cc+c+cc..c.c343c.c..cc+c+cc..
  ..cc.....cc*1..c3c..1*cc.....cc..
 ..ccM.....Kcc|2.....2*cc...%...cc..
@.ccN.......Lcc*.ccc.*cc..FF.FF..cc.@
.ccM..AA.BB..Kcc.*|*.cc...xE.Ex...cc.
cc.....AxB.....ccccccc...%E232E%...cc
cc.....BxA.....ccccccc...%E121E%...cc
.ccK..BB.AA..Mcc.O.P.cc...xE.Ex...cc.
@.ccL.......Nc+Q.....R+c..FF.FF..cc.@
 ..ccK.....M+c....D....c+...%...cc..
  ..cc.....ccQ.C.x.x.C.Rcc.....cc..
   ..ccccccc...CD...DC...ccccccc..
    .....ccc...DC...CD...ccc.....
        ...ccO.D.x.xDD.Pcc...
          ..cc....C....cc..
           ..ccP.....Pcc..
            ..cc.R.R.cc..
             @.ccccccc.@
ENDMAP

NAME:    hangedman_hanging_gardens
TAGS:    no_monster_gen no_pool_fixup allow_dup
ORIENT:  float
DEPTH:   Depths
MONS:    plant w:14 / fungus w:4 / bush w:2
MONS:    fire dragon / ice dragon / lindwurm / hell hound
MONS:    death yak / griffon / catoblepas / dire elephant
MONS:    hydra / anaconda / red wasp w:2 / redback w:3 / wolf spider w:5 / harpy
MONS:    spriggan druid, spriggan rider, sphinx / phoenix / hell beast
ITEM:    pear q:1 / apple q:1 / choko q:1 / apricot q:1 / \
         orange q:1 / banana q:1 / rambutan q:1 / lychee q:1
NSUBST:  ' : 1:2 / 1:3 / 1:4 / 2 = 233444. / *:.
NSUBST:  " : 2:2 / 2:3 / 2:4 / 4 = 233444. / *:.
NSUBST:  C : 2:1 / * = c:299 1:1
SUBST:   X = xx., 1 = 1:29 .:1, t = t:99 1:1, u = t
SUBST:   . = .:2975 1:20 2:1 3:2 4:2, | = *||
TILE:    c = wall_marble
FTILE:   .t1234567Gcx+d*| = floor_lair
COLOUR:  c = white
COLOUR:  .1234567d*| = green
MAP
   ccccccccccccccccccccccccccccccccc
   ctttttttttttttttttttttttttttttttcc
   ctdd...""....""................2|c
cccct"5................1"........".7c
ctttt*"................".ww.."tttttcc
ctd"*G*..................ww..tcccccc.
ctd5"*7.........".t..........tC1111..
ct.....T...WWW."dt.........."tC11...
ct.........WWW.dt.1.....ttttttC...1.
ct.............t.11....tcCCCCCC.....
ct"......."dt..........tC1111.......
ct"......"dt....1......tC11'.........
ct........t.1.....ttttttC...1.XXXXXXX
ct.......t.11.....cCCCCCC....Xxxxxxxx
ct................C1111......Xx
ct...1".........u.C11'......1Xx
ct"..".ww.........+...16XXXXXxx
ct"....ww..tcCCCC+c....Xxxxxxx
ct.........tC1111..u..'Xx
ct........"tC11'.....'1Xx
ct...."tttttC...16XXXXXxx
ct....tcCCCCC....Xxxxxxx
ct..".tc1111....'Xx
ct.2..tc11'....'1Xx
cc.|7.cc...1.XXXXxx
.cccccc.....Xxxxxx
 .11111.....Xx
 ..........'Xx
..........'1Xx
XXXXXXXXXXXXxx
xxxxxxxxxxxxx
ENDMAP

###############################################################################

NAME: lightli_eight_chambers
ORIENT: float
DEPTH: Depths
TAGS: no_monster_gen
SHUFFLE: PFIEMONLSAKRVCTHD
KMONS: P = green death / green draconian / greater naga w:5 / swamp dragon / \
           redback
KMONS: F = fire giant w:5 / fire dragon / fire elemental / red draconian / sun demon
KMONS: I = frost giant w:5 / ice dragon / blizzard demon / white draconian / \
           ice dragon
KMONS: E = titan w:5 / storm dragon / black draconian / ball lightning / vapour
KMONS: O = orc knight / orc sorcerer / orc warlord w:5 / orc high priest / \
           orc warrior
KMONS: N = deep elf knight / deep elf annihilator w:5 / deep elf death mage / \
           deep elf summoner / deep elf sorcerer
KMONS: L = death yak / catoblepas w:5 / basilisk / lindwurm / blink frog
KMONS: S = swamp dragon / hydra w:5 / vapour / large slime creature / alligator
KMONS: A = alligator snapping turtle w:5 / siren / merfolk aquamancer / \
           merfolk impaler / harpy
KMONS: K = greater naga w:5 / naga mage / anaconda / naga warrior / \
           guardian serpent
KMONS: R = emperor scorpion / wolf spider / orb spider / ghost moth w:5 / \
           tarantella
KMONS: M = acid blob w:5 / brown ooze / azure jelly / jelly / great orb of eyes
KMONS: V = vault guard / stone giant w:5 / yaktaur captain / dancing weapon / \
           skeletal warrior
KMONS: C = skeletal warrior / bone dragon / flaming corpse / vampire / lich w:5
KMONS: T = mummy priest w:5 / guardian mummy / sphinx / mummy / reaper
KMONS: H = balrug w:5 / blizzard demon w:5 / shadow demon w:5 / \
           hell beast w:5 / sun demon / ice devil / iron devil / soul eater / \
           hell knight w:5
KMONS: D = giant newt / kobold / hobgoblin w:5 / goblin / adder
MAP
xxvvvvvvvvvvvvvxx
xvv..vvvvvvv..vvx
vv.II.vvvvv....vv
v..II.+...+.EEE.v
v..II.+...+.EEE.v
vv....vvvvv....vv
xvv++vv|||vv++vvx
xxv..v*****v..vxx
xxv..v%%%%%v..vxx
xvv++vv%%%vv++vvx
vv.FF.vv+vv....vv
v..FF..v.v..MM..v
v..FF..v.v..MM..v
vv....vv+vv.MM.vv
xvv++vv...vv++vvx
xxv..v.NNN.v..vxx
xxv..v.NNN.v..vxx
xvv++vv...vv++vvx
vv.PP.vv+vv....vv
v..PP..v.v..OO..v
v..PP..v.+..OO..v
vv....vvvvv.OO.vv
xvv..vvvvvvv..vvx
xxv==vvvvvvvvvvxx
   @@
ENDMAP

###############################################################################

NAME:    hangedman_staggered_hall
TAGS:    transparent allow_dup no_monster_gen
ORIENT:  float
DEPTH:   Elf, Crypt
: if you.branch() == "Crypt" then
TAGS:    patrolling
MONS:    skeletal warrior / eidolon, necromancer / deep dwarf necromancer w:6
MONS:    necromancer band / vampire mage band w:6
MONS:    vampire knight w:25 / bone dragon / ghoul / lich
SUBST:   x = e
: else
KMONS:   12 = deep elf conjurer / deep elf knight
MONS:    deep elf fighter band w:6 / deep elf knight band
MONS:    deep elf annihilator / deep elf sorcerer / deep elf high priest
: end
SUBST:   a : X., A : Y., b : XX., B : YY.
SUBST:   c : X.., C : Y., d : XX., D : YY.
SUBST:   X : xcccbv, Y : ccbv, e = c, % = %%$., * = %***|
MAP
     xxx.....................xxx
     x...........a.........X...x
     x..X.X...X.Xa....c...XXX..x
     x.XXXXXXXXXXb...c...d3XXX2x
     x..X%X%1%X%Xa........XXX..x
     x...........a.........X...xx
     xx...Y.........A...........x
      x..YYY........AY.Y...Y.Y..x
      x.YYY3D...C...BYYYYYYYYYY.x
      x..YYY...C....AY*Y%4%Y*Y..x
xxxxxxx...Y.........A...........x
x...........A.........Y...xxxxxxx
x..Y*Y%4%Y*YA....C...YYY..x
x.YYYYYYYYYYB...C...D3YYY.x
x..Y.Y...Y.YA........YYY..x
x...........A.........Y...xx
xx...X.........a...........x
 x..XXX........aX%X%1%X%X..x
 x2XXX3d..c....bXXXXXXXXXX.x
 x..XXX..c.....aX.X...X.X..x
 x...X.........a...........x
 xxx.....................xxx
ENDMAP

###############################################################################
# The air twists around and strikes you! The ettin shouts in stereo!
# The spriggan berserker goes berserk!
# The spriggan berserker moves out of view.
# You hear a shout! x27
#
# (Arguably doesn't work as well in Zot / entering Zot, but moths of wrath...)
NAME:   grunt_twist_and_shout
ORIENT: centre
DEPTH:  Depths, Zot, !Zot:$
PLACE:  Depths:$
{{
  if you.branch() == "Zot" then
     kmons("1 = draconian annihilator band")
     kmons("2 = moth of wrath")
     kmons("3 = base draconian")
     kfeat("1 = stone_stairs_down_i / stone_stairs_down_ii / "
           .. "stone_stairs_down_iii / escape_hatch_down")
     subst("* = .")
  elseif you.where() == dgn.level_name(dgn.br_entrance("Zot")) then
     colour(".+123* = magenta")
     ftile(".+123* = floor_hall")
     kfeat("1 = enter_zot")
     kmons("1 = nonbase draconian")
     kmons("2 = moth of wrath")
     kmons("3 = base draconian")
     kmons("* = golden dragon / shadow dragon / storm dragon / bone dragon"
           .. " / fire dragon / ice dragon")
     colour("c = lightmagenta")
  else
     local bigweight = you.absdepth() - 16
     local littleweight = 27 - you.absdepth()
     kmons("1 = titan w:" .. bigweight
           .. " / spriggan air mage w:" .. littleweight)
     kmons("2 = ettin w:" .. 2*bigweight
           .. " / spriggan berserker w:" .. littleweight
           .. " / deep dwarf berserker w:" .. littleweight)
     kmons("3 = nothing")
     kitem("1 = superb_item")
     kitem("2 = any")
  end
}}
SUBST:  G = GTt
NSUBST: @ = 1:@ / *:.
MAP
 .x...   .@.   .x...
...x...  ...  ...x...
..3x3.x...G.....3x3.x
.xx2xx.c.......xx2xx.
x.3x..c.......x..x3..
...x..c..cccccc..x...
 ...xcc.cc....ccc.x.
  ...c..c........c.
  ...c..c+ccccc....
.....c..c.*.+.cc.....
@.G..c..c*1*c..c..G.@
.....cc.+.*.c..c.....
  ....ccccc+c..c...
  .c........c..c...
 .x.ccc....cc.ccx...
...x..cccccc..c..x...
..3x..x.......c..x3.x
.xx2xx.......c.xx2xx.
x.3x3.....G...x.3x3..
...x...  ...  ...x...
 ...x.   .@.   ...x.
ENDMAP

NAME:   grunt_double_whammy
ORIENT: float
DEPTH:  Depths, !Depths:$
MONS:   ettin / two-headed ogre
MONS:   deep elf blademaster / ettin
MAP
   cccccccc     cccccccc
  cc|*2...ccccccc......cc
 cc|**..2.+......cccccc.c
  cc|*2...cccccccc    c.c
ccccccccccccccccccccccc.cc
@...1...1........1...1...@
..........................
@.1.....1........1.....1.@
..........................
@...1...1........1...1...@
cc.ccccccccccccccccccccccc
 c.c    cccccccc...2*|cc
 c.cccccc......+.2..**|cc
 cc......ccccccc...2*|cc
  cccccccc     cccccccc
ENDMAP

NAME: lightli_tower_of_babel
ORIENT: float
# Random interlopers look badly out of place here.
TAGS: no_monster_gen no_item_gen
DEPTH: Dis, !D:$, Depths, !Depths:$
: if you.branch() == "Depths" then
# A more worthy adversary badly wanted.
# What about an obvious up-HD rename of hell knight? *duck*
MONS: fire giant
: else
MONS: Hell Sentinel
: end
MONS: flying skull
ITEM: bolt ego:exploding q:5
ITEM: potion of heal wounds
ITEM: plate armour good_item
ITEM: crossbow ego:flame w:9 ident:type / crossbow unrand:hellfire w:1
SUBST: ( = ()
TILE: c = wall_marble
MAP
1.........2...2..........
..cc....2...c...2....cc..
..cc........2........cc..
....v...............v....
.....v...vvv.vvv...v.....
......vvvv.ddd.vvvv......
......vvvvdddddvvvv......
.....vvvvv.ddd.vvvvv.....
.2...vvvvvvv.vvvvvvv...2.
....vvvvvvvv+vvvvvvvv....
2...vd.dvvvv.vvvvd.dv...2
....v...vvv...vvv...v....
.c2...e..+..(..+..g...2c.
....v...vvv.f.vvv...v....
2...vd.dvvvv.vvvvd.dv...2
....vvvvvvvv+vvvvvvvv....
.2...vvvvvvv.vvvvvvv...2.
.....vvvvvd...dvvvvv.....
......vvvv..e..vvvv......
......vvvvd...dvvvv......
.....v...vvv.vvv...v.....
....v...............v....
..cc........2........cc..
..cc....2...c...2....cc..
..........2...2..........
ENDMAP

NAME:    grunt_bruiser_brothers
ORIENT:  centre
TAGS:    transparent no_monster_gen no_item_gen
DEPTH:   Depths, Geh, !Geh:$
PLACE:   Depths:$
# Spell set is Hell Knight II; it has the more powerful ranged spell.
MONS:    hell knight spells:pain;bolt_of_fire;.;.;.;blink
: if you.where() == dgn.level_name(dgn.br_entrance("Zot")) then
MONS:    base draconian
: elseif you.branch() == "Depths" then
MONS:    deep troll
: else
# hell beast perm_ench:invis is too gimmicky.
# Fortunately, we have a permanently invisible 2 already.
MONS:    lorocyproca
: end
ITEM:    crossbow, bolt, potion of curing, potion of heal wounds
ITEM:    potion of invisibility
KFEAT:   - = open_door
SHUFFLE: })]>
: if you.where() == dgn.level_name(dgn.br_entrance("Zot")) then
KFEAT: } = enter_zot
KMONS: )]> = nonbase draconian w:30 / \
             shadow dragon / golden dragon / storm dragon
: end
MARKER:  O = lua:fog_machine { cloud_type = 'flame', pow_min=10, pow_max=10, \
                               delay=10, size=1, excl_rad=0 }
MAP
             cccc
             c})c
             c]>c
             c..c
             c..c
             c..c
             c++c
             c..c
             c2gc
            cc2ecc
            cc..cc
            c....c
            c....c
           cc....cc
           c......c
          cc......cc
ccccccccccc........ccccccccccc
c.h........................g.c
cc..e....................e2.cc
 cc....2................2..cc
  ccc.2..................ccc
    cc.....ccc..ccc.....cc
     ccc...c1c..c1c...ccc
       cc..c-c..c-c..cc
        c............c
       cc............cc
       c..............c
       c..............c
      cc......++......cc
      c......c++c......c
      c....ccc..ccc....c
     cce2.cc c..c cc.2ecc
     cf2.cc  c..c  cc.2dc
     c.ccc   c..c   ccc.c
     ccc     c..c     ccc
             c..c
             c..c
             c..c
            cc..cc
           ccO..Occ
           c......c
           c......c
           cc....cc
            cc..cc
             c++c
              @@
ENDMAP

###############################################################################
#
# <<3>> Abstract or generic threat vaults.
#       098 randomness, really minimal specified content,
#       or very generic vault contents like statues and oklobs.
#
###############################################################################

##############################################################################
# Kobold bridge
# Could perhaps use colour.
NAME:   minmay_kobold_bridge
TAGS:   no_monster_gen
DEPTH:  D:1-2
ORIENT: float
WEIGHT: 3
MONS:   kobold / nothing w:3
MAP
xxxxxx@xxxxxx
xxxx.....xxxx
xxx.......xxx
xxxw.....wxxx
xxxww..1wwxxx
xxxww...wwxxx
xxxww1..wwxxx
xxxww..1wwxxx
xxxww.1.wwxxx
xxxww...wwxxx
xxxww1..wwxxx
xxxw.....wxxx
xxx.......xxx
xxxx.....xxxx
xxxxxx@xxxxxx
ENDMAP

##############################################################################
# Rooms
NAME:    minmay_opposing_rooms
DEPTH:   D:4-
ORIENT:  float
SHUFFLE: 0%
MAP
xxxxxxxxxxxxxxxxxxxxx
xxxx........xxx%.%xxx
xx......x...xx....%xx
xx..9..xx...x......%x
x......xx...+.......x
@....xxxx...x......%x
xxxxxxxxx...xx....%xx
xxx0.0xxx...xxx%.%xxx
xx0....xx...xxxxxxxxx
x0......x...xxxx....@
x.......+...xx......x
x0......x...xx..9..xx
xx0....xx...x......xx
xxx0.0xxx........xxxx
xxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Klotski a.k.a. what we learned from the elders.
NAME:    minmay_klotski
SHUFFLE: }])>
DEPTH:   D:6-12
ORIENT:  float
SUBST:   0 = 0.., 9 = 9., 8 = 88.
MONS:    bush
MAP
xxxxxxxxxxxxxxxxxxxxx
x....+.........+...9x
x1...x...0.0...x....x
x...1x..0.0.0..x.0..x
x1...x.0.....0.x..0.x
x...1x..0...0..x.0..x
x1...x.0.....0.x..0.x
x...1x.........x.0..x
x1...x.9.9.9.9.x....x
x...1x8.8...8.8x9...x
x+xxxxxxxx+xxxxxxxx+x
x...9x$$$$$$$$$xG...x
x.0..x$$$$$$$$$x....x
x..0.x$$$$$$$$$x....x
x.0..x$$$$$$$$$x....x
x..0.xxxxxxxxxxx....x
x.0..+....x0...+....x
x..0.x..T.x.0..x....x
x.0..x.T..x..0.x....x
x9...x....x...0x...Gx
xxxxxxxxx+x+xxxxxxxxx
     x....x....x
     x....x....x
     x....x....x
     x}...x...]x
     xxxxxxxxxxx
ENDMAP

##############################################################################
# Statue pair
NAME:    minmay_separated_statues
DEPTH:   D:8-
ORIENT:  float
MONS:    silver statue, orange crystal statue, ice statue
SHUFFLE: 123
MAP
     ......
    ......xx
   xxx.....xx
  xx*xx.....xx
 xx...xx.....xx
xx*.1..xx..2.*xx
 xx.....xx...xx
  xx.....xx*xx
   xx.....xxx
    xx......
     ......
ENDMAP

##############################################################################
# Statue gallery
NAME:    minmay_statue_hallways
DEPTH:   D:8-
ORIENT:  float
MONS:    orange crystal statue / silver statue / ice statue
MAP
           xxx
           x1x
           x|x
          xx.xx
          x...x
         xx...xx
       xxx.....xxx
xxxxxxxx.........xxxxxxxx
@.......................@
.........................
xxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Wavy paths in crystal walls
NAME:   minmay_wavy_crystal
DEPTH:  D:10-
ORIENT: float
MAP
bbbbbbbbbbbb@.@bbbbbb
@...bbbbbbbb...bbbbbb
......bbbbbb...bbbbbb
@......bbbbb...bbbbbb
bbbb...bbbbb...bbbbbb
bbbbb...bbbbb...bbbbb
bbbbb...bbbbb.....bbb
bbbbb...bbbbbb.....bb
bbbbb...bbb..bbb...bb
bbbbb...bb9bb.bb....b
bbbbb...bb|bb.bb....b
bbbbbb...bbb.bb....bb
bbbbbb.............bb
bbbbbbb...........bbb
bbbbbbbbb.......bbbbb
bbbbbb..bbb...bbb..bb
bbbbb.bb9bb...bb9bb.b
bbbbb.bb|bb...bb|bb.b
bbbbbb.bbb.....bbb.bb
@...................@
............b........
@.........bbbbb.....@
bbbbbbbbbbbbbbbbbbbbb
ENDMAP

##############################################################################
# A traditional vault
NAME:    minmay_two_paths
ORIENT:  float
DEPTH:   D:12-, Depths, !Depths:$
MAP
cccccccccccccccc
c.9.c......c.9.c
c.c.c.c88c.c.c.c
c.c.c.c$$c.c.c.c
c.c.c.c%%c.c.c.c
c.c.c.c**c.c.c.c
c.c.9.c||c.9.c.c
c.cccccccccccc.c
c.cccccccccccc.c
c.9.c......c.9.c
c.c.c.c..c.c.c.c
c.c.c.c..c.c.c.c
c.c.c.c..c.c.c.c
c.c.c.c..c.c.c.c
c9c.9.c00c.9.c9c
ccccccc..ccccccc
ENDMAP

##############################################################################
# Sometimes You Just Gotta Run
NAME:    erik_ogre
DEPTH:   D:6-10
ORIENT:  float
SHUFFLE: 1X / 1X / 2l / 3Y
SUBST:   X=x, Y:x.
MONS:    ogre, iron devil, nothing
ITEM:    potion of heal wounds / potion of speed / potion of berserk rage /\
         scroll of blinking
MAP
        xx@xx
        x...x
        xX.Xx
        xX1Xx
        xX.Xx
        xx.xx
        xx.xx
        xx.xx
        x...x
        x...x
        x...x
        x.d.x
        x...x
        x.d.x
        x...x
     xxxx...xxxx
     x....[....x
xxxxxx..x(.{x..xxxxxx
x...x...x...x...x...x
x....x..xx]xx..x....x
xxxxxx...xxx...xxxxxx
     xxxxx xxxxx
ENDMAP

##############################################################################
# An old-fashioned vault, by Lemuel
NAME:  lemuel_traditional_vault
DEPTH: D:12-, Depths
ORIENT: float
SUBST: 9 = 9 0 8:2
SUBST: 0 = 0 .
KFEAT: $ = ^
KITEM: $ = gold
MAP
........................
wwwwwwwwwwwwwwwwwwwwwwww
wcccccccccc++ccccccccccw
wc$..................$cw
wc.........99.........cw
wc..cccccccccccccccc..cw
wc0.c$....9999....$c.0cw
wc..c.....9999.....c..cw
wc..c..cccc++cccc..c..cw
wc..c..c$......$c..c..cw
wc..c..c...99...c..c..cw
wc..c..c..cccc..c..c..cw
wc00c..c..c||c..c..c00cw
wc00c..c..c||c..c..c00cw
wc..c..c..c++c..c..c..cw
wc..c..c..9889..c..c..cw
wc..c..c$.9889.$c..c..cw
wc..c..cccccccccc..c..cw
wc..c......99......c..cw
wc0.c$............$c.0cw
wc..ccccccc++ccccccc..cw
wc.........00.........cw
wc$........00........$cw
wccccccccccccccccccccccw
wwwwwwwwwwwwwwwwwwwwwwww
........................
ENDMAP

##############################################################################
# An Ice Statue vault
NAME:    lemuel_ice_statue
MONS:    ice statue
TAGS:    no_pool_fixup
ORIENT:  float
DEPTH:   D:7-, Elf, Depths
SUBST:   * : *$
SUBST:   * = |:5 * .
SUBST:   T = TU
SHUFFLE: xb/cc, ABC
: if crawl.coinflip() then
KFEAT:   X = < / >
KITEM:   X = any good_item
SUBST:   A=., B=., C=.
: else
SUBST:   X=*, B=., C=.
KFEAT:   A = teleport trap
: end
MAP
xxxxxxxxxxxxxxxxx
xbbbbbbbbbbbbbbbb
xb**....www.....T
xb**..A.www......
xbX*..B.w1w.....@
xb**..C.www......
xb**....www.....T
xbbbbbbbbbbbbbbbb
xxxxxxxxxxxxxxxxx
ENDMAP

#############################################################################
# Oklob gauntlet
# average of 2.3 oklobs, and at most 5 of them
NAME:    dpeg_oklob_gauntlet
DEPTH:   Depths, !Depths:$, Lair, Snake, Elf
MONS:    plant, oklob plant
ORIENT:  float
SHUFFLE: ]}) / def
NSUBST:  ' = 5:2 *:1
NSUBST:  2 = 1:2 *:112
SUBST:   1=1w, "=ww.
SUBST:   d=|*, e=|*, f=|*
MAP
xxxxxxxxxxxxx
xxxxx]})xxxxx
xxxxx...xxxxx
x'ww.....ww'x
x'"......."'x
x'"......."'x
x'"......."'x
x'"......."'x
x'ww.....ww'x
xxxxxx+xxxxxx
ENDMAP

#############################################################################
# Oklob shooting range
#
# Disallowing this vault on branch starts, as brown stairs can't help there.
# Chance for a plant to turn oklob based on dungeon level. Elf:$ is hard, but
# considered okay. Has between 1 and 3 oklob plants.
# Single oklob at absolute depth 10, expect 2 oklobs at depth 20.
# Rarely (2.5% chance) the whole vault is enclosed. This is the hardest case,
# and the walls are always diggable then.
#
NAME:    dpeg_oklob_shooting_range
DEPTH:   Depths, !Depths:$, Lair:2-, Elf:2-
WEIGHT:  2
ORIENT:  float
SHUFFLE: ab" / AB'
SUBST:   a=[, b=(, A=., B=., '=., "=>
#
SUBST:   x : y:1 x:5 .:34
SUBST:   x = x:2 .
SHUFFLE: xcv
SUBST:   y = x
#
NSUBST:  1 = 1:2 1:3 *:1
: wt = (you.absdepth()-10)/2
: mons("oklob plant w:" .. wt .. " / plant")
MONS:    plant, oklob plant
MAP
xxxxxxxxxxxxxxx
xxxx...1...xxxx
xx...........xx
xx...........xx
x......"......x
x......'......x
x1...aA{Bb...1x
x......'......x
x......"......x
xx...........xx
xx...........xx
xxxx...1...xxxx
xxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Shantytown, noun: An area containing a collection of shacks,
#                   shanties or makeshift dwellings. By Core Xii
#
NAME:   corexii_shantytown
DEPTH:  D:4-8
ORIENT: float
TAGS:   no_monster_gen no_item_gen
NSUBST: + = 1:+ / *:=
SUBST:  = = + x:100
NSUBST: A = 1:+ / *:x, B = 1:+ / *:x, C = 1:+ / *:x, D = 1:+ / *:x
NSUBST: E = 1:+ / *:x, F = 1:+ / *:x, G = 1:+ / *:x, H = 1:+ / *:x
NSUBST: I = 1:+ / *:x, J = 1:+ / *:x, K = 1:+ / *:x, L = 1:+ / *:x
NSUBST: M = 1:+ / *:x, N = 1:+ / *:x, O = 1:+ / *:x, P = 1:+ / *:x
NSUBST: Q = 1:+ / *:x, R = 1:+ / *:x, S = 1:+ / *:x, T = 1:+ / *:x
NSUBST: U = 1:+ / *:x, V = 1:+ / *:x
# There are 111 - glyphs, which leads to about 27 monsters
SUBST:  - = 2:20 d .:50
ITEM:   any food / any weapon / any missile / w:5 any armour / \
        w:5 any potion / w:5 any scroll, w:1 any misc
MONS:   plant / w:40 nothing, goblin / hobgoblin / kobold / orc / w:5 gnoll
MAP
.......................
..1..x++x..1..x+++++x..
.1x++x--x++x++x-----+..
..+--A--B--C--E-----+..
..+--A--B--C--E-----+..
.1xxAxxBxCCx--xEExEExx.
..1+--R....xDDx..F---+.
..1+--R..........F---+.
.1xxRxx.xSSSxTTx.xFx+x.
..+--Q..S---T--T...+1..
..+--Q..S---T--T...+1..
.xxPxx.xxSSxxTxx.xGxx1.
.+--P..V---U--U..G--+..
.+--P..V---U--U..G--+..
.xxOxx.V---xUUx..xHHxx.
..+--O.xVVVx.....H---+.
..+--O........xJJx---+.
.xxNNxMMx..xKKx--xIIxx.
.+---N--xLLx--J--I--+..
.+---N--M--L--x++x--+..
.x+++x++x--L--+11+--+..
.1...1..x++x++x..x++x1.
.......................
ENDMAP

##############################################################################
# Cluster of small buildings with basic monsters and loot, by Core Xii
NAME:   corexii_shack_village
DEPTH:  D:3-9
ORIENT: float
TAGS:   no_monster_gen no_item_gen
NSUBST: A = 1:+ / *:x, B = 1:+ / *:x, C = 1:+ / *:x, D = 1:+ / *:x
NSUBST: E = 1:+ / *:x, F = 1:+ / *:x, G = 1:+ / *:x, H = 1:+ / *:x
# 39 - glyphs, leading to about 10 monsters.
SUBST:  - = 2:20 d .:50
SUBST:  T = T:50 V G 1 .:20
ITEM:   any food / any weapon / any missile / w:5 any armour / w:5 any potion
MONS:   plant / w:40 nothing, goblin / hobgoblin / kobold / orc
MAP
....................
.1xAAx1.xxxxx111111.
.1x--x1.B---xxxxxxx.
.1x--x1.B---C--D--x.
.1xAAx1.xBBBx--D--x.
............xCCxDDx.
..........T.........
.11.xFFFx...........
.xEEx---xGGx1.xHHx1.
.x--E---G--x1.H--x1.
.x--E---G--x1.H--x1.
.xxxxxxxxxxx1.xxxx1.
....................
ENDMAP

##############################################################################
# Marble ring (evilmike)
# outer ring contains 10 to 30 monsters, mean of 16.7
#
NAME:   evilmike_marble_ring
DEPTH:  Depths
ORIENT: float
COLOUR: b-+89*| = white
FTILE:  b-+89*| = FLOOR_MARBLE
TILE:   b = WALL_MARBLE
NSUBST:  0 = 3:9 / 7:0 / 20=0.. / *:.
# can rarely be a statue gallery, otherwise use stone pillars
: if crawl.one_chance_in(4) then
SUBST:  c = G, v = .
: else
SUBST:  v: ...c
: end
SUBST:  b = c, - = .
MAP
xxxxxxxxxxx@xxxxxxxxxxx
x.....................x
x.c.c.c.c.c.c.c.c.c.c.x
x..v.v.v.v.v.v.v.v.v..x
x.c.................c.x
x..v...............v..x
x.c...00000000000...c.x
x..v..0bbbbbbbbb0..v..x
x.c...0b---8---b0...c.x
x..v..0b-b---b-b0..v..x
x.c...0b--|||--b0...c.x
x..v..0b9-***-9b0..v..x
x.c...0b--***--b0...c.x
x..v..0b-b---b-b0..v..x
x.c...0b-------b0...c.x
x..v..0bbbb+bbbb0..v..x
x.c...00000000000...c.x
x..v......T.T......v..x
x.c.................c.x
x..v.v.v.v.v.v.v.v.v..x
x.c.c.c.c.c.c.c.c.c.c.x
x.....................x
xxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# evilmike's hourglass vault
NAME:   evilmike_hourglass
DEPTH:  Depths
ORIENT: float
NSUBST: . = 7:0 / 2:9 / 1:8 / *:.
NSUBST: ' = 7:0 / 2:9 / 1:8 / *:.
SUBST:  . = .:90 ~
NSUBST: % = 6:* / 3:| / *=%$
SUBST:  - = .
MAP
xxxxxxxxxxxxxxxxxx@xxxxxxxxxxxxxxxxxx
xx%%xxxxxxxxxxxxxx-xxxxxxxxxxxxxx$$xx
xx%%%%xxxxxxxxxxxx-xxxxxxxxxxxx$$$$xx
xx%%%%%%xxxxxxxxxx-xxxxxxxxxx$$$$$$xx
xx''''''''xxxxxxxx-xxxxxxxx........xx
xx''''''''''xxxxxx-xxxxxx..........xx
xx''''''''''''xxxx-xxxx............xx
xx'''''''''''''-xx-xx-.............xx
xx'''''''''''''-------.............xx
xx'''''''''''''-------.............xx
xx'''''''''''''-xx-xx-.............xx
xx''''''''''''xxxx-xxxx............xx
xx''''''''''xxxxxx-xxxxxx..........xx
xx''''''''xxxxxxxx-xxxxxxxx........xx
xx$$$$$$xxxxxxxxxx-xxxxxxxxxx%%%%%%xx
xx$$$$xxxxxxxxxxxx-xxxxxxxxxxxx%%%%xx
xx$$xxxxxxxxxxxxxx-xxxxxxxxxxxxxx%%xx
xxxxxxxxxxxxxxxxxx@xxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Inaccessible box (evilmike)
NAME:   evilmike_inaccessible_box
DEPTH:  Depths
ORIENT: float
SUBST:  x : xxxm
NSUBST: 9 = 6:. / *:9
NSUBST: 0 = 6:. / *:0
KPROP:  .089*| = no_rtele_into
SUBST:  ' = .
MAP
'''''''''''''''''''''
'cccmcccccccccccmccc'
'c.....x.....x.....c'
'c..0..x..9..x..0..c'
'm.000.m.999.m.000.m'
'c..0..x..9..x..0..c'
'c.....x.....x.....c'
'cxxmxxcxxmxxcxxmxxc'
'c.....x..*..x.....c'
'c..9..x.|8|.x..9..c'
'c.999.m*8|8*m.999.c'
'c..9..x.|8|.x..9..c'
'c.....x..*..x.....c'
'cxxmxxcxxmxxcxxmxxc'
'c.....x.....x.....c'
'c..0..x..9..x..0..c'
'm.000.m.999.m.000.m'
'c..0..x..9..x..0..c'
'c.....x.....x.....c'
'cccmcccccccccccmccc'
'''''''''''''''''''''
ENDMAP

##############################################################################
# Path along pillars (evilmike)
NAME:   evilmike_pillar_path
DEPTH:  Depths
ORIENT: float
SUBST:  c: cvxbwl.t
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxx......xvvvvvx......x
xxxxxxx...0..xv*|*vx..8...x
@.x...x.0cc..xv|c*+...cc8.x
x...x....cc0.xv*|*vx.8cc..x
xxxxxxx..0...xvvvvvx...8..x
xxxxxxx......xxxxxxx......x
xxxxxxxxxx.xxxxxxxxxxx.xxxx
      xxx..xxxxxxxxxxx..xxx
      xxx.xxxxxxxxxxxxx.xxx
      xxx..xxxxxxxxxxx..xxx
      xxxx.xxxxxxxxxxx.xxxx
      xxx..xxxxxxxxxxx..xxx
      xxx.xxxxxxxxxxxxx.xxx
      x......xxxxxxx......x
      x...0..xxxxxxx..9...x
      x.9cc..x...x....cc9.x
      x..cc9...x...x.9cc..x
      x..0...xxxxxxx...9..x
      x......xxxxxxx......x
      xxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Cross vault (evilmike)
NAME:   evilmike_cross
DEPTH:  Depths
ORIENT: float
MAP
...................
.......xxxxx.......
.......x...x.......
.......+.8.+.......
.......x...x.......
.......xx.xx.......
.......0x.x0.......
.xx+xx0cc+cc0xx+xx.
.x...xxc*%*cxx...x.
.x.9.xxc%G%cxx.9.x.
.x...xxc*%*cxx...x.
.xx+xx0ccccc0xx+xx.
.......0xxx0.......
.......xxxxx.......
.......x...x.......
.......+.9.+.......
.......x...x.......
.......xxxxx.......
...................
ENDMAP

##############################################################################
# Lava path (evilmike)
# Has mostly lava and sometimes water, a bit sewer-like.
#
NAME:   evilmike_lava_path
DEPTH:  Depths
ORIENT: float
TAGS:   no_pool_fixup
SUBST:  w: wlll
NSUBST: . = 10:0 / 4:9 / *:.
SUBST:  . = .:90 ~
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
wwwwwwwwwwxxxxxxwwwxxxxxxxxxxxx
@........wwxxxxxw|wxxxxxxxxxxxx
wwwwwwww..wwxxxxw*wxxxxxxxxxxxx
@......ww..wwxxxw8wwwwwwwwwwwwx
wwwwww..ww..wwxxw............wx
xxxxxww..ww..wwxw8www.wwwwww.wx
xxxxxxww..ww..wxw*wxw.w......wx
xxxxxxxww..ww.wxw|wxw.w.wwwwwwx
xxxxxxxxww..w.wxwwwxw.w.wxxxxxx
xxwwwxxxxww.w.wxxxxxw.w.wxxxxxx
xxw|wxxxxxw.w.wxxxxww.w.wxxxxxx
xxw|wxxxxxw.w.wxxxww..w.wxxxxxx
xxw*wxxxxxw.w.wxxww..ww.wxxxxxx
xxw*wxxxxxw.w.wxww..ww..wxxxxxx
xxw8wwwwwww.w.www..ww..wwxxxxxx
xxw.........w.....ww..wwxxxxxxx
xxwwwwwwwww.wwwwwww..wwxxxxxxxx
xxxxxxxxxxw.........wwxxxxxxxxx
xxxxxxxxxxwwwwwwwwwwwxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################

NAME:   nooodl_glass_maze
ORIENT: float
SUBST:  M = cm..
NSUBST: N = 1:+ / *:m
MAP
cccccccccccccccc@c
c*..........M....c
c.cc.cc.cc.cc.cc.c
c.ccMcc.cc.cc.ccMc
c.M......M...0...c
c.cc0cc.cc.cc.cc.c
c.cc.ccNccNcc.ccMc
c..M..N....N.....c
c.cc.cc.*0.cc.cc.c
cMcc.cc.0*.ccMcc.c
c...0.N....N.....c
c.cc.ccNccNcc.cc.c
cMcc.cc.cc.cc.cc.c
c....M...M.M...M.c
c.cc.cc.cc.cc.cc.c
c.cc.cc.cc.cc.cc.c
c..M....M.....0.*c
c@cccccccccccccccc
ENDMAP

##############################################################################

NAME:    hangedman_arrow_trap
ORIENT:  float
DEPTH:   D:12-, !D:$, Depths, !Depths:$
SHUFFLE: [( / ]) / [] / []
MAP
xxxxxxxxxxxxxxxxxx
x*xxxx*x[(x*xxxx*x
x..x9x9x..x9x9x..x
xx..x9xx..xx9x..xx
xxx..x0x00x0x..xxx
xxxx..xx00xx..xxxx
xxxxx..x++x..xxxxx
xxxxxx......xxxxxx
xxxxxxx....xxxxxxx
xxxxxxxx**xxxxxxxx
xxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################

NAME:   wad_guarded_room
DEPTH:  D:6-, !D:$, Depths:2-, !Depths:$
ORIENT: float
WEIGHT: 4
FTILE: ' = floor_pebble
MONS:   plant
MAP
xx....xxxxxxxxxxxxxxxxxxxx
x.........+.xxxxxxxxxxxxxx
...xx...xxx..............x
...xxx....x..xx..xx0.xx..x
..xxx.....x..xx0.xx..xx..x
...xx...xxxxx..xx..xx..xxx
x......x.xxxx.0xx..xx.0xxx
xx....xxx..xxxx0.xx..xx..x
xx....xxx..xxxx..xx0.xx..x
x......x.xxxx..xx..xx.0xxx
...xx...xxxxx..xx..xx..xxx
...xxx..xxxxxxx0.xx..xx.0x
..xxx...xxxxxxx..xx..xx..x
...xx...xxxxx..xx.0xx..xxx
x......xxxxxx..xx..xx0.xxx
xx....xxxx.............xxx
xx....xxxx.xcccccc+ccccccx
x......xxx.xc..xxx.xxx..cx
...xx...xx.xcxxxx...xxxxcx
...xxx..+..xcxxxx0'0xxxxcx
..xxx...xxxxcxxx9.'.9xxxcx
...xx...xxxxcxxxxx'xxxxxcx
x......xxxxxcxx..0'0..xxcx
xx....xxxxxxcxx.T.'.T.xxcx
xx....xxxxxxcxx..'''..xxcx
x......xxxxxcxx..'''..xxcx
...xx...xxxxcxx.T.'.T.xxcx
...xxx..xxxxcx%%%xxx%%%xcx
..xxx...xxxxc**xxxxxxx**cx
...xx...+..xcnncccccccnncx
x......xxx...............@
xx....xxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################

NAME:   wad_statue_garden
DEPTH:  D:8-, !D:$, Depths:2-, !Depths:$
ORIENT: float
WEIGHT: 4
FTILE: ' = floor_pebble
FTILE: 12"t> = floor_grass
COLOUR: 12" = green
FTILE: ; = floor_dirt
COLOUR: ; = brown
MONS:   plant, bush
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x""""""1;;;;;;;xxxxxxxxxxxxxxxcxxxxxxxxxxxxxxxx.+.9xx
x""""""";"t"t";xxxxxGxcxGxcxGxcxGxcxGxcxGxxxxxx.xxxxx
x""""""1;;;;;;;+.....+.................+.....+;;x'%'x
x""""""";;"";;xxxx.xGxcxGxcxGc+cGxcxGxcxGx.xxx.;x.'0x
x""""""1;2222;"t"c+ccccccccccc'ccccccccccc+c...;x'0'x
x"""""1";2tt2;t"tc.cn%xxx'.'.'''.'0'xxx%nc.c...;xxx+x
x1"1"1"1;2222;"t"c.nc%+...'0'.'9'.'...+%cn.c...;;;;;x
x""WWWWW;;"";;cccc.cn*xxG'.'.'''.'.'Gxx*nc.cccc;xxx+x
x"WWwwwWW;;;;;c'.'.cn%+...'.'.'.'0'...+%nc.'.'c;x'.'x
x""WWWWW;;"";;c.G..nc*x..'.'.'U'.'.'..x%cn..G.c;x*'9x
x1"1"1"1;2222;c'.'.cn%+...'0'.'.'.'...+*nc.'.'c;x'.'x
x"""""1";2tt2;cccc.cn%xxG'.'.'''.'.'Gxx%nc.cccc;xxx+x
x""""""1;2tt2;t11c.nc*+..0'.'.'.'.'...+*cn.c...;;;;;x
x""""""";2tt2;t>1c.cn%xxx'.'8'''.'0'xxx%nc.cxx.;xxx+x
x""""""1;2tt2;t"1c+ccccccccccccccccccccccc+cxx.;x'.'x
x""""""";2222;t"tx.xxx.x.x.x.x.x.x.x.x.xxx.xx..;x0'%x
x1"1"1"1;;"";;;.xx.xx...................xx.xx..;x'0'x
xxxxxxxx;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;xxxxx
ENDMAP

##############################################################################

NAME: wad_mega_maze
ORIENT: float
WEIGHT: 4
SUBST:  X : cccvvb
MAP
X@XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
X....XXXXXXXX0X.X.............~...........X....X
XXXX.XX.......X.X.XXX.XXXXXXXX.XXXXXXXX.X.X.XX.X
X....XX.XXXXXXX.X...X.XG.....X.XXXXXXXX.X...XX.X
X.XXXXX.X.......XXX.X.XXXXXX.X.X.....XX.XXXXXX.X
X.XXXXX.X.XXXXX...X.X.X....X.X.X.XXX.XX.X......X
X....XX.X.XXXXXXXXX.X.X.XX.X.X.X.XXX.X%9X.XXXXXX
XXXX.XX.X...........X.X.XXXX.X.X.XXX.XXXX.XG...X
X.....X.XXXXXXX.XXXXX.X....X.X.X.XXX......XXXX.X
X.XX.XX..X....X.....X.X.XX.X.X.X.XXXXXXXXXXXXX.X
X.XXXXXX.X.XX.XXXXXXX.XGXX.....X.X|X...XX....X.X
X......X.X.XXXXXXXXXXXXXXXXXXXXX.n|X.X.XX.XX.X.X
XXXXXX.X.X..........~............X|+.X....XX.X.X
X.XXXX.X.X.XXXXXXXX.X.XXXXXXXXXX.XXXXXXXXXXX.X.X
X.XXXX.X.X.X.XX...X.X.XXXXX....X.XX..........X.X
X......X...X....XXX.X.X...X.XX.X.XX.XXX.XXXXXX.X
XXXXXX.XXXXXXX.XXXX.X.XXX.X.XX.X....XXX........X
X....X.XXXXX.....X..X.XXX.X.XX.XXXXXXXXXXXXXXXXX
X.XX.X.XXXXXXX.X.X.XX.XX..X.XX...X......XXX....X
X.XX...XXX.XXX.X.X.XX.XX.XX.XXXX.X.XXXX.....XX.X
X.XXXXXXXX.X...X...XX.XX.....9*X.X....XXX.XXXX.X
X.XXXXXXXX.XXXXXXXXXX.XXXXXXXXXX.X.XXXX0X.X.GX.X
X.XXXXXXXX............~..........X.XXX..X.X.XX.X
X.X...XXXX.XXXXXXXXXX.XXXXXXXXXX.X.....XX.X.XX.X
X.X.X.XXXX.X........X.XX.......X.XXXXXXXX.X.XX.X
X...X........XXXXXX0X....XXXXX............X....X
XXXXXXXXXXXXXXXXXXXXX@XXXXXXXXXXXXXXXXXXXXXXXXXX
ENDMAP

##############################################################################

NAME: wad_river_or_something
ORIENT: float
DEPTH:  D:2-, !D:$
WEIGHT: 4
MAP
xx@xxxxxxxxxxxxxxxxxxxxxxxxxx
xx..0....0....0....+........x
xxG%xxxG%xxxG%xxxG%x........x
xxxxxxxxxxxxxxxxxxxx+xxxxxxxx
xx0........0.......~.......xx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwx
xx.........................xx
xxxx.xxxxxxxxxxxxxxxxxxxxxxxx
xxx.xxx~.xxxxxxxxx..xxxxxG.xx
xx.xx0.xx.xxxxxxx.xx.xxxxxx.x
x.xxxxxxxx.xxx...xxxx..xxxx~x
x.x...xxx.x.x.xxx.xxxxx.xx.xx
xx.xxx...xxx.xxxxx.xx%0x..xxx
xxxxxxxxxxxxxxxxxxx.xxxxxxxxx
.x.0..x.x..0.x....x.x....x...
..x..x...x..x.....~.......x..
..................x.x........
...................@.........
ENDMAP

##############################################################################

NAME: wad_crossroads
DEPTH:  D:2-, !D:$
ORIENT: float
WEIGHT: 4
FTILE: 'G = floor_pebble
SUBST:  X : cccvvb
MAP
Xxxxxxxxxxxxx..@..xxxxxxxxxxxxX
xxx.xxxxx'...xx'xx...'xxxxx.xxx
x0x...xx'.xxxx'''xxxx.'xx...x0x
Xx.xx......'.''x''.'......xx.xX
XXx%x...xxxx''xxx''xxxx...x.xXX
XXXx.xx....'.''x''.'....xx.xXXX
XXXXx.x...xxxx'''xxxx...x.xXXXX
XXXXXx.xx..xGxX'XxGx..xx.xXXXXX
@.....x.xXxxxX.'.XxxxXx.x.....@
XXXXXxxxxxxx.xX'Xx.xxxxxxxXXXXX
XXXXx.x.x..x..'''..x..x.x.xXXXX
XXXxxx.x....x.'G'.x....x.xxxXXX
XXx.x.....x..x'''x..x.....x.xXX
Xxxx.......x..'G'..x.......xxxX
x0x.....xxX.XX'''XX.Xxx.....x0x
xx....xxxX.X.XX'XX.X.Xxxx..%.xx
XxxxxxxxxXXXXXX@XXXXXXxxxxxxxxX
ENDMAP

###############################################################################

NAME:   wad_arrow_thing
DEPTH:  D:8-, !D:$, Depths:2-, !Depths:$
ORIENT: float
WEIGHT: 4
FTILE:  ' = floor_pebble
SUBST:  X : cccvvb
MAP
XXXXXXXXXXXXXXXXXXXXXXX
X..%X.............X%..X
X0G.X.GXGXG.GXGXG.X.G0X
X.0.X.X.........X.X.0.X
X+XXX.X.XXXXXXX.X.XXX+X
X.......~.............X
XXXXX+XXXXXXXXXXX+XXXXX
XXXX...X.X.X.X.X...XXXX
XXX.....'..X..'.9...XXX
@.....%'''.+~'''......@
XXX..0..'..X..'.....XXX
XXXX...X.X.X.X9X...XXXX
XXXXX+XXXXnXnXXXX+XXXXX
XXXXX.XXX..*..XXX.XXXXX
XXXXXX.XXX..0XXX.XXXXXX
XXXXXXX.XXX9XXX.XXXXXXX
XXXXXXXX.XX+XX.XXXXXXXX
XXXXXXXXX.X.X.XXXXXXXXX
XXXXXXXGXX.X.XXGXXXXXXX
X0......XXX.XXX.~...0%X
XXXXXXX.+..X..+.XXXXXXX
XXXXXXXXXXXXXXXXXXXXXXX
ENDMAP

###############################################################################

NAME:   wad_cavern_with_rooms
DEPTH:  D:8-, !D:$, Depths:2-, !Depths:$
ORIENT: float
WEIGHT: 4
FTILE: ' = floor_pebble
MONS:   plant
MAP
xxxxxxxxxxxxxxxxx@xxxxxxxxxx
xxxxxx...xxxxxxx..xxxxxxxxxx
xxxxxx..%xxxxxxx...xxxxxxxxx
x%..xx.x~xxxxxxx....xxxxxxxx
x...+.x.xxxxxxx..'..xxxxxxxx
x...xxxx.xxx....'.'..xxxxxxx
xxxxxxxxx......'.'.'~..xxxxx
xxxx1.WWW..xxx..'.'.xx+xxxxx
xx...WW....+0x~..'..x....xxx
x1.........xxx......x...0xxx
xxx.xxxx...'........xxxxxxxx
xxxxx0.+..'.'...~.xxxxxxxxxx
xxxxxxxx.'.'.'.xx+x%..x9..xx
xxxxxxxxx.'.'xxxx.x...+...xx
xxxxxxxxxx.'xxxxx....0x...xx
xxxxxxxxxxx@xxxxxxxxxxxxxxxx
ENDMAP

###############################################################################

NAME:   wad_courtyard
DEPTH:  Depths, !Depths:$
ORIENT: float
WEIGHT: 4
FTILE:  "+ = floor_pebble
FTILE:  ; = floor_limestone
COLOUR: ; = white
SUBST:  X : cccvvb
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x.%9x0x0x.x%xXXXXXXXXXXXx.x0x%x0x9%.x
x...x...x...xX8..***..8Xx...x...x...x
x...x...x...xX.;;;;;;;.Xx...x...x...x
x...........xX.........Xx...........x
x...........xXXX+XXX+XXXx...........x
x........xxxxxxx"xxx"xxxxxxx........x
xWWWWWWxxx0....+".x."+....0xxxWWWWWWx
xwwwwwwx%+.;;;.x".x."x.;;;.+9xwwwwwwx
xwWWWWWxxx0....x+xxx+x....0xxxWWWWWwx
xwW......xxxxxxx.".".xxxxxxx......Wwx
xwW............xxx+xxx............Wwx
xwW............xG.".Gx............Wwx
xwW......xxx...xxx+xxx...xxx......Wwx
xwW...xxxxXx......"......xXxxxx...Wwx
xwW...x..xxx...W.."..W...xxx..x...Wwx
xwW........0..WWW.".WWW..0........Wwx
xwW...........WWW.".WWW...........Wwx
xwW.......xxx..W.."..W..xxx.......Wwx
xwW....xxxxxx..T.."..T..xXxxxx....Wwx
xwW....x..xxx..W.."..W..xxx..x....Wwx
xwW.......0...WWW.".WWW...0.......Wwx
xwW...........WWW.".WWW...........Wwx
xwW........xxx.W.."..W.xxx........Wwx
xwW.....xxxxXx...."....xXxxxx.....Wwx
xwW.....x..xxx...."....xxx..x.....Wwx
xwW.............x.".x.............Wwx
xwWWWWWWWWWWWWWxxx.xxxWWWWWWWWWWWWWwx
xwwwwwwwwwwwwwwwx...xwwwwwwwwwwwwwwwx
xxxxxxxxxxxxxxxxxx@xxxxxxxxxxxxxxxxxx
ENDMAP

###############################################################################

NAME:    hangedman_cross_stitches
ORIENT:  float
TAGS:    transparent no_monster_gen
DEPTH:   D:5-, !D:$, Depths, !Depths:$, Elf, Snake
WEIGHT:  5 (Elf, Snake), 10
KMONS:   " = 0
KMONS:   ` = 9
KITEM:   "` = % / $ w:5
KMASK:   , = !no_monster_gen
SHUFFLE: XzZ / '"`
SUBST:   XzZ = x, ' = ., x : x:90 c
MAP
           ,,,,,,,,,,,,,,,,,,,,,
          ,,,,,,,,,,,,,,,,,,,,,,,
         ,,.X..'..X..'..X..'..X.,,
        ,,.'x'XxX'x'XxX'x'XxX'x',,
       ,,.,,X..'..X..'..X..'..X.,,
      ,,.X,,'..z..`..z..'..X...,,
     ,,.'x'XxX"x"ZxZ"x"XxX'x'.,,
    ,,..,X..'..z..`..z..'..X.,,
   ,,.X,,'..z..`..z..'..X...,,
  ,,.'x'XxX"x"ZxZ"x"XxX'x'.,,
 ,,...X..'..z..`..z..'..X.,,
,,.X..'..X..'..X..'..X...,,
,,'x'XxX'x'XxX'x'XxX'x'.,,
,,.X..'..X..'..X..'..X.,,
 ,,,,,,,,,,,,,,,,,,,,,,,
  ,,,,,,,,,,,,,,,,,,,,,
ENDMAP

###############################################################################

NAME:    hangedman_cross_veins
ORIENT:  float
TAGS:    transparent no_monster_gen no_pool_fixup
DEPTH:   D:8-, Depths, !Depths:$, Elf, Snake, Abyss
WEIGHT:  5 (Elf, Snake), 10
KMONS:   12 = 0
KMONS:   34 = 9
KMONS:   5 = 9 / 8
KITEM:   1 = $ / % w:5
KITEM:   23 = % / $ w:5
KITEM:   45 = * / % w:5
: if you.branch() == "Abyss" then
SUBST:   x = xxmmccbv, X = xxccvb, . = .:990 W, ' = .:55 WWwl G:5, 23 = 1, 45 = 2
: elseif you.absdepth() > 20 then
SUBST:   x : x m:5, X : xxxcccbv, ' : .:70 w:20 l
: elseif you.absdepth() > 13 then
SUBST:   x : x m:3, X : xcc, ' : .:80 wW
: else
SUBST:   x : x m:2, X : xxc, ' : .:90 W
: end
MAP
  xxxxxxxx
 xxXXXXXxxx
xxxX1'1Xxxxx
xXXXX.XXXXxx
xX.XX.XX1Xxx
+.'..'..'Xxxxxx
xX.XX.XX1Xxxxxxx
xXXXX.XXXXXXXXxxx
xxxX.'.XX5.'4Xxxxx
xXXXX.XXXXX.XXXXxx
xX2XX.XX.XX.XX4Xxx
xX'..'..'..'..'Xxxxxx
xX3XX.XX.XX.XX.Xxxxxxx
xXXXX.XXXXX.XX5XXXXXxxx
xxxX3'3XxX.'.XXX1'1Xxxx
xxxXXXXXXXX.XXXXX.XXXXx
 xxxxxxX3XX.XX.XX.XX1Xx
  xxxxxX'..'..'..'..'Xx
     xxX3XX.XX.XX.XX1Xx
     xxXXXX.XXXXX.XXXXx
     xxxxX3'2XxX.'.Xxxx
      xxxXXXXXxXX.XXxx
       xxxxxxxxxx+xxx
ENDMAP

###############################################################################

NAME:   hangedman_cross_cluster
TAGS:   patrolling transparent no_monster_gen
DEPTH:  D:8-, !D:$, Depths, !Depths:$
ORIENT: float
SUBST:  C = CTUV, y : xc, z : ccvb, 1 = 0''
KMASK:  ' = !no_monster_gen
MAP
              '''''''''
             '''.xxx.'''
            '''...x...'''
           ''''x.1x1.x''''
        '''''''xxxyxxx'''''''
       ''''''''x.0x0.x''''''''
       ''.xxx.....x.....xxx.'''
     '''...x.....xxx.....x...'''
 '''''''x.1x$.x'''''''x.$x1.x'''''''
''''''''xxxyxxx'''''''xxxyxxx''''''''
''.xxx..x.$x0.x..ccc..x.0x$.x..xxx.''
''..x......x......c......x......x...'
'x.1x$.x..xxx..c.%c%.c..xxx..x.$x1.x'
'xxxyxxx'''''''ccczccc'''''''xxxyxxx'
'x.$x0.x'''''''c.9c9.c'''''''x.0x$.x'
'...x.....ccc.....c.....ccc.....x...'
''.xxx.....c.....ccc.....c.....xxx.''
'''''...c.%c9.c.......c.9c%.c...'''''
 '''''..ccczccc..|C|..ccczccc..'''''
'''''...c.%c9.c.......c.9c%.c...'''''
''.xxx.....c.....ccc.....c.....xxx.''
'...x.....ccc.....c.....ccc.....x...'
'x.$x0.x'''''''c.9c9.c'''''''x.0x$.x'
'xxxyxxx'''''''ccczccc'''''''xxxyxxx'
'x.1x$.x..xxx..c.%c%.c..xxx..x.$x1.x'
'...x......x......c......x......x...'
''.xxx..x.$x0.x..ccc..x.0x$.x..xxx.''
''''''''xxxyxxx'''''''xxxyxxx''''''''
 '''''''x.1x$.x'''''''x.$x1.x'''''''
     '''...x.....xxx.....x...'''
       ''.xxx.....x.....xxx.''
       ''''''''x.0x0.x''''''''
         ''''''xxxyxxx''''''
          '''''x.1x1.x'''''
            '''...x...'''
             '''.xxx.'''
              '''''''''
ENDMAP

###############################################################################

NAME:    hangedman_stitches_corridor
TAGS:    no_monster_gen no_item_gen
DEPTH:   Depths, !Depths:$
ORIENT:  float
KFEAT:   ~ : needle trap / dart trap / . w:80
KFEAT:   " : shallow_water / . w:90
KFEAT:   ^ : alarm trap / . w:190
KMASK:   '~ = !no_monster_gen
SUBST:   1 = 0.
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xx"x~x~x~x~x~x~x~x~x~x~x~x~x~x~x~x~x@
x"""''''''''''''''''''''''''''''''''@
xx"xx'G'xxxxxxxxxxxxx'G'xxxxxxxxxxxxx
x~'xcccccx"x.x.x.x"xcccccx"x~x~x~x"xx
xx''c$0.c"""....."""c%0%c"""'''''"""x
x~'xc0%1+x"xx.G.xx"x+0%9cx"xx'G'xx"xx
xx''ccccc..xcccccx..ccccc..xcccccx'~x
x~'mmmmmmm..c*9$c..xxxxxxx..c.0$c''xx
xx''ccccc..xc9*$+x^.ccccc..x+1%0cx'~x
x~'xc0%1+x..ccccc.^x+$*9cx..ccccc''xx
xx''c$0.c..xxxxxxx..c$9*c..mmmmmmm'~x
x~'xcccccx..ccccc..xcccccx..ccccc''xx
xx"xx'G'xx"xc9%0+x"xx'G'xx"x+1%0cx'~x
x"""'''''"""c%0%c"""....."""c.0$c''xx
xx"x~x~x~x"xcccccx"x.x.x.x"xcccccx'~x
xxxxxxxxxxxxx'G'xxxxxxxxxxxxx'G'xx"xx
@''''''''''''''''''''''''''''''''"""x
@x~x~x~x~x~x~x~x~x~x~x~x~x~x~x~x~x"xx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

###############################################################################

NAME:   evilmike_horseshoe
DEPTH:  D:12-, Depths, !Depths:$
ORIENT: float
MAP
   xxxxxxxxxxxxxxxxxxxxx
  xxcccccccccccccccccccxx
 xxcc...c.........c...ccxx
xxcc....+.x9.8.9x.+....ccxx
xcc.....c.........c.....ccx
xc.9...cccccc+cccccc...9.cx
xc.x..cc**%%%%%%%**cc..x.cx
xc...ccccccccccccccccc...cx
xcc+ccxxxxxxxxxxxxxxxcc+ccx
xc...cx     @@@     xc...cx
xc.x.cx             xc.x.cx
xc.0.cx             xc.0.cx
xc...cx             xc...cx
xc.9.cx             xc.9.cx
xc...cx             xc...cx
xc.0.cx             xc.0.cx
xc.x.cx             xc.x.cx
xc...cx             xc...cx
xcc+ccxx           xxcc+ccx
xc...ccxx         xxcc...cx
xc....ccxx       xxcc....cx
xc.....ccx       xcc.....cx
xcc.....cx       xc.....ccx
xxcc....cx       xc....ccxx
 xxcc...cx       xc...ccxx
  xxcc+ccx       xcc+ccxx
ENDMAP

###############################################################################
# Death Spiral stair vault (in two versions, with the smaller one being for
# non-D branches)

NAME:   grunt_deathspiral_large
TAGS:   patrolling uniq_deathspiral
DEPTH:  Depths, !Depths:$
ORIENT: centre
SHUFFLE: )]}>
SUBST:  } = }>
SUBST:  ] = ]>
KMONS:  0 = 0 band
KMONS:  8 = 8 band
MAP
               ccccc@
            cccc.....
           cc........
         ccc....cccccccccccc
        cc....ccc..........ccc
       cc...ccc..............ccc
      cc...cc.....cccccccc.....ccc
      c...cc...cccc......cccc....cc
     cc..cc...cc............ccc...cc
     c..cc...cc.....ccc.......cc...cc
    cc..c...cc...cccc%ccccc....cc...c
    c..cc..cc..ccc........ccc...cc..cc
   cc..c...c..cc....0.......cc...cc..cc
   c...c..cc..c....ccccccc...cc...c...c
   c..cc..c..cc..ccc0....ccc..cc..cc..c
   c..c...c..c..cccc+ccc...cc..cc..c..cc
   c..c...c..c.cc0..8.0cc...c...c..cc..c
   c..c..cc..c.c0*....*0cc..cc..c...c..c
   c..c..c%..c.c........cc...c..cc..c..c
@..c..c..cc.0c0+8..}]...ccc..c...c..c..c
c..c..c...c..ccc...)>..8+0c0.cc..c..c..@
c..c..cc..c...cc........c.c..%c..c..c
c..c...c..cc..cc0*....*0c.c..cc..c..c
c..cc..c...c...cc0.8..0cc.c..c...c..c
cc..c..cc..cc...ccc+cccc..c..c...c..c
 c..cc..cc..ccc....0ccc..cc..c..cc..c
 c...c...cc...ccccccc....c..cc..c...c
 cc..cc...cc.......0....cc..c...c..cc
  cc..cc...ccc........ccc..cc..cc..c
   c...cc....ccccc%cccc...cc...c..cc
   cc...cc.......ccc.....cc...cc..c
    cc...ccc............cc...cc..cc
     cc....cccc......cccc...cc...c
      ccc.....cccccccc.....cc...cc
        ccc..............ccc...cc
          ccc..........ccc....cc
            cccccccccccc....ccc
                   ........cc
                   .....cccc
                   @ccccc
ENDMAP

NAME: grunt_deathspiral_small
TAGS: patrolling uniq_deathspiral
DEPTH: Depths, !Depths:$, Crypt, !Crypt:$, \
       Dis, !Dis:$, Geh, !Geh:$, Tar, !Tar:$, Coc, !Coc:$, \
       Zot, !Zot:$
ORIENT: centre
SHUFFLE: )]}>
SUBST:  } = }>
SUBST:  ] = ]>
# Like the door vault, pick interesting monsters for the 8s in branches,
# but use normal OOD for D.
# Also remove the bits of loot for Hell branches and Zot.
: if you.branch() == "Crypt" then
KMONS:  8 = bone dragon / lich / ancient lich / w:5 death cob / \
            w:5 curse toe / w:5 zombie / w:5 skeleton / eidolon / \
            w:20 reaper / soul eater / w:30 place:Crypt
: elseif you.branch() == "Dis" then
KMONS:  8 = w:5 shadow fiend / w:5 ice fiend / w:5 brimstone fiend / \
            w:5 hell sentinel / iron dragon / tormentor / iron golem / \
            place:Dis
SUBST:  *% = .
: elseif you.branch() == "Geh" then
KMONS:  8 = brimstone fiend / w:5 hell sentinel / hellion / balrug / \
            fire giant / place:Geh
SUBST:  *% = .
: elseif you.branch() == "Tar" then
KMONS:  8 = w:15 shadow fiend / reaper / w:5 soul eater / shadow dragon / \
            lich / ancient lich / w:15 place:Tar
SUBST:  *% = .
: elseif you.branch() == "Coc" then
KMONS:  8 = w:15 blizzard demon / frost giant / w:5 titan / w:15 ice fiend / \
            w:5 simulacrum / place:Coc
SUBST:  *% = .
: elseif you.branch() == "Zot" then
KMONS:  8 = w:35 nonbase draconian / orb of fire / electric golem / \
            killer klown / death cob / w:5 curse toe / w:5 ancient lich
SUBST:  *% = .
:else
KMONS:  8 = 8 band
: end
KMONS:  0 = 0 band
MAP
       ccccc@
      cc.....
     cc.ccccccccccc
    cc.cc.........ccc
   cc.cc.ccccccccc..cc
   c.cc.cc..0....ccc.cc
  cc.c.cc.ccccccc..cc.cc
  c.cc.c.cc%0...ccc.cc.cc
  c.c.cc.ccc+ccc..cc.cc.c
  c.c.c.cc.080.ccc.cc.c.c
  c.c.c.c.*...*.ccc.c.c.c
  c.c.c.c0..}..0c%c.c.c.c
@.c.c0c0+8.].).8+0c0c.c.@
c.c.c.c%c0..>..0c.c.c.c
c.c.c.ccc.*...*.c.c.c.c
c.c.cc.ccc.080.cc.c.c.c
c.cc.cc..ccc+ccc.cc.c.c
cc.cc.ccc...0%cc.c.cc.c
 cc.cc..ccccccc.cc.c.cc
  cc.ccc....0..cc.cc.c
   cc..ccccccccc.cc.cc
    ccc.........cc.cc
      ccccccccccc.cc
            .....cc
            @ccccc
ENDMAP

###############################################################################

NAME:    hangedman_box_tower
TAGS:    no_monster_gen no_item_gen no_pool_fixup transparent
DEPTH:   Depths, !Depths:$
ORIENT:  float
KMONS:   1 = patrolling 0 w:40 / nothing
KMONS:   4 = 9 w:40 / 0 band
KITEM:   1 = % w:75 / $ w:20 / nothing w:4 / * w:1
KITEM:   $ = $ w:40 / nothing
KFEAT:   D = any shop
SHUFFLE: *|, CT, 23 / CT, 8w / 8w / w8
SHUFFLE: 1239 / 1239 / 1239 / 1239 / $444, wW / wW / wW / mm
NSUBST:  = = 1:+ / 1:==XX+ / *:=, - = 1:+ / 1:xxxx+ / *:x
SUBST:   X = x, 23 = 9, = : y:3 x:1, | : |:4 D:1, C : C:3 T:17
SUBST:   T = T:5 U:2 V:2 Y:1, x : x:13 y:1 c:1, G : G:1 c:1 y:8
SUBST:   z : y:3 v:1 b:1 x:1, y : c:13 v:1 b:1
MAP
               .........
               .xxx-xxx......
          ......x1...1xx-xxx.
          .xxx-xxxx+xxx1..1x...
        ...x1..1x.....xxx+xxxx.
        .xxxx+xxx..2..x....x1x.
        .x1x....x..G..+..9.+.x.
        .x.+.9..+..T..x....x.-.x
       x.-.x....x.....x....x1x......
   ......x1x....xxx+xxxxx+xxxxx-xxx.
   .xxx-xxxxx+xxxx1.1xxx.....x1..1x...
 ...x1..1x.....xxyy=yy1x...3.xxx+xxxx.
 .xxxx+xxx.3...x1y...y.+..G..x....x1x.
 .x1x....x..G..+.y.W.=.x.C...+..9.+.x.
 .x.+.9..+...C.x.=.0.y1x.....x....x.-.
 .-.x....x.....x1y.%.yyyxx+xxx....x1x...
..x1x....xxx+xxyyy.z.%9y1..1xxxx+xxxxxx.
.xxxxx+xxxx1..1y9%....%yy=yyyxx.....x1x.
.x1x.....xxyy=yy%.8*|8......y1x.....x.x.
.x.x.....x1y......|ww*.z%0W.=.+.TG2.+.-.
.-.+.2GT.+.=.W0%z.*ww|......y1x.....x.x.
.x.x.....x1y......8|*8.%yy=yyxx.....x1x.
.x1x.....xxyyy=yy%....%9y1..1xxxx+xxxxx.
.xxxxxx+xxxx1..1y9%.z.yyyxx+xxx....x1x..
...x1x....xxx+xxyyy.%.y.x.....x....x.-.
  .-.x....x.....x1y.0.=.x.C...+..9.+.x.
  .x.+.9..+...C.x.=.W.y.+..G..x....x1x.
  .x1x....x..G..+.y...y.x...3.xxx+xxxx.
  .xxxx+xxx.3...x1yy=yyxx.....x1..1x...
  ...x1..1x.....xxx1.1xxxx+xxxxx-xxx.
    .xxx-xxxxx+xxxxx+xxx....x1x......
    ......x1x....x.....x....x.-.x
        x.-.x....x..T..+..9.+.x.
         .x.+.9..+..G..x....x1x.
         .x1x....x..2..xxx+xxxx.
         .xxxx+xxx.....x1..1x...
         ...x1..1xxx+xxxx-xxx.
           .xxx-xx1...1x......
           ......xxx-xxx.
                .........
ENDMAP

###############################################################################

NAME:   minmay_spiral_squares
DEPTH:  D:12-, !D:$, Depths, !Depths:$
ORIENT: float
SUBST:  0 = 0., 9 = 9.
SUBST:  x : x:7 c:1 v:1 b:1
MAP
x@xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x.x|+......+.....+....+...+..+.@
x+xxx......x.....x....x...x00xxx
x.0xx......x.....x....x000xxxx|x
x.0xx......x.....x0000xxxxxxxx+x
x+xxx......x00000xxxxxx.+0.....x
x..0x000000xxxxxxx......x0.....x
x..0x+xxxxxx......xxxxxxx0.....x
x..0x..x9...xxxxxxx|x..9x0.....x
x+xxxx.x.xxxx.....9xx.x.x0.....x
x...0x.x.....xxxxxx.x.x.x0.....x
x...0x.xxxxxxx......x.x.xxxxxx+x
x...0x.x|x..x0xxxxxxx.xx.x0....x
x...0x.xx9x.xx....x0xx.x.x0....x
x+xxxxx.x.x.x.9xx9.x.x.x.x0....x
x....0x.x.x.x.xxxx.x.x.x.x0....x
x....0x.x.x.x.xxxx.x.x.x.x0....x
x....0x.x.x.x.9xx9.x.x.x.xxxxx+x
x....0x.x.xx0x....xx.x9xx.x0...x
x....0x.xx.xxxxxxx0x..x|x.x0...x
x+xxxxxx.x.x......xxxxxxx.x0...x
x.....0x.x.x.xxxxxx.....x.x0...x
x.....0x.x.xx9.....xxxx.x.xxxx+x
x.....0x9..x|xxxxxxx...9x..x0..x
x.....0xxxxxxx......xxxxxx+x0..x
x.....0x......xxxxxxx000000x0..x
x.....0+.xxxxxx00000x......xxx+x
x+xxxxxxxx0000x.....x......xx0.x
x|xxxx000x....x.....x......xx0.x
xxx00x...x....x.....x......xxx+x
@.+..+...+....+.....+......+|x.x
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx@x
ENDMAP

NAME:   minmay_five_octagons
DEPTH:  D:12-, !D:$, Depths, !Depths:$
ORIENT: float
KPROP:  8 = no_rtele_into
SUBST:  8 = .:130 8 |, 0 = 0 .:240
MAP
xxxxxxx@xxxxxxxxxxxxxxxx@xxxxxxx
xxxx0000000xxxxxxxxxx0000000xxxx
xxx000000000xxxxxxxx000000000xxx
xx00000000000xxxxxx00000000000xx
x0000000000000xxxx0000000000000x
x0000000000000xxxx0000000000000x
x0000000000000xxxx0000000000000x
@0000000000000xxxx0000000000000@
x0000000000000xxxx0000000000000x
x0000000000000xxxx0000000000000x
x0000000000000xxxx0000000000000x
xx00000000000mmmmmm00000000000xx
xxx000000000mm8888mm000000000xxx
xxxx0000000mm888888mm0000000xxxx
xxxxxxxxxxxm88888888mxxxxxxxxxxx
xxxxxxxxxxxm88888888mxxxxxxxxxxx
xxxxxxxxxxxm88888888mxxxxxxxxxxx
xxxxxxxxxxxm88888888mxxxxxxxxxxx
xxxx0000000mm888888mm0000000xxxx
xxx000000000mm8888mm000000000xxx
xx00000000000mmmmmm00000000000xx
x0000000000000xxxx0000000000000x
x0000000000000xxxx0000000000000x
x0000000000000xxxx0000000000000x
@0000000000000xxxx0000000000000@
x0000000000000xxxx0000000000000x
x0000000000000xxxx0000000000000x
x0000000000000xxxx0000000000000x
xx00000000000xxxxxx00000000000xx
xxx000000000xxxxxxxx000000000xxx
xxxx0000000xxxxxxxxxx0000000xxxx
xxxxxxx@xxxxxxxxxxxxxxxx@xxxxxxx
ENDMAP

NAME:   minmay_imperfect_grid
DEPTH:  D:8-, !D:$, Depths, !Depths:$
ORIENT: float
SUBST:  0 = 0., 9 = 9.
SUBST:  x : x:7 c:1 v:1 b:1
MAP
@xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x.x%xxx%xxx%xxx%xxx%xxx%xxx%xxxx
xx0x0x0x0x0x0x0x0x0x0x0x0x0x9xxx
x%x.x.xxx.xxx.xxx.xxx.xxx.xxx9xx
xx0x.x.x.x.x.x.x.x.x.x.x.x.xxx%x
xxx.x.x.xxx.xxx.xxx.xxx.xxx.x0xx
xx0x.x.x.x.x.x.x.x.x.x.x.xxx.xxx
x%xxx.x.x.xxx.xxx.xxx.xxx.x.x0xx
xx0x.x.x.x.x.x.x.x.x.x.xxx.xxx%x
xxx.xxx.x.x.xxx.xxx.xxx.x.x.x0xx
xx0x.x.x.x.x.x.x.x.x.xxx.xxx.xxx
x%xxx.xxx.x.x.xxx.xxx.x.x.x.x0xx
xx0x.x.x.x.x.x.x.x.xxx.xxx.xxx%x
xxx.xxx.xxx.x.x.xxx.x.x.x.x.x0xx
xx0x.x.x.x.x.x.x.xxx.xxx.xxx.xxx
x%xxx.xxx.xxx.x.x.x.x.x.x.x.x0xx
xx0x.x.x.x.x.x.x.x.xxx.xxx.xxx%x
xxx.xxx.xxx.xxx.x.x.x.x.x.x.x0xx
xx0x.x.x.x.x.xxx.x.x.xxx.xxx.xxx
x%xxx.xxx.xxx.x.x.x.x.x.x.x.x0xx
xx0x.x.x.x.xxx.xxx.x.x.xxx.xxx%x
xxx.xxx.xxx.x.x.x.x.x.x.x.x.x0xx
xx0x.x.x.xxx.xxx.xxx.x.x.xxx.xxx
x%xxx.xxx.x.x.x.x.x.x.x.x.x.x0xx
xx0x.x.xxx.xxx.xxx.xxx.x.x.xxx%x
xxx.xxx.x.x.x.x.x.x.x.x.x.x.x0xx
xx0x.xxx.xxx.xxx.xxx.xxx.x.x.xxx
x%xxx.x.x.x.x.x.x.x.x.x.x.x.x0xx
xx9xxx.xxx.xxx.xxx.xxx.xxx.x.x%x
xxx9x0x0x0x0x0x0x0x0x0x0x0x0x0xx
xxxx%xxx%xxx%xxx%xxx%xxx%xxx%x.x
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx@
ENDMAP

NAME:   minmay_quarter_circles
DEPTH:  D:8-, !D:$, Depths, !Depths:$
ORIENT: float
SUBST:  0 = 0., 9 = 99.
SUBST:  x : x:7 c:1 v:1 b:1
MAP
xxxxxxxxxxxxx@xxxxxxxxxxxxxxxxxx
@.............x*..0...0..+..0..x
x+xx..........x*..0...0..x..0..x
x..xxxx.......xx00xxxxx00x00x00x
x..0..xxx......x..9xxxx..x..x..x
x..0....xx.....x...xxxx..x..x..x
x00x.....x.....xx..9xxx..x..x..x
x..xxx...xx.....x....9x00x..x..x
x..xxxx...x.....xx....0..x..x..x
x..xxxxx00x......xxx..0..x..x..x
x..x...0..xx.......xxxx**x..x..x
x..x...0...x........+.xxxx00x..x
x..x00xxxx.+........x...0...x..x
x..x..x**xxxx.......xx..0...x..x
x..x..x..0..xxx......x00xxxxx..x
x..x..x..0....xx.....x...xxxx..x
x..x..x00x9....x.....xx...xxx..x
x..x..x..xxx9..xx.....x.....x00x
x..x..x..xxxx...x.....xx....0..x
x..x..x..xxxx9..x......xxx..0..x
x00x00x..xxxxx00xx.......xxxx..x
x..0..x......0..*x..........xx+x
x..0..+......0..*x.............@
xxxxxxxxxxxxxxxxxx@xxxxxxxxxxxxx
ENDMAP

NAME:   minmay_ornate_pinched
DEPTH:  D:8-, !D:$, Depths, !Depths:$
ORIENT: float
SUBST:  0 = 0., 9 = 99.
SUBST:  x : x:7 c:1 v:1 b:1
MAP
...........       ...........
.xx.......................xx.
.xxxxxx...............xxxxxx.
..xxxx.xxxx.......xxxx.xxxx..
..xxx.xx.0.xxxxxxx.0.xx.xxx..
..xx.x..x..xxxxxxx..x..x.xx..
..x.x.xxxxx..x.x..xxxxx.x.x..
...xx.xx...xx.0.xx...xx.xx...
...x.xx.x.0.xx.xx.0.x.xx.x...
...x0.x..x.x.xxx.x.x..x.0x...
...x..x.0.xxx...xxx.0.x..x...
 ...xx.x.xxx..x..xxx.x.xx...
 ...xx.xx.x.x.9.x.x.xx.xx...
 ...xxx.xx...x*x...xx.xxx...
 ...xx.0.x.x9*x*9x.x.0.xx...
 ...xxx.xx...x*x...xx.xxx...
 ...xx.xx.x.x.9.x.x.xx.xx...
 ...xx.x.xxx..x..xxx.x.xx...
...x..x.0.xxx...xxx.0.x..x...
...x0.x..x.x.xxx.x.x..x.0x...
...x.xx.x.0.xx.xx.0.x.xx.x...
...xx.xx...xx.0.xx...xx.xx...
..x.x.xxxxx..x.x..xxxxx.x.x..
..xx.x..x..xxxxxxx..x..x.xx..
..xxx.xx.0.xxxxxxx.0.xx.xxx..
..xxxx.xxxx.......xxxx.xxxx..
.xxxxxx...............xxxxxx.
.xx.......................xx.
...........       ...........
ENDMAP

NAME:   minmay_split_circle
DEPTH:  Depths, !Depths:$
ORIENT: float
SUBST:  0 = 01., 9 = 91.
SUBST:  c : cv b:1
MONS:   shapeshifter / glowing shapeshifter w:1
MAP
ccccccccccccccc@ccccccccccccccc
c.............................c
c.ccccccccccccc.ccccccccccccc.c
c.cccccccccc**c.c||cccccccccc.c
c.ccccccc.....c.c.....ccccccc.c
c.ccccc...0.c.c.c.c.....ccccc.c
c.cccc........c.c...9....cccc.c
c.ccc0........c.c.........ccc.c
c.ccc.........c.c.........ccc.c
c.cc....c...c.c.c.c...c....cc.c
c.cc......0...c.c.........9cc.c
c.cc..........c.c9.........cc.c
c.c%..........c.c..........*c.c
c.c%c...c...c.c.c.c...c...c*c.c
c.c%..........c.c..........*c.c
c.c%..0......0c.c..9.......*c.c
c.c%..........c.c..........*c.c
c.c%c...c...c.c.c.c...c...c*c.c
c.c%..........c.c..........*c.c
c.cc....0.....c.c.9.....9..cc.c
c.cc..........c.c..........cc.c
c.cc....c...c.c.c.c...c....cc.c
c.ccc.........c.c.........ccc.c
c.ccc0........c.c....9....ccc.c
c.cccc........c.c.......9cccc.c
c.ccccc.....c.c.c.c.....ccccc.c
c.ccccccc.....c.c.....ccccccc.c
c.cccccccccc0.c.c..cccccccccc.c
c.ccccccccccc.c.c.ccccccccccc.c
c.............c.c.............c
ccccccccccccccc@ccccccccccccccc
ENDMAP

NAME:   minmay_quantized_circle
DEPTH:  Depths, !Depths:$
ORIENT: float
SUBST:  0 = 0., 9 = 9., c : ccb, t : ttm, x : xxxxvb
MAP
xxxxxxxxxxxxxxx@xxxxxxxxxxxxxxx
x.x0x.x.x.x0x*x.x*x0x.x.x.x0x.x
xx.x0x.x.x.x9xx.xx9x.x.x.x0x.xx
x0x.x0x.x.x0x*x.x*x0x.x.x0x.x0x
xx0x.xxxxxxxxxx.xxxxxxxxxx.x0xx
x.x0x.xxxxxxx.....xxxxxxx.x0x.x
xx.xxx.xxxxx.......xxxxx.xxx.xx
x.x.xxx.x......t......x.xxx.x.x
xx.xxxxx......tct......xxxxx.xx
x.x.xxx...t....t....t...xxx.x.x
xx.xxxx..tct.......tct..xxxx.xx
x0x0xxx...t.........t...xxx0x0x
xx9xxx........ttt........xxx9xx
x*x*x........tccct........x*x*x
xxxxx...t...tccccct...t...xxxxx
@......tct..tccccct..tct......@
xxxxx...t...tccccct...t...xxxxx
x*x*x........tccct........x*x*x
xx9xxx........ttt........xxx9xx
x0x0xxx...t.........t...xxx0x0x
xx.xxxx..tct.......tct..xxxx.xx
x.x.xxx...t....t....t...xxx.x.x
xx.xxxxx......tct......xxxxx.xx
x.x.xxx.x......t......x.xxx.x.x
xx.xxx.xxxxx.......xxxxx.xxx.xx
x.x0x.xxxxxxx.....xxxxxxx.x0x.x
xx0x.xxxxxxxxxx.xxxxxxxxxx.x0xx
x0x.x0x.x.x0x*x.x*x0x.x.x0x.x0x
xx.x0x.x.x.x9xx.xx9x.x.x.x0x.xx
x.x0x.x.x.x0x*x.x*x0x.x.x.x0x.x
xxxxxxxxxxxxxxx@xxxxxxxxxxxxxxx
ENDMAP

NAME:   minmay_four_partial_circles
DEPTH:  D:8-, !D:$
ORIENT: float
WEIGHT: 7
SUBST:  ? : . x:8 b:1 v:1, ! : . x:2, 0 = 00.
SUBST:  x : x:9 c:1
MAP
  xxxxxx        xxxxxx
 xx0..0xx      xx0..0xx
xx......xx    xx......xx
x0..??..0x    x0..??..0x
x..????..x    x..????..x
x..????..x@..@x..????..x
x0..??............??..0x
xx....................xx
 xx0......!!!!......0xx
  xxxx...!!!!!!...xxxx
     @..!!!!!!!!..@
     ...!!!!!!!!...
     ...!!!!!!!!...
     @..!!!!!!!!..@
  xxxx...!!!!!!...xxxx
 xx0......!!!!......0xx
xx....................xx
x0..??............??..0x
x..????..x@..@x..????..x
x..????..x    x..????..x
x0..??..0x    x0..??..0x
xx......xx    xx......xx
 xx0..0xx      xx0..0xx
  xxxxxx        xxxxxx
ENDMAP

NAME:   minmay_eight_partial_circles
DEPTH:  D:12-, !D:$, Depths, !Depths:$
ORIENT: float
WEIGHT: 3
SUBST:  ? : . x:5 G:2 b:1 c:1 T:1, 0 = 0., 9 = 99., 8 = 8.
MAP
  xxxxx     xxxxx     xxxxx
 xx0.0xx   xx0.0xx   xx0.0xx
xx.....xx xx.....xx xx.....xx
x0.....0x x0.....0x x0.....0x
x...?...x@x...?...x@x...?...x
x0.........................0x
xx.........................xx
 xx0.....................0xx
  xxx.......xx+xx.......xxx
    @.....xxx...xxx.....@
  xxx....xx.......xx....xxx
 xx0.....x..xxxxx..x.....0xx
xx......xx.xx|||xx.xx......xx
x0......x9.x*****x.9x......0x
x...?...x..x..8..x..x...?...x
x0......x9.x.8.8.x.9x......0x
xx......xx.xx.9.xx.xx......xx
 xx0.....x..xx.xx..x.....0xx
  xxx....xx...9...xx....xxx
    @.....xxx9.9xxx.....@
  xxx.......xxxxx.......xxx
 xx0.....................0xx
xx.........................xx
x0.........................0x
x...?...x@x...?...x@x...?...x
x0.....0x x0.....0x x0.....0x
xx.....xx xx.....xx xx.....xx
 xx0.0xx   xx0.0xx   xx0.0xx
  xxxxx     xxxxx     xxxxx
ENDMAP

NAME:   minmay_swirls_squares
DEPTH:  D:12-, !D:$, Depths, !Depths:$
ORIENT: float
SUBST:  9 = 999.
SUBST:  x : x:7 c:1 v:1 b:1
MAP
...............................
.xxxxx.........x.xx.xxx......x.
.....xxx....xx...xx.xxx.xx...x.
...x...xx...xx......xxx.xx.x.x.
........xx...................x.
..xx.x...x..xxx......xxxxx..xx.
..xx.x...xx.xxx....xxx......x..
.....x....x.xxx...xx.......xx..
.xxx.x....x......xx.......xx...
.xxx.xx...x..xx..x9.....xxx....
.xxx..x...xx.xx.xx99xxxxx......
......xx..9x..9.x|xxx..........
.xx....xx99xx9x9xxx....xxx.xx..
.xx.....xxx|x***x*9.xx.xxx.xx..
..........xxxxxxx*x9xx.xxx.....
.x..........9*xxx*9..........x.
.....xxx.xx9x*xxxxxxx..........
..xx.xxx.xx.9*x***x|xxx.....xx.
..xx.xxx....xxx9x9xx99xx....xx.
..........xxx|x.9..x9..xx......
......xxxxx99xx.xx.xx...x..xxx.
....xxx.....9x..xx..x...xx.xxx.
...xx.......xx......x....x.xxx.
..xx.......xx...xxx.x....x.....
..x......xxx....xxx.xx...x.xx..
.xx..xxxxx......xxx..x...x.xx..
.x...................xx........
.x.x.xx.xxx......xx...xx...x...
.x...xx.xxx.xx...xx....xxx.....
.x......xxx.xx.x.........xxxxx.
...............................
ENDMAP

NAME:    minmay_big_diamond_grid
DEPTH:   D:12-, !D:$, Depths, !Depths:$
ORIENT:  float
SHUFFLE: ABC
SUBST:   A = ., B = ., C = x, 0 = 0., 9 = 9.
SUBST:   x : x:7 c:1 v:1 b:1
MAP
x@xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx@x
x.xxx0......0xx0......0xx0......0xx0......0xxx.x
x.xxx........xx........xx........xx........xxx.x
x.x0.x......x..x......x00x......x..x......x.0x.x
x.x...x....x....x....x....x....x....x....x...x.x
x.x....x..x......x..x......x..x......x..x....x.x
x.A.....xx........xx0..**..0xx........xx.....B.x
x.A.....xx........xx0..**..0xx........xx.....B.x
x.x....x..x......x00x......x00x......x..x....x.x
x.x...x....x....x....x....x....x....x....x...x.x
x.x0.x......x..x......x99x......x..x......x.0x.x
x.xxx........xx0..**..9xx9..**..0xx........xxx.x
x.xxx........xx0..**..9xx9..**..0xx........xxx.x
x.x0.x......x..x......x99x......x..x......x.0x.x
x.x...x....x....x....x....x....x....x....x...x.x
x.x....x..x......x00x......x00x......x..x....x.x
x.B.....xx........xx0..**..0xx........xx.....A.x
x.B.....xx........xx0..**..0xx........xx.....A.x
x.x....x..x......x..x......x..x......x..x....x.x
x.x...x....x....x....x....x....x....x....x...x.x
x.x0.x......x..x......x00x......x..x......x.0x.x
x.xxx........xx........xx........xx........xxx.x
x.xxx0......0xx0......0xx0......0xx0......0xxx.x
x@xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx@x
ENDMAP

NAME:   minmay_blocking_statues
DEPTH:  D:12-, !D:$, Depths, !Depths:$
ORIENT: float
SUBST:  0 = 0., 9 = 99.
MAP
@xxxxxxxxxxxxxxxxxx
.xxxxxxxxxxxx99***x
..GxxG....GxxGxxx*x
.0.xx.0..0.xx.xxx*x
...G...xx.....xxx9x
...G...xx......0x9x
.0.xx.0..0.xx...x0x
..GxxG....Gxx...x0x
.xxxxxx..xxxxxx.00x
.xxxxxx..xxxxxx.00x
..GxxG....Gxx...x0x
.0.xx.0..0.xx...x0x
.......xx...G..0x9x
.......xx...G.xxx9x
.0.xx.0..0.xx.xxx*x
..GxxG....GxxGxxx*x
.xxxxxxxxxxxx99***x
@xxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   minmay_flawed_box
DEPTH:  D:6-10
ORIENT: float
SUBST:  X = x.
MONS:   orc wizard / nothing
MAP
....................
.xxxxX...........Xx.
.XxxxxX.XxxX....Xxx.
..XxxX...XxxX..xxxx.
...xxX........Xxxxx.
...xxxX.......Xx.xX.
...xXxx......xxx.x..
...X.Xx......xX..x..
......xX..11Xx...X..
...X..xxx11Xxx......
...x...XxXXxx....X..
..Xx...1xxxxX1...x..
..xx..1.XxxX1...Xx..
..xX...1XxxX11..xx..
..X....1xxxxx...xX..
......xxxX1XxX..x...
..X...xX11..xxX.X...
..x..Xx...1.Xxx.....
..xXXxx......XxXX...
.XxxxxX.......xxx...
.xxxX.........Xxx...
.xxxxxX.xxX...XxxX..
.xxX....XxxX.XxxxxX.
.xX...........Xxxxx.
....................
ENDMAP

NAME:   minmay_four_inscribed_fans
DEPTH:  Depths
ORIENT: float
SUBST:  x : x:7 c:1 v:1 b:1
SUBST:  9 = 999.
MAP
            ....             ....
            .xx...           .xx...
   ...     ..xxxx.. ...     ..xxxx..
   .x..    .xxxxxx...x..    .xxxxxx...
  ..xx...  .xx%%xxxx.xx...  .xx%%xxxx.
 ..xxxxx....xx%%xxx.xxxxx....xx%%xxx..
 .xxxxxxxxxx.9xxxx.xxxxxxxxxx.9xxxx..
..xx%%xxxxxx...xxx.xx%%xxxxxx...xxx.
.xxx%%x...x....xx.xxx%%x...x....xx..
.xxxxx9....9x..xx.xxxxx9....9x..xx.
...xxx...xxmx.xxx...xxx...xxmx.xxx.
  ....xx.9m|m9.xx..00..xx.9m|m9.xx....
     .xxx.xmxx...xxx...xxx.xmxx...xxx...
     .xx..x9....9xxxxx.xx..x9....9xxxxx.
    ..xx....x...x%%xxx.xx....x...x%%xxx.
    .xxx...xxxxxx%%xx.xxx...xxxxxx%%xx..
   ..xxxx9.xxxxxxxxxx.xxxx9.xxxxxxxxxx.
  ..xxx%%xx....xxxxx.xxx%%xx....xxxxx..
  .xxxx%%xx..xx..xx9xxxx%%xx..xx..xx..
  ...xxxxxx..xxxx.x**9xxxxxx..xxxx.x.
   .x.xxxx..xxxxxx9**x.xxxx..xxxxxx...
  ..xx..xx..xx%%xxxx9xx..xx..xx%%xxxx.
 ..xxxxx....xx%%xxx.xxxxx....xx%%xxx..
 .xxxxxxxxxx.9xxxx.xxxxxxxxxx.9xxxx..
..xx%%xxxxxx...xxx.xx%%xxxxxx...xxx.
.xxx%%x...x....xx.xxx%%x...x....xx..
.xxxxx9....9x..xx.xxxxx9....9x..xx.
...xxx...xxmx.xxx...xxx...xxmx.xxx.
  ....xx.9m|m9.xx......xx.9m|m9.xx....
     .xxx.xmxx...xxx...xxx.xmxx...xxx...
     .xx..x9....9xxxxx.xx..x9....9xxxxx.
    ..xx....x...x%%xxx.xx....x...x%%xxx.
    .xxx...xxxxxx%%xx.xxx...xxxxxx%%xx..
   ..xxxx9.xxxxxxxxxx.xxxx9.xxxxxxxxxx.
  ..xxx%%xx....xxxxx.xxx%%xx....xxxxx..
  .xxxx%%xx.  ...xx.xxxx%%xx.  ...xx..
  ...xxxxxx.    ..x...xxxxxx.    ..x.
    ..xxxx..     ... ..xxxx..     ...
     ...xx.           ...xx.
       ....             ....
ENDMAP

NAME:   minmay_concentric_multi
DEPTH:  D:12-, Depths
ORIENT: float
NSUBST: A = 1:+ / *:+xx, B = 1:+ / *:+xx, C = 1:+ / *:+xx
SUBST:  x : x:7 c:1 v:1 b:1
SUBST:  9 = 999., 0 = 000.
MAP
      .....................
     ...........x...........
    ...........xxx...........
   ...........xx9xx...........
  ...........xx...xx...........
 ...........xx.....xx...........
......xxxAxxx.......xxxAxxx......
......x.....0.......0.....x......
......x.....xxxxBxxxx.....x......
......A....xx.0...0.xx....A......
......x...xx..x...x..xx...x......
......x..xx...xx.xx...xx..x......
.....xx0xx....xxCxx....xx0xx.....
....xx..x....9x...x9....x..xx....
...xx...x0xxxxx.9.xxxxx0x...xx...
..xx....x..xx...|...xx..x....xx..
.xx9....B...C.9|x|9.C...B....9xx.
..xx....x..xx...|...xx..x....xx..
...xx...x0xxxxx.9.xxxxx0x...xx...
....xx..x....9x...x9....x..xx....
.....xx0xx....xxCxx....xx0xx.....
......x..xx...xx.xx...xx..x......
......x...xx..x...x..xx...x......
......A....xx.0...0.xx....A......
......x.....xxxxBxxxx.....x......
......x.....0.......0.....x......
......xxxAxxx.......xxxAxxx......
 ...........xx.....xx...........
  ...........xx...xx...........
   ...........xx9xx...........
    ...........xxx...........
     ...........x...........
      .....................
ENDMAP

NAME:   minmay_diamond_stairs
DEPTH:  D:8-, !D:$, Depths:2-, !Depths:$
ORIENT: float
SUBST:  x : x:7 c:1 v:1 b:1
SUBST:  F : .:40 GTUb
SUBST:  9 = 9.
MAP
            .......
          ....9x9....
        .....xxxxx.....
      .....xxx<.>xxx.....
    .....xxx.x.8.x.xxx.....
  .....xxx...x...x...xxx.....
.....xxx...F.x...x.F...xxx.....
..9xxx.......x...x.......xxx9..
.xxx{........+...+........}xxx.
..9xxx.......x...x.......xxx9..
.....xxx...F.x...x.F...xxx.....
  .....xxx...x...x...xxx.....
    .....xxx.x.8.x.xxx.....
      .....xxx<.>xxx.....
        .....xxxxx.....
          ....9x9....
            .......
ENDMAP

NAME:   minmay_octo_star
DEPTH:  D:12-, Depths
ORIENT: float
SUBST:  x : x:7 c:1 v:1 b:1
SUBST:  9 = 999.
MAP
     .....................
    .........xx+xx.........
   ..........xx.xx..........
  ....xx.....x...x.....xx....
 ....xxxx....x...x....xxxx....
....xx**xx...x...x...xx**xx....
...xx**..xx..x...x..xx..**xx...
...xx*....xx.x...x.xx....*xx...
....xx..8..xxx.9.xxx..8..xx....
.....xx.....xx....xx....xx.....
......xx..9..xx....x+..xx......
.......xx.....xx....xxxx.......
........xx..9..xx....xx........
.xxxxxxxxxx.....x+....xxxxxxxx.
.xx.......xx..9..xx.........xx.
.+......9..xx.....xx..9......+.
.xx.........xx..9..xx.......xx.
.xxxxxxxx....+x.....xxxxxxxxxx.
........xx....xx..9..xx........
.......xxxx....xx.....xx.......
......xx..+x....xx..9..xx......
.....xx....xx....xx.....xx.....
....xx..8..xxx.9.xxx..8..xx....
...xx*....xx.x...x.xx....*xx...
...xx**..xx..x...x..xx..**xx...
....xx**xx...x...x...xx**xx....
 ....xxxx....x...x....xxxx....
  ....xx.....x...x.....xx....
   ..........xx.xx..........
    .........xx+xx.........
     .....................
ENDMAP

NAME:   minmay_central_circle
DEPTH:  Depths
ORIENT: float
SUBST:  x : x:8 c:1 v:1
SUBST:  9 = 999., 8 = 888.
MAP
.................................
...xxxxx.......xxx.......xxxxx...
..xx...xx.....xx.xx.....xx...xx..
.xx..9..xx....x.9.x....xx..9..xx.
.x.......x....xx.xx....x.......x.
.x.9.8.9.x.....x.x.....x.9.8.9.x.
.x.......x.............x.......x.
.xx..9...x....xx+xx....x...9..xx.
..xx........xxx...xxx........xx..
...xxxxx..xxx.......xxx..xxxxx...
.........x+.....9.....+x.........
.........x....9...9....x.........
........xx..9.xx+xx.9..xx........
........x....+x***x+....x........
..xxx..xx..9xx**b**xx9..xx..xxx..
.xx.xx.x....x***b***x....x.xx.xx.
.x.9...+..9.+*bbbbb*+.9..+...9.x.
.xx.xx.x....x***b***x....x.xx.xx.
..xxx..xx..9xx**b**xx9..xx..xxx..
........x....+x***x+....x........
........xx..9.xx+xx.9..xx........
.........x....9...9....x.........
.........x+.....9.....+x.........
...xxxxx..xxx.......xxx..xxxxx...
..xx........xxx...xxx........xx..
.xx..9...x....xx+xx....x...9..xx.
.x.......x.............x.......x.
.x.9.8.9.x.....x.x.....x.9.8.9.x.
.x.......x....xx.xx....x.......x.
.xx..9..xx....x.9.x....xx..9..xx.
..xx...xx.....xx.xx.....xx...xx..
...xxxxx.......xxx.......xxxxx...
.................................
ENDMAP

NAME:   minmay_strange_plus
DEPTH:  D:12-, Depths
ORIENT: float
SUBST:  9 = 999., 8 = 888.
MAP
        .............
       ....nnn.nnn....
      ....nn.....nn....
     ....nn.nn.nn.nn....
    .....n.nn...nn.n.....
   ......n.n.....n.n......
  ....9.......9.......9....
 ........n.n.....n.n........
........nn.n.....n.nn........
...nnn.nn.nn.....nn.nn.nnn...
..nn.....nn.......nn.....nn..
.nn.nn.nnn....8....nnn.nn.nn.
.n.nn...................nn.n.
.n.n.........|||.........n.n.
......9....8.|||.8....9......
.n.n.........|||.........n.n.
.n.nn...................nn.n.
.nn.nn.nnn....8....nnn.nn.nn.
..nn.....nn.......nn.....nn..
...nnn.nn.nn.....nn.nn.nnn...
........nn.n.....n.nn........
 ........n.n.....n.n........
  ....9.......9.......9....
   ......n.n.....n.n......
    .....n.nn...nn.n.....
     ....nn.nn.nn.nn....
      ....nn.....nn....
       ....nnn.nnn....
        .............
ENDMAP

NAME:   minmay_cut_squares
DEPTH:  D:8-, Depths
ORIENT: float
SUBST:  9 = 9.
SUBST:  x : x:7 c:1 v:1 b:1
MAP
.......................
.xxxxxx....xxxxxx....x.
.x|........x|..........
.x.........x......xx...
.x..9......x..9...xx...
.x...9.....x...9.......
.x.........x....x....x.
.......9.........9...x.
........9....xx...9..x.
.............xx......x.
....................|x.
.xxxxxx....x....xxxxxx.
.x|....................
.x......xx.............
.x..9...xx....9........
.x...9.........9.......
.x....x....x.........x.
.......9...x.....9...x.
...xx...9..x......9..x.
...xx......x.........x.
..........|x........|x.
.x....xxxxxx....xxxxxx.
.......................
ENDMAP

NAME:   minmay_bad_star
DEPTH:  D:12-, Depths
ORIENT: float
SUBST:  9 = 9., 8 = 8.
SUBST:  x : x:7 c:1 v:1 b:1
MAP
... . . . ...... . . . ...
.x.........x99x.........x.
..xx.......x..x.......xx..
 .x*xxx.....xx.....xxx*x.
...x99xxx...xx...xxx99x...
 ..x9x..xx..xx..xx..x9x..
...xx.x..xxx..xxx..x.xx...
 ...x..x...xxxx...x..x...
....xx..x.x.xx.x.x..xx....
 ....xx..xxx**xxx..xx....
......x.xxx....xxx.x......
.xx...xx.x.x88x.x.xx...xx.
.9.xxx*xx*.8xx8.*xx*xxx.9.
.9.xxx*xx*.8xx8.*xx*xxx.9.
.xx...xx.x.x88x.x.xx...xx.
......x.xxx....xxx.x......
 ....xx..xxx**xxx..xx....
....xx..x.x.xx.x.x..xx....
 ...x..x...xxxx...x..x...
...xx.x..xxx..xxx..x.xx...
 ..x9x..xx..xx..xx..x9x..
...x99xxx...xx...xxx99x...
 .x*xxx.....xx.....xxx*x.
..xx.......x..x.......xx..
.x.........x99x.........x.
... . . . ...... . . . ...
ENDMAP

NAME:   minmay_door_path
DEPTH:  D:1-4
ORIENT: float
SUBST:  0 = .0%
SUBST:  ? : .GTV
MAP
xxxxxxxxxxxxx
@...........x
x.xxxx.xxxx.x
x.x0.+.+..x.x
x.x..x.x.?x.x
x.x+xx.xxxx.x
x...........x
x.x+xx.xxxx.x
x.x..+.+.0x.x
x.x0.x.x..x.x
x.xxxx.xx+x.x
x...........x
x.xxxx.xx+x.x
x.x0.+.+..x.x
x.x..x.x.0x.x
x.x+xx.xxxx.x
x...........x
x.x+xx.xxxx.x
x.x..+.+.0x.x
x.x0.x.x..x.x
x.xxxx.xx+x.x
x...........x
x.xxxx.xx+x.x
x.x..+.+..x.x
x.x?.x.x.0x.x
x.xxxx.xxxx.x
x...........@
xxxxxxxxxxxxx
ENDMAP

NAME:   minmay_tri_box
DEPTH:  D:8-, Depths
ORIENT: float
SUBST:  9 = 9.
SUBST:  x : x:7 c:1 v:1 b:1
MAP
xxxxxxxxx.@.xxxxxxxxx
x......xx...xx......x
x9.xx..x..x..x..xx.9x
x99x..xx..x..xx..x99x
x.xx..x..xxx..x..xx.x
x|x..xx..x|x..xx..x|x
xxx..x..xx.xx..x..xxx
xx..xx..x999x..xx..xx
xx..x..xx...xx..x..xx
x..xx..x..x..x..xx..x
x..x..xx..x..xx..x..x
x.xx..x..xxx..x..xx.x
x.x..xx..x|x..xx..x.x
x.x..x..xx.xx..x..x.x
x...xx..x...x..xx...x
x...x..xx...xx..x...x
x..xx..x..x..x..xx..x
x..x..xx99x99xx..x..x
x.xx.............xx.x
x.....xx.....xx.....x
x.xx.............xx.x
@...................@
xxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   minmay_dotted_concentric_squares
DEPTH:  D:12-, Depths
ORIENT: float
SUBST:  9 = 9....
SUBST:  x : x:7 c:1 v:1 b:1
SUBST:  + : + G:1
MAP
...........................
.xxx9xxx9xxx9x9xxx9xxx9xxx.
.x.......................x.
.x.xxx9xxx9xx9xx9xxx9xxx.x.
.9.x...................x.9.
.x.x.xxx9xxx9x9xxx9xxx.x.x.
.x.9.x...............x.9.x.
.x.x.x.xxx9xx9xx9xxx.x.x.x.
.9.x.9.x...........x.9.x.9.
.x.x.x.x.xxx9x9xxx.x.x.x.x.
.x.9.x.9.x.......x.9.x.9.x.
.x.x.x.x.x.xxxxx.x.x.x.x.x.
.9.x.9.x.9.x***x.9.x.9.x.9.
.x.9.x.9.x.x***x.x.9.x.9.x.
.9.x.9.x.9.x***x.9.x.9.x.9.
.x.x.x.x.x.xx+xx.x.x.x.x.x.
.x.9.x.9.x.......x.9.x.9.x.
.x.x.x.x.xxx+x+xxx.x.x.x.x.
.9.x.9.x...........x.9.x.9.
.x.x.x.xxx+xx+xx+xxx.x.x.x.
.x.9.x...............x.9.x.
.x.x.xxx+xxx+x+xxx+xxx.x.x.
.9.x...................x.9.
.x.xxx+xxx+xx+xx+xxx+xxx.x.
.x.......................x.
.xxx+xxx+xxx+x+xxx+xxx+xxx.
...........................
ENDMAP

NAME:   minmay_textured_paths
DEPTH:  D:12-, Depths
ORIENT: float
SUBST:  8 = 888., 9 = 9.
SUBST:  ? = .:70 GTUVm<>
SUBST:  x : x:7 c:1 v:1 b:1
MAP
...........................
.xxxxxxxxxxxxxxxxxxxx.x.xx.
.x.x.x.x.x.x.x.x..99x.x..x.
.x.x.x.x.x.x.x.x...9xx.x.x.
..x.x.x.x.x.x.x.....x..x.x.
..x.x.x.x.x.x.x.....x.x.xx.
.xxxxxxxxxxxxxxx.x.xx.x..x.
.x99..xxx.x...8xx.x.xx.x.x.
.x9....x.x.....x.x.xx..x.x.
.x.....x.x.....xx.x.x.x.xx.
.x....x.xxx....x.x.xx.x..x.
.x.x..xxxxx.........xx.x.x.
.x..x.x8....***.....x..x.x.
.x..xxx.....*?*.....x.x.xx.
.xxx..x.....***....8x.x..x.
.x.x..x.........xxxxxx.x.x.
.x..x.xx.xxx....x...x....x.
.x..xxx.xxxx....x.x.x....x.
.xxx..xx.xxx....x.x.x...9x.
.x.x..xxx.xx8.....x....99x.
.x..x.xxxx.xxxxxxxxxxxxxxx.
.x..xxx......x...x...x...x.
.xxx..x.....x...x...x...x..
.x.x..x9...x...x...x...x...
.x..x.x99.....x...x...x....
.x..xxxxxxxxxxxxxxxxxxxxxx.
...........................
ENDMAP

NAME:   minmay_distorted_concentric_squares
DEPTH:  D:8-, Depths
ORIENT: float
SUBST:  9 = 9., 0 = 0.
SUBST:  x : x:27 c:1 v:1 b:1
MAP
...................
..xxxxxxxx.........
..x.......xxxxxxxx.
..x.xxxxxx0......x.
..x.x0....xxxxxx.x.
..x.x.xxxx....0x.x.
..x.x.x9..xxxx.x.x.
..x.x.x.xx..9x.x.x.
..x0x.x.x*xx.x.x.x.
.x.x.x.x***x.x.x.x.
.x.x.x.xx*x.x.x0x..
.x.x.x9..xx.x.x.x..
.x.x.xxxx..9x.x.x..
.x.x0....xxxx.x.x..
.x.xxxxxx....0x.x..
.x......0xxxxxx.x..
.xxxxxxxx.......x..
.........xxxxxxxx..
...................
ENDMAP

NAME:   minmay_containers
DEPTH:  D:12-, Depths
ORIENT: float
SUBST:  C : B C:5
SUBST:  B : A B:5
SUBST:  A : x c:1 v:1 b:1
SUBST:  B : xcvb
SUBST:  C : .:50 G:50 T U V m
SUBST:  8 = 88., 9 = 9.
MAP
@xxxxxxxxxxxxxxxxxxxxxxx
x....................xxx
x.....................xx
x....AAAAAAAAAAAAAA.9..x
x...........9..........x
x..A.9..BBBBB..B..9.A..x
x..A...BB***B..BB...A..x
x..A..BBB...B..BBB..A..x
x..A.BBBB...B..BBBB.A..x
x..A.B*.............A..x
x..A.B*...8.........A..x
x..A.B*....8C..BBBB9A..x
x..A9BBBB..C8....*B.A..x
x..A.........8...*B.A..x
x..A.............*B.A..x
x..A.BBBB..B...BBBB.A..x
x..A..BBB..B...BBB..A..x
x..A...BB..B***BB...A..x
x..A.9..B..BBBBB..9.A..x
x..........9...........x
x..9.AAAAAAAAAAAAAA....x
xx.....................x
xxx....................x
xxxxxxxxxxxxxxxxxxxxxxx@
ENDMAP

NAME:   minmay_curly_fans
DEPTH:  D:8-, Depths
ORIENT: float
SUBST:  x : x:7 c:1 v:1 b:1
SUBST:  9 = 9.
MAP
...................
.xxxxxxxxx.......x.
...xxx..xx.......x.
....x....xx.....xx.
....x.....xxx.xxxx.
.......x........xx.
....x..x9........x.
....x..xx.9xxx...x.
...xx..9x*xx9...xx.
.xxx....***....xxx.
.xx...9xx*x9..xx...
.x...xxx9.xx..x....
.x........9x..x....
.xx........x.......
.xxxx.xxx.....x....
.xx.....xx....x....
.x.......xx..xxx...
.x.......xxxxxxxxx.
...................
ENDMAP

NAME:   minmay_crescents
DEPTH:  D:12-, Depths
ORIENT: float
SUBST:  x : x:7 c:1 v:1 b:1
SUBST:  G = .GTU
SUBST:  9 = 9....
MAP
  xxxxxxx       xxxxxxx
 xxxxxxxxx     xxxxxxxxx
xxxx*99..xxx@xxx..99*xxxx
xxx999....x...x....999xxx
xx*99xxxxx.....xxxxx99*xx
xx99xxxxxxx...xxxxxxx99xx
xx9.xx*99.......99*xx.9xx
xx..xx99.........99xx..xx
xx..xx9.xxx...xxx.9xx..xx
 xx.xx..xxx...xxx..xx.xx
  xx.x..xx*.9.*xx..x.xx
  x........999........x
  @.......99G99.......@
  x........999........x
  xx.x..xx*.9.*xx..x.xx
 xx.xx..xxx...xxx..xx.xx
xx..xx9.xxx...xxx.9xx..xx
xx..xx99.........99xx..xx
xx9.xx*99.......99*xx.9xx
xx99xxxxxxx...xxxxxxx99xx
xx*99xxxxx.....xxxxx99*xx
xxx999....x...x....999xxx
xxxx*99..xxx@xxx..99*xxxx
 xxxxxxxxx     xxxxxxxxx
  xxxxxxx       xxxxxxx
ENDMAP

NAME:    minmay_nine_rooms
DEPTH:   Depths
ORIENT:  float
SHUFFLE: ABCDE
SUBST:   A : c:7 x:1 b:1 v:1
SUBST:   B : .:5 c:7 x:1 b:1 v:1
SUBST:   C : .:10 c:7 x:1 b:1 v:1
SUBST:   D : .:20 c:7 x:1 b:1 v:1
SUBST:   E : .:30 c:7 x:1 b:1 v:1
SUBST:   c : c:8 b:1 v:1
SUBST:   0 = 0....., 1 = 0...., 2 = 00...
SUBST:   3 = 9........., 4 = 99....., 5 = 99...
SUBST:   6 = 8..., 7 = 8888...
MAP
ccccccccccccccccccccccccc
........c...............c
c.A0EBA.c.C1B1C.c.22B2E.c
c.BDC0D.c.EA1A1.c.2C2CD.c
c.0C0C0.c.B1D1B.c.BDADB.c
c.D0CDB.c.1A1AE.c.DC2C2.c
c.ABE0A.c.C1B1C.c.E2B22.c
c...............c.......c
ccccccccccccccccccccccc.c
c...............c.......c
c.DC5AD.c.ECDB4.c.BDA3B.c
c.A555C.c.C4A4B.c.333ED.c
c.5EBE5.c.DA4AD.c.A3C3A.c
c.C555A.c.B4A4C.c.DE333.c
c.DA5CD.c.4BDCE.c.B3ADB.c
c.......c...............c
c.ccccccccccccccccccccccc
c.......c...............c
c.6AD6C.c.A7DCB.c.*****.c
c.ACBD6.c.CB7AE.c.*|||*.c
c.B6E6B.c.D777D.c.*|G|*.c
c.6DBCA.c.EA7BC.c.*|||*.c
c.C6DA6.c.BCD7A.c.*****.c
c...............c.......c
ccccccccccccccccccccccccc
ENDMAP

###############################################################################

NAME:    hangedman_arch_fort
TAGS:    transparent no_monster_gen no_item_gen no_pool_fixup
ORIENT:  float
DEPTH:   Depths, !Depths:$
SUBST:   Y : y:8 b:2, y : c:9 b:1, z : Z:16 x:1 b:2 ":1, Z : x:9 b:1
SUBST:   X = .:19 x:1, G : G:12 c:1 b:1 v:1, w : w:3 l:1 .:1
SUBST:   + : +:24 W:1, " : .:24 W:1, 9 = 990, % = %%.
MAP
           ....  ..........  ...
          ........................
         ..Zxxx++xxxZ..Zxxx++xxxZ..
      .....x9%z..z%9x..x9%z..z%9x.....
    .......x%."..".%x..x%."..".%x.......
    .Zxxx++xz""..""zx++xz""..""zx++xxxZ.
   ..x9%z.......X..........X.......z%9x..
   ..x%."..........................".%x..
   ..xz""..yyyY..""zx++xz""..Yyyy..""zx..
  ...+.......9y..".%x..x%."..y9.......+...
 ....+....w%|.y..z%9x..x9%z..y.|%w....+....
 .Zxxx..y.%G%.y..xxxZ..Zxxx..y.%G%.y..xxxZ.
..x9%z..y.*%w....+........+....w%*.y..z%9x..
..x%."..y9.......+...  ...+.......9y..".%z..
..xz""..Yyyy..""zx..    ..xz""..yyyY..""zx..
 .+...........".%x..    ..x%."...........+.
 .+...X.......z%9x..    ..x9%x.......X...+.
..xz""..""zx++xxxZ..    ..Zxxx++xz""..""zx..
..x%."..".%x.......      .......x%."..".%x..
..x9%z..z%9x.....          .....x9%z..z%9x..
..Zxxx++xxxZ..                ..xxxx++xxxZ..
 ............                  ............
  ....  ....                    ....  ....
ENDMAP

###############################################################################

NAME:   minmay_rotating_shell
ORIENT: float
DEPTH:  Depths
WEIGHT: 3
# 27 / 3 items!
KITEM:  d = |,|,|,|,|,|,|,|,|
SUBST:  A : ABB
SUBST:  B : BCC
SUBST:  C : CDD
SUBST:  D : DEE
SUBST:  E : .:100 x:1 v:1 b:1 G:1
SUBST:  D : .:100 x v b G:4
SUBST:  A : .:100 x v b:20 G:16
SUBST:  B : .:100 x v b:40 G:64
SUBST:  C : .:100 x v b:80 G:128
SUBST:  0 = 0..
SUBST:  9 = 9..
SUBST:  8 = 8.
MAP
          .............
        ...vvvvvvvvvvv...
      ...vvv....+...0vvv...
     ..vvv...vvvv.DD..0vvv..
    ..vv...vvv..v0.DDD..0vv..
   ..vv..vvv....vv0..DDD.0vv..
  ..vv..vv...AA.vvvv0..DD.0vv..
  .vv..vv..AAA..+..vvv0.DD.0vv.
 ..v..vv..AA..9vvv...vv0.DD.0v..
 .vv.vv..AA.9vvvvvvv..vv0.DD.vv.
..v..v..AA.9vv.....vv..vv0.D.0v..
.vv.vv.AA.9vv..vvv..vv..vv.DD.vv.
.v..v..A.9vv..vvvvv..vv..v0.D.0v.
.v.vv.AA.vv..vv.+8vv..vv.vv.DD.v.
.v.v..A.9v..vv..v88vv..v..v0.D.v.
.v.v...9vv.vv..vv888vv.vv.vv...v.
.v.v+vvvvv+vv.vvdvvvvv+vv.vv+vvv.
.v.v.vv....vv..vvv8....vv.vv.v0..
.v.v..v9.B.9vv..v..CC.8v..v..v.E.
.v.vv.vv.BB.9vv.+.CC.8vv.vv.vv.E.
.v..v..v9.BB.9vvv.C.8vv..v..v0.E.
.vv.vv.vv9.BB.9vv..8vv..vv.vv.EE.
..v..v..vv9.BB.9v.8vv..vv..v0.E..
 .vv.vv..vv9.BB.vvvv..vv..vv.EE..
 ..v..vv..vv9...vv...vv..vv0.E...
  .vv..vv..vvv9.+..vvv..vv0.EE...
  ..vv..vv...vvvvvvv...vv0.EE....
   ..vv..vvv...vvv...vvv0.EE.....
    ..vv...vvv.....vvv0..EE......
     ..vvv...vvvvvvv0..EEE.......
      ...vvv....+....EEE.........
        ...vvvvvv.EEEE...........
          .......................
ENDMAP

###############################################################################

NAME:   grunt_crosshair
DEPTH:  Depths
PLACE:  Depths:$
ORIENT: centre
: if you.where() == dgn.level_name(dgn.br_entrance("Zot")) then
KMONS:  8 = golden dragon / shadow dragon / storm dragon / bone dragon \
           / fire dragon / ice dragon
KMONS:  9 = nonbase draconian
KMONS:  0 = base draconian
KFEAT:  O = enter_zot
COLOUR: c = lightmagenta
COLOUR: -+8|O = magenta
FTILE:  -+8|O = floor_hall
KITEM:  | = nothing
: else
KMONS:  8 = 8 band
KMONS:  9 = 9 band
KMONS:  0 = 0 band
KITEM:  O = superb_item
KITEM:  9 = any good_item
: end
MAP
      x@x
      x.x
   xxxx.xxxx
  xx0.0.0.0xx
  x0xxx9xxx0x
  x.xcc+ccx.x
xxx0xc|8|cx0xxx
@...9+8O8+9...@
xxx0xc|8|cx0xxx
  x.xcc+ccx.x
  x0xxx9xxx0x
  xx0.0.0.0xx
   xxxx.xxxx
      x.x
      x@x
ENDMAP

NAME:   grunt_x_marks_the_spot
DEPTH:  Depths
PLACE:  Depths:$
ORIENT: centre
: if you.where() == dgn.level_name(dgn.br_entrance("Zot")) then
KMONS:  8 = golden dragon / shadow dragon / storm dragon / bone dragon \
           / fire dragon / ice dragon
KMONS:  9 = nonbase draconian
KMONS:  0 = base draconian
KFEAT:  O = enter_zot
COLOUR: c = lightmagenta
COLOUR: -+8|O = magenta
FTILE:  -+8|O = floor_hall
KITEM:  | = nothing
: else
KMONS:  8 = 8 band
KMONS:  9 = 9 band
KMONS:  0 = 0 band
KITEM:  O = superb_item
KITEM:  9 = any good_item
: end
MAP
        xxxxx@xxxxx
      xxx.........xxx
    xxx......0......xxx
   xx..0..xxxxxxx..0..xx
  xx....xxxxxxxxxxx....xx
  x..9.xxxxxxxxxxxxx.9..x
 xx.....xxxxxxxxxxx.....xx
 x.0.x...xxxxxxxxx...x.0.x
xx..xxx...xxxxxxx...xxx..xx
x...xxxx...xxxxx...xxxx...x
x..xxxxxx...ccc...xxxxxx..x
x..xxxxxxx.++c++.xxxxxxx..x
x..xxxxxxxc+|8|+cxxxxxxx..x
@.0xxxxxxxcc8O8ccxxxxxxx0.@
x..xxxxxxxc+|8|+cxxxxxxx..x
x..xxxxxxx.++c++.xxxxxxx..x
x..xxxxxx...ccc...xxxxxx..x
x...xxxx...xxxxx...xxxx...x
xx..xxx...xxxxxxx...xxx..xx
 x.0.x...xxxxxxxxx...x.0.x
 xx.....xxxxxxxxxxx.....xx
  x..9.xxxxxxxxxxxxx.9..x
  xx....xxxxxxxxxxx....xx
   xx..0..xxxxxxx..0..xx
    xxx......0......xxx
      xxx.........xxx
        xxxxx@xxxxx
ENDMAP

###############################################################################
# Megastairs vaults.
# "Mega" is only really in opposition to the ministair vaults (cf.
# mini_monsters.des).
# Intended to be generic and placeable in a large variety of locales.

NAME: grunt_megastairs_1
: grunt_megastairs_setup(_G)
MAP
          xxxxx
         xx.G.xx
         x.....x
         xG.{.Gx
         x.....x
         xx...xx
 xxxxxxx xxx+xxx xxxxxxx
xx..0..xxx.....xxx..0..xx
x.xxxxx.x.x.0.x.x.xxxxx.x
@.......x.x...x.x.......@
x...G.0.+.x.G.x.+.0.G...x
@...... x.x...x.x.......@
x.xxxxx.x.x.0.x.x.xxxxx.x
xx..0..xxx.....xxx..0..xx
 xxxxxxx xxx+xxx xxxxxxx
         xx...xx
         x.....x
         xG.[.Gx
         x.....x
         xx.G.xx
          xxxxx
ENDMAP

NAME: grunt_megastairs_2
: grunt_megastairs_setup(_G)
MAP
       xxxxx
      xx.G.xx
      x.....x
      xG.{.Gx
      x.....x
      xx...xx
      xxx+xxx
    xxx.....xxx
  xxx....0....xxx
 xx....xxxxx....xx
xx..x0xx...xx0x..xx
x..xx.........xx..x
x..x...G...G...x..x
@.0x0....[....0x0.@
x..x...G...G...x..x
x..xx.........xx..x
xx..x0xx...xx0x..xx
 xx....xxxxx....xx
  xxx....0....xxx
    xxx.....xxx
      xxx@xxx
ENDMAP

NAME: grunt_megastairs_3
: grunt_megastairs_setup(_G)
MAP
        xxxxx
       xx.G.xx
       x.....x
       xG.{.Gx
       x.....x
       xx...xx
       xxx+xxx
      xx.....xx
     xx...G...xx
    xx.........xx
   xx..G..[..G..xx
  xx.............xx
 xx..0....G....0..xx
 x.....x..0..x.....x
xx..G..x.....x..G..xx
x.....xx..G..xx.....x
x..0..xx.....xx.....x
x.....xx..0..xx..0..x
xx@x@xxx.....xxx@x@xx
       xx@x@xx
ENDMAP

NAME: grunt_megastairs_4
: grunt_megastairs_setup(_G)
MAP
xxxxx            xx@xx
x...xxxx         x...x
x.{....xxx      xx.G.@
x........xx     x...xx
xxxxx..0..xxxxxxx...x
    xxx..........0.xx
      xxx..0.G.0..xx
        xx.......xx
         x.G.0.G.x
        xx.......xx
       xx..0.G.0..xxx
      xx.0..........xxx
      x...xxxxxxx..0..xxxxx
     xx...x     xx........x
     @.G.xx      xxx....[.x
     x...x         xxxx...x
     xx@xx            xxxxx
ENDMAP

NAME: grunt_megastairs_5
: grunt_megastairs_setup(_G)
MAP
        xxxxx
      xxx.[.xxx
     xx.......xx
    xx.0..G..0.xx
   xx...xxxxx...xx
  xx.0.xx   xx.0.xx
  x...xxxxxxxxx...x
xxx...xxx...xxx...xxx
@...................@
xG..0..G..{..G..0..Gx
@...................@
xxx...xxx...xxx...xxx
  x...xxxxxxxxx...x
  xx.0.xx   xx.0.xx
   xx...xxxxx...xx
    xx.0..G..0.xx
     xx.......xx
      xxx.(.xxx
        xxxxx
ENDMAP
